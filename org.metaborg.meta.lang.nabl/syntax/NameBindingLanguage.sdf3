module NameBindingLanguage

imports
  
  common/Layout
  common/Identifiers
  terms/Signatures
  terms/Vars
  Modules

context-free start-symbols

  Start

context-free syntax
  
  Start = Module
  
templates // namespaces

  ModuleSection.Namespaces  = <
  namespaces   
    <NamespaceDef*>
  > 
  
  NamespaceDef.NamespaceDef = 
    <<NamespaceID>
    >
  
  NamespaceRef.NamespaceRef = <<NamespaceID>>

  RestrictedNamespaceRef.Restricted = <<Restriction*> <NamespaceRef>>
  
  Restriction.Imported = <imported>
  Restriction.Multiple = <multiple>
  
lexical syntax

  NamespaceID = Id
                
templates // properties

  ModuleSection.Properties = <
  properties
    <PropertyDef*>
  >  
    
  PropertyDef.PropertyDef = <
  <PropertyID> of <NamespaceRef+; separator=", "> : <Sort>>
  
  PropertyDef.QualityDef = <
  <PropertyID> of <NamespaceRef+; separator=", ">>
  
  PropertyRef.TypeProp    = <type>  
  PropertyRef.PropertyRef = <<PropertyID>>

  QualityRef.QualityRef = <<PropertyID>>

  PropertyTerm.PropertyTerm       = <of <PropertyRef> <Term>>
  PropertyTerm.QualityTerm        = <of quality <QualityRef>>
  PropertyPattern.PropertyPattern = <of <PropFilter> <PropertyRef> <Term>>
  PropertyPattern.QualityPattern  = <of quality <QualityRef>>

  PropFilter.Equal      = <>
  PropFilter.Conformant = <conformant>
  
lexical syntax

  PropertyID = Id
  PropertyID = "type"    {reject}
  PropertyID = "quality" {reject}
  
templates // binding rules
    
  ModuleSection.BindingRules = <
  binding rules
    <BindingRule*>
  >
  
  BindingRule.BindingRule = <
  <Pattern> <Constraints> : <BindingClause+>> 

templates // clauses

  BindingClause.DefClause = <
    <DefKind> defines <Unique> <NamespaceRef> <Term> <PropertyTerm*> <InDefScopes> <Constraints>
  > 
  
  DefKind.Explicit = <>
  DefKind.Implicit = <implicitly>
  
  Unique.Unique    = <>
  Unique.Unique    = <unique>
  Unique.NonUnique = <non-unique>
    
  BindingClause.ScopeClause = <
    scopes <NamespaceRef+; separator=", ">
  >
   
  BindingClause.NonTransitiveScopeClause = <
    non-transitively scopes <NamespaceRef+; separator=", ">
  > 
    
  BindingClause.RefClause     = <
    <RefClausePart+; separator="otherwise">
  >
    
  RefClausePart.RefClausePart = <
  refers to <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope> <Constraints>>
  
  BindingClause.ImportClause = <
    <ImportClausePart+; separator="otherwise\n">
  > 
  
  ImportClausePart.SingleImport = <
  imports <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <FromRefScope> <Alias> <IntoDefScopes> <Constraints>>             

  ImportClausePart.WildcardImport = <
  imports <RestrictedNamespaceRef+; separator=", "> <PropertyPattern*> <FromRefScope> <IntoDefScopes> <Constraints>> 
  
  Alias.None  = <>
  Alias.Alias = <as <Term>>

  BindingClause.FilterClause = <
    filters <NamespaceRef> <Term> 
      <Filters>
      <Constraints>
  > 

  BindingClause.DisambiguateClause = <
    disambiguates <NamespaceRef> <Term>
      by <Disambiguator> 
      <Constraints>
   >
  
  Disambiguator.MinimalDistance = <
    minimal distance <Term> <Relation> <Term>
  >
  
lexical syntax

  Relation = "<:"
  
templates // scopes

  InDefScopes.Current   = <>
  InDefScopes           = <in <DefScopes>>   {bracket}
  
  IntoDefScopes.Current = <>
  IntoDefScopes         = <into <DefScopes>> {bracket}

  DefScopes.Current     = <current scope>
  DefScopes.DefScopes   = <<DefScope+; separator=", ">> 
  DefScope.Subsequent   = <subsequent scope>
  DefScope.DefScope     = <<Term>>
 
  InRefScope.Current    = <>
  InRefScope            = <in <RefScope>>    {bracket}
  
  FromRefScope.Current   = <>
  FromRefScope           = <from <RefScope>>  {bracket}
  
  RefScope.Current    = <current scope>
  RefScope.Enclosing  = <enclosing <NamespaceRef>>
  RefScope.Context    = <<Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope>>
  RefScope.RefScope   = <<Term>>
  
  Disambiguator.All   = <>
  Disambiguator.Best  = <best>
  
templates // constraints

  Constraints = <> {ast("[]")}
  Constraints = <
    where
      <Constraint+; separator="\n and">
  > {bracket}
  
  Conditions = <> {ast("[]")}
  Conditions = <
    when
      <Constraint+; separator="\n and">
  > {bracket}
  
  Filters.Filters = <
    with 
      <Constraint+; separator="\n and">
  >
  
  Constraint.PropertyConstraint = <
    <Term> has <PropertyRef> <Pattern>>
  
  Constraint.QualityConstraint = <
    <Term> is <QualityRef>>
  
  Constraint.RefPropertyConstraint = <
    <NamespaceRef> <Term> has <PropertyRef> <Pattern>> {avoid}
   
  Constraint.RefQualityConstraint = <
    <NamespaceRef> <Term> is <QualityRef>> {avoid}
   
  Constraint.ReferenceConstraint = <
    <Term> refers to <NamespaceRef> <Pattern> <PropertyPattern*> <InRefScope>> 
  
context-free syntax

  Id = "into" {reject}

templates // messages

  BindingClause.MessageClause = <
  <MessageKind> <Term> on <Term> 
  <Conditions> 
  >
  
  MessageKind.Note    = <note>
  MessageKind.Warning = <warning>
  MessageKind.Error   = <error>
  
template options

  keyword -/- [a-zA-z]
    
 
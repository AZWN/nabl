module NaBLCore

imports
  
  common/Layout
  common/Identifiers
  terms/Signatures
  terms/Vars
  core/Modules

context-free start-symbols

  Start

context-free syntax
  
  Start = Module
  
templates // properties

  ModuleSection.Properties = <
  properties
    <PropertyDef*>
  >  
    
  PropertyDef.PropertyDef = <
  <PropertyID> of <NamespaceRef+; separator=", "> : <Sort>>
  
  PropertyRef.TypeProp    = <type>  
  PropertyRef.PropertyRef = <<PropertyID>>

  PropertyTerm.PropertyTerm       = <of <PropertyRef> <Term>>
  PropertyPattern.PropertyPattern = <of <PropFilter> <PropertyRef> <Term>>

  PropFilter.Equal      = <>
  PropFilter.Conformant = <conformant>
  
lexical syntax

  PropertyID = Id
  PropertyID = "type"    {reject}
  
templates // binding rules
    
  ModuleSection.BindingRules = <
  binding rules
    <BindingRule*>
  >
  
  BindingRule.BindingRule = <
  <Pattern> <Constraints> : <BindingClause>> 

templates // clauses

  BindingClause.DefClause = <
    <DefKind> defines <Unique> <NamespaceRef> <Term> in <DefScopes>
  > 
  
  DefKind.Explicit = <>
  DefKind.Implicit = <implicitly>
  
  Unique.Unique    = <unique>
  Unique.NonUnique = <non-unique>
    
  BindingClause.ScopeClause = <
    scopes <NamespaceRef>
  >
   
  BindingClause.NonTransitiveScopeClause = <
    non-transitively scopes <NamespaceRef; separator=", ">
  > 
    
  BindingClause.RefClause     = <
    <RefClausePart+; separator="otherwise">
  >
    
  RefClausePart.RefClausePart = <
  refers to <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope>>
  
  BindingClause.ImportClause = <
    <ImportClausePart+; separator="otherwise\n">
  > 
  
  ImportClausePart.SingleImport = <
  imports <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <FromRefScope> <Alias> <IntoDefScopes> <Constraints>>             

  ImportClausePart.WildcardImport = <
  imports <RestrictedNamespaceRef+; separator=", "> <PropertyPattern*> <FromRefScope> <IntoDefScopes> <Constraints>> 
  
  Alias.None  = <>
  Alias.Alias = <as <Term>>

templates // scopes

  IntoDefScopes         = <into <DefScopes>> {bracket}

  DefScopes.Current     = <current scope>
  DefScopes.DefScopes   = <<DefScope+; separator=", ">> 
  DefScope.Subsequent   = <subsequent scope>
  DefScope.DefScope     = <<Term>>
 
  InRefScope.Current    = <>
  InRefScope            = <in <RefScope>>    {bracket}
  
  FromRefScope.Current   = <>
  FromRefScope           = <from <RefScope>>  {bracket}
  
  RefScope.Current    = <current scope>
  RefScope.Enclosing  = <enclosing <NamespaceRef>>
  RefScope.Context    = <<Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope>>
  RefScope.RefScope   = <<Term>>
  
  Disambiguator.All   = <>
  Disambiguator.Best  = <best>
  
templates // constraints

  Constraints = <
    where
      <Constraint+; separator="\n and">
  > {bracket}
  
  Conditions = <
    when
      <Constraint+; separator="\n and">
  > {bracket}

  Constraint.True = <true>
    
  Constraint.PropertyConstraint = <
    <Term> has <PropertyRef> <Pattern>>
  
  Constraint.RefPropertyConstraint = <
    <NamespaceRef> <Term> has <PropertyRef> <Pattern>> {avoid}
   
  Constraint.ReferenceConstraint = <
    <Term> refers to <NamespaceRef> <Pattern> <PropertyPattern*> <InRefScope>> 
  
context-free syntax

  Id = "into" {reject}

templates // messages

  BindingClause.MessageClause = <
  <MessageKind> <Term> on <Term> 
  <Conditions> 
  >
  
  MessageKind.Note    = <note>
  MessageKind.Warning = <warning>
  MessageKind.Error   = <error>
  
template options

  keyword -/- [a-zA-z]
    
 
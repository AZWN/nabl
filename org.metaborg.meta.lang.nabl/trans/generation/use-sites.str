module generation/use-sites

imports
  
  libstrc
  include/NameBindingLanguage
  
  generation/util
  generation/sections
  generation/scoping-sites
  generation/prop-sites
  generation/constraints
  
rules
  
  use-to-name = 
    ?RefClause(<fetch-elem(use-to-name)>)
  + ?ImportClause(<fetch-elem(use-to-name)>)
  + ?RefClausePart(_, _, <id>, _, _, _)
  + ?SingleImport(_, _, <id>, _, _, _, _, _)
  + ?WildcardImport(_, _, Context(_, _, <id>, _, _), _, _)
    
  binding-to-use-rule(|bound*, task*, repl*):
    (i, BindingRule(pattern, constr*, clause*)) -> USE_RULE(pattern, seq)
    where 
      use-clause* := <filter-with-index(to-use-clauses(|pattern, bound*, repl*, i)); concat; nonempty> clause*
    ; seq         := <to-seq> [task*, use-clause*]
      
  to-use-clauses(|pattern, bound*, repl*, i):
    (j, clause) -> [clause*, cong]
    where
      RefClause(part*)    := clause
    + ImportClause(part*) := clause
    where
      k        := <extend-index(|i)> j
    ; (clause*, candidate*) 
               := <filter-with-index(to-use-candidate(|pattern, bound*, repl*, k)); unzip; (concat, id)> part*
    ; term     := <use-to-name> clause
    ; use-call := USE_CALL(candidate*)
    ; cong     := <replace-all-id(|[(term, use-call)])> pattern
  
  to-use-candidate(|pattern, bound*, glob-repl*, i):
    (j, part) -> (task*, CANDIDATE(ns-term, prop-term*, ctx-term, dep*))
    where
      RefClausePart(disamb, ns, term, prop*, ctx, constr*)                    := part
    + SingleImport(disamb, ns, term, prop*, ctx, alias, _, constr*)           := part
    + WildcardImport(_, _, Context(disamb, ns, term, prop*, ctx), _, constr*) := part
    where
      ns-term        := <ns-to-term> ns
    ; k              := <extend-index(|i)> j
    ; (task*, repl*) := <constraints-to-tasks(|bound*, k, 1)> (constr*, glob-repl*)
    ; prop-term*     := <map(prop-pattern-to-str(|[]); replace-all(|repl*))> prop*
    ; ctx-term       := <scope-to-term; replace-all(|repl*)> ctx
    ; dep*           := <map(Snd)> repl*
       
  to-match-rules(|bound*, i):
    (j, RefClause(part*)) -> rule*
    where
      k      := <extend-index(|i)> j
    ; rule*  := <map-with-index(to-match-rules(|bound*, k)); concat> part*
  
  to-match-rules(|bound*, i):
    (j, RefClausePart(disamb, ns, term, prop*, scope, constr*)) -> rule*
    where
      k     := <extend-index(|i)> j
    ; rule* := <constraints-to-match-rules(|bound*, k, 1)> constr*

	prop-pattern-to-str(|dependencies): PropertyPattern(type, p, t) -> PROPERTY(<nabl-to-str> p, t, dependencies) 

overlays
     
  USE_RULE(pattern, body) =
  SDefT(
    "nabl-use-site"
  , []
  , [ DefaultVarDec("lang__")
    , DefaultVarDec("partition__")
    , DefaultVarDec("uris__")
    , DefaultVarDec("states__") 
    ]
  , Seq(Match(pattern), body)
  )
  
  USE_CALL(candidates) =
  CallT(
    SVar("nabl-use")
  , []
  , [ Var("lang__")
    , Var("partition__")
    , Var("uris__")
    , LIST(candidates)
    ] 
  )
  
  CANDIDATE(ns, ps, ctx, deps) =
  TERM("UseCandidate", [ns, LIST(ps), ctx, LIST(deps)])
  
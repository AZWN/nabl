module test1 

language NamingExperiment

setup Header [[
  module M
    
  entity A {
    x : B
  }
]]

test Defs [[
  entity B {
    
  }
]] build generate-defs to 
   [Def(_), _, _, _]

test Unknown type Q [[
  entity B {
    q : Q
  }
]] 1 error

test Resolve property access [[
  entity B {
    f(c : C) {
      var v : C = c.[[c]]
    }
  }
  entity C {
    [[c]] : C
  }
]] resolve #1 to #2

test Resolve local variable [[
  entity B {
    f() {
      var [[a]] : A
      var b : B = [[a]]
    }
  }
]] resolve #2 to #1

test Resolve local property access [[
  entity B {
    [[x]] : A
    f() {
      var v : A = [[x]]
    }
  }
]] resolve #2 to #1
// TODO: occurrence highlighting, ref resolv, lang-specific identifiers?

test Resolve parameter access [[
  entity B {
    x : A
    f([[x]] : A) {
      var v : A = [[x]]
    }
  }
]] resolve #2 to #1

test Complete property access [[
  entity B {
    yyyyy : A
    f(a : A) {
      var v : A = a.x.[[y]]
    }
  }
]] complete to "yyyyy"

test Resolve property access fails [[
  entity B {
    y : A
    f(a : A) {
      var v : A = a.x.y.x.x
    }
  }
]] 1 error

test Resolve function call [[
  entity B {
    f(a : A) {
      var v : A = f(a)
    }
  }
]]

test Resolve property access fails [[
  entity B {
    b : B
    f(a : A) {
      var v : A = f(b) 
    }
  }
]] 1 error

test Def before use allowed [[
  entity B {
    c : C
  }
  entity C {}
]]

test Def before use error [[
  entity B {
    f() {
      var v : A = w
      var w : A
    }
  }
]] 1 error

test Shadowing of property (allowed) [[
  entity B {
    a : A
    f() {
      var a : A
    }
  }
]]

test Shadowing of var (not allowed) [[
  entity B {
    f() {
      var a : A
      { 
        var a : A
      }
    }
  }
]] 1 error

test Duplicate entity [[
  entity B {}
  entity B {}
]] 1 error


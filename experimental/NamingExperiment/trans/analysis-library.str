module analysis-library

imports
  libstratego-lib
  lib/editor-common.generated

signature constructors

  // Index elements:
  Def          : List(UriPart) -> Summary
  Use          : List(UriPart) * List(UriPart) -> Summary
  BadUse       : List(UriPart) -> Summary
  DefData      : List(UriPart) * DefDataType * Term -> Summary
  Read         : List(UriPart) -> Summary
  ReadWildcard : List(UriPart) * String -> Summary
  
  // URI header:
  Namespace      : UriPart
  Unresolved     : Namespace -> UriPart
  INTERNAL_ERROR : UriPart
  
  // Remainder of URI:
  String : UriPart
  Anon   : Int -> UriPart
  Anon   : UriPart

rules // extension points
  
  // Should return list of Def(_) and/or [namespace | path]
  adjust-index-lookup(is-use |namespace, path, prefix) = fail
  
  // adjust-index-select(|namespace, path, use) = fail // (e.g., for imports)
  
  // Should call <store-results> on a (list of) DefData 
  adjust-index-def-data(store-results |namespace, path) = fail
  
  // Should return a path
  adjust-index-path(is-def |namespace, path) = fail
  
  // adjust-index-path-from-filesystem(|project-path, path)

rules // analysis traversals
   
  analyze-top:
    (ast, path, project-path) -> (ast5, defs, uses, data', added, removed)
    with
      // Init
      full-path := $[[project-path]/[path]];
      prim("LANG_index_setup", "NamingExperiment", [project-path])
    with 
      // Store copy of defs for diff and clear file
      olddefs := <filter(?Def(_))> <index-get-all-in-file> full-path;
      prim("LANG_index_clear_file", full-path)
    with
      {| ReadSet:
        readSet := <new-iset>;
        rules(ReadSet: _ -> readSet);
      
        // Add Unresolved annotations, record globals 
        (Some(ast2), defs) := <analyze-defs(|Anon(), Anon())> Some(ast); // HACK: force origin tracking with Some()... // TODO: still needed?
        <index-add-all(|full-path)> defs;
        
        // Find DefData
        ast3 := <prim("SSL_EXT_clone_and_set_parents", <id>)> ast2;
        data := <origin-track-forced(analyze-tree-data)> ast3;
        <index-add-all(|full-path)> data;
        
        // Resolve an references in DefData (using what we just stored)
        (data', data-uses) := <analyze-uses> data;
        <index-remove-all(|full-path)> data;
        <index-add-all(|full-path)> data';
        
        // Resolve all unresolved references in the tree
        (ast4, uses) := <analyze-uses> ast3;
        <index-add-all(|full-path)> uses;
        ast5         := <prim("SSL_EXT_clone_and_set_parents", <id>)> ast4;
        
        // Store reads
        if not(<is-test-input> (ast, path)) then
          <index-add-all(|full-path)> <iset-elements> readSet
        end
      |}
    with
      // Find removed and added definitions
      (added, removed) := <analyze-diff> (olddefs, defs)
    with
      // Schedule re-analysis of files that have a use of a removed def
      <map(debug(!"Re-analyzing "); prim("SSL_EXT_queue_analysis"))> <analyze-changed(|full-path)> removed;
      // Schedule re-analysis of files that have a read of an added def
      <map(debug(!"Re-analyzing "); prim("SSL_EXT_queue_analysis"))> <analyze-changed(|full-path)> added
      
  analyze-diff: 
    (defs1, defs2) -> (added, removed)
    with
      added   := <diff(analyze-diff-eq)> (defs2, defs1);
      removed := <diff(analyze-diff-eq)> (defs1, defs2)
  
  // Compensate for changing anonymous namespace names.
  analyze-diff-eq = ?(Def(u1), Def(u2)); (<eq> (u1, u2) <+ <eq> (<remove-all(?Anon(_))> u1, <remove-all(?Anon(_))> u2))
  
  // Tests if the current file is just a testing language input
  is-test-input:
    (ast, path) -> (ast, path)
    where
      <string-ends-with(|".spt")> path;
      not(prim("SSL_EXT_origin_language", ast) => "Spoofax-Testing")
  
  analyze-changed(|currentFile):
    defs -> filenames
    with
      reads     := <mapconcat(index-get-reads-all)> defs;
      filenames := <make-set> <remove-all(?currentFile)> <mapconcat(index-get-files-of)> reads
  
  /**
   * Identifies all definitions in the tree
   * and annotates them with their URI.
   * Also annotates uses with a preliminary "Unresolved(_)" URI.
   */
  analyze-defs(|head-scope, head-scope-ns):
    ast -> (ast', defs')
    with
      if def := <nam-get-definition> ast then
        Def(def-path)                     := def;
        [head-scope-ns', head-scope' | _] := def-path
      else
        def-path       := INTERNAL_ERROR();
        head-scope-ns' := head-scope-ns;
        head-scope'    := head-scope
      end;
      if scope-types := <nam-get-scope-types> ast then
        {| IndexPath:
          <list-loop(update-index-path(|head-scope', head-scope-ns', ast))> scope-types;
          // <balanced-update-path> head-scope';
          (ast', defs) := <analyze-defs-recurse(|Anon(), Anon(), def-path)> ast
        |}
      else
        (ast', defs) := <analyze-defs-recurse(|head-scope', head-scope-ns', def-path)> ast
      end;
      defs' := <![def | defs] <+ !defs>
  
  analyze-defs-recurse(|head-scope, head-scope-ns, def-path):
    ast -> (ast'', defs)
    where
      analyzed      := <all(analyze-defs(|head-scope, head-scope-ns))> ast;
      (ast', defs)  := <unzip-analyzed> analyzed;
      ast''         := <try(nam-annotate-names(|def-path))> ast'
  
  update-index-path(|head-scope, head-scope-ns, ast):
    scope-type -> scope-type
    where
      if !head-scope-ns => Anon() then
        path  := <IndexPath <+ ![]> scope-type;
        path' := <do-adjusted-index-path(|scope-type, path, Anon(<new>))> ast
      else
        path  := <IndexPath <+ ![]> head-scope-ns;
        path' := <do-adjusted-index-path(|scope-type, path, head-scope)> ast
      end;
      rules(IndexPath: scope-type -> path')
  
  /* TODO: consider using simple-update-def-path
   *       which uses "balanced" path scopes
   *       e.g. when Entity doesn't scope Function
   *       then it's hard to access properties from a function
  balanced-update-index-path:
    head-scope -> head-scope
    where
      if !head-scope => Anon() then
        head-scope' := Anon(<new>)
      else
        head-scope' := head-scope
      end;
      (something with do-adjust-path)
      rules(IndexPath := [head-scope' | <IndexPath <+ ![]> ()])
  */
  
  /**
   * Analyze all uses, changing their preliminary
   * "Unresolve(_)" URI to a definite URI of their definition. 
   */
  analyze-uses = analyze-uses(|None())
  analyze-uses(|parent):
    ast -> (ast'', uses')
    with
      analyzed     := <all(analyze-uses(|ast))> ast;
      (ast', uses) := <unzip-analyzed> analyzed;
      if !ast' => _{unresolved@[Unresolved(namespace), x | path]} then
        if Def(def-uri) := <index-lookup(id |namespace, path, "")> ast' then // TODO: Change ""
          ast'' := ast{def-uri};
          if key{keyUri} := <nam-get-definition-key> parent ; not(<eq>(key, ast')) then
            uses' := [Use(def-uri, keyUri) | uses]
          else
            uses' := [Use(def-uri, [namespace | path]) | uses]
          end
        else
          ast'' := ast';
          uses' := [BadUse([namespace, x]) | uses]
        end
      else
        ast'' := ast';
        uses' := uses
      end
  
  /**
   * Collects all index data (e.g., types of definitions).
   */
  analyze-tree-data:
    tree -> data
    where
      set := <new-iset>;
      <topdown(analyze-tree-data-part(|set))> tree;
      data := <iset-elements> set
  
  analyze-tree-data-part(|set):
    tree -> tree
    where
      if def-term := <nam-get-definition-key> then
        _{[namespace | path]} := def-term;
        if result := <adjust-index-def-data(store-index-data-results(|set) |namespace, path)> tree then
          <fatal-err(|"Unexpected result from adjust-index-def-data; should call <store-results>")> result
        end
      end
  
  store-index-data-results(|set):
    t -> <fail>
    where
      if is-list then
        <iset-addlist(|t)> set
      else
        <iset-add(|t)> set
      end
  
  /**
   * Transforms a term C( (a1, [b1]), (a2, [b2, b3]) )
   * to a tuple (C(a1, a2), [b1, b2, b3]).
   */
  unzip-analyzed:
    appl -> (appl', unzipped-parts)
    with
      appl'          := <all(\(a, _) -> a\)> appl;
      unzipped-parts := <concat> <get-appl-arguments(\(_, b) -> b\) <+ map(\(_, b) -> b\) <+ ![]> appl

rules // index construction
    
  index-add-all(|file) =
    list-loop(with(prim("LANG_index_add", <id>, file)))
    
  index-remove-all(|file) =
    list-loop(with(prim("LANG_index_remove", <id>, file)))
    
  index-clear = prim("LANG_index_clear_all")

rules // index API primitives
  
  index-get-data(|kind):
    <with(?Def(uri) | "Def expected")> -> data
    where
      <index-get-all> DefData(uri, kind, []) => [DefData(_, _, data) | _]
  
  index-get-data-all(|kind):
    <with(?Def(uri) | "Def expected")> -> datas'
    where
      datas  := <index-get-all> DefData(uri, kind, []);
      datas' := <map(\DefData(_, _, d) -> d\)> datas
      
  index-get-uses-all:
    <with(?Def(uri) | "Def expected")> -> uses
    where
      uses := <index-get-all> Use(uri, [])
      
  index-get-reads-all:
    template -> <conc> (reads, readwildcards')
    where
      uri   := <index-uri> template;
      reads := <index-get-all> Read(uri);
      if !uri => [namespace, prefix | path-parent] then
        readwildcards  := <index-get-all> ReadWildcard([namespace | path-parent], ());
        readwildcards' := <filter(index-is-name-substring(|prefix))> readwildcards
      else
        readwildcards' := []
      end
      
 
  index-get-all-in-file:
    template -> <prim("LANG_index_get_all_in_file", template)>
  
  index-get-all-files =
    prim("LANG_index_all_filenames")
  
  index-get-files-of:
    template -> <prim("LANG_index_get_file_of", template)>
  
  /**
   * Get all index entries that match the given template.
   * Example:
   *   <index-get-all> Def([Entity(), "Bar"]) => [Def([Entity(), "Bar"])]
   */
  index-get-all:
    template -> <prim("LANG_index_get", template)>
      with
       if set := <ReadSet> then
         uri := <index-uri>;
         <iset-add(|Read(uri))> set
       end
  
  /**
   * Get the first index entry that matches the given template, or fail.
   * Example:
   *   <index-get-all> Def([Entity(), "Bar"]) => Def([Entity(), "Bar"])
   */
  index-get:
    template -> <Fst> <index-get-all> template
  
  index-uri:
    Def(uri) -> uri
  
  index-uri:
    Use(uri, _) -> uri
  
  index-uri:
    DefData(uri, _, _) -> uri
  
  index-uri:
    Read(uri) -> uri
  
  index-uri:
    ReadWildcard(uri, _) -> uri
    
  index-uri:
    x{[namespace | path]} -> [<index-namespace-unwrap> namespace | path]
    
  index-namespace:
    x{[namespace | path]} -> <index-namespace-unwrap> namespace

  index-path:
    x{[namespace | path]} -> path'
    where
      if !namespace => Unresolved(namespace) then
        Def(path') := <index-lookup> 
      else
        path' := path
      end
  
  /**
   * Determines if a given AST node is a definition site,
   * according to the syntax.
   */
  index-is-definition =
    where(nam-get-definition-key)
  
  /**
   * Returns all children of a Def.
   *
   * @param namespace Only child Defs with this namespace are returned.
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   *
   */
  index-get-children(|namespace, prefix):
    <with(?Def([parent-ns | path]) | "Def expected")> -> children'
    with
      with(not(!namespace => Def(_)) | "index-get-children interface changed");
      prefix'   := <strip-annos> prefix;
      template  := Def([namespace | path]);
      children  := <prim("LANG_index_get_children", template)>;
      children' := <filter(index-is-name-substring(|prefix'))> children;
      store     := [namespace, prefix' | path];
      // Store read in index.
      if set := <ReadSet> then
        <iset-add(|ReadWildcard([namespace | path], prefix'))> set
      end
      
rules // index lookup rules (take into account adjust-index-lookup)

  /**
   * Given an annotated AST node, resolves it, returning its Def.
   */
  index-lookup:
    x{[namespace | path]} -> <index-lookup(id |<index-namespace-unwrap> namespace, path, "")>

  /**
   * Given an annotated AST node, resolves it, returning all its Defs.
   */
  index-lookup-all:
    x{[namespace | path]} -> <index-lookup-all(id |<index-namespace-unwrap> namespace, path, "")>

  /**
   * Given an annotated AST node, resolves it, returning all its Defs.
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   *
   * @internal
   */
  index-lookup(is-adjust-lookup-enabled |namespace, path, prefix):
    x -> def
    where
      candidates := <index-lookup-one-level(is-adjust-lookup-enabled|namespace, path, prefix)>;
      def        := <index-select(|namespace, path, x)>
    <+
      // TODO: optimize: try not to call do-adjust-index-lookup from here
      [_ | path'] := path;
      def         := <index-lookup(is-adjust-lookup-enabled |namespace, path', prefix)> x

  /**
   * Given an annotated AST node, resolves it, returning all its Defs.
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   *
   * @internal
   */
  index-lookup-all(is-adjust-lookup-enabled |namespace, path, prefix):
    x -> defs'
    where
      candidates := <index-lookup-one-level(is-adjust-lookup-enabled|namespace, path, prefix)>;
      defs       := <index-select-all(|namespace, path, x)>;
      // TODO: optimize: try not to call do-adjust-index-lookup from here
      if [_ | path'] := path then
        defs2 := <index-lookup-all(is-adjust-lookup-enabled |namespace, path', prefix)> x;
        defs' := <conc> (defs, defs2)
      else
        defs' := defs
      end

  /**
   * Given an annotated AST node, returns the outermost Def with a corresponding URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-outermost(|prefix):
    x{[namespace | path]} -> <index-lookup-outermost(id |<index-namespace-unwrap> namespace, path, prefix)>

  /**
   * Given an annotated AST node, returns the outermost Def with a corresponding URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   *
   * @internal
   */
  index-lookup-outermost(is-adjust-lookup-enabled |namespace, path, prefix):
    x -> def
    where
      // TODO: optimize: just like index-lookup
      [_ | path'] := path;
      def         := <index-lookup-outermost(is-adjust-lookup-enabled |namespace, path', prefix)> x
    <+
      candidates := <index-lookup-one-level(is-adjust-lookup-enabled|namespace, path, prefix)>;
      def        := <index-select(|namespace, path, x)>

  /**
   * Given an annotated AST node, returns a Def that has the same parent URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-one-level(|prefix):
    x{[namespace | path]} -> <index-lookup-one-level(id|<index-namespace-unwrap> namespace, path, prefix)>
  
  /**
   * Given an annotated AST node, returns a Def that has the same parent URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-one-level(is-adjusted-lookup-enabled |namespace, path, prefix):
    x{_} -> defs
    with
      is-adjusted-lookup-enabled;
      do-adjust-index-lookup(|namespace, path, x, prefix);
      mapconcat(\d@Def(p) -> [d]\
          <+ \[namespace' | path'] -> <index-lookup-one-level(fail |namespace', path', prefix)> x\
          <+ fatal-err(|"Unexpected result from adjust-index-lookup, should be a Def(_) or [namespace | path]"));
      ?defs
    <+
      defs := <index-get-children(|namespace, prefix)> Def([namespace | path])

  /**
   * Given an annotated AST node, returns a Def with a common ancestor URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-all-levels(|prefix):
    x{[namespace | path]} -> <index-lookup-all-levels(id|<index-namespace-unwrap> namespace, path, prefix)>
  
  /**
   * Given an annotated AST node, returns a Def with a common ancestor URI. 
   *
   * @param prefix    Only Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-all-levels(is-adjust-lookup-enabled |namespace, path, prefix):
    x{_} -> all-defs
    with
      is-adjust-lookup-enabled;
      do-adjust-index-lookup(|namespace, path, x, prefix);
      mapconcat(\d@Def(p) -> [d]\
          <+ \[namespace' | path'] -> <index-lookup-all-levels(fail |namespace', path', prefix)> x\
          <+ fatal-err(|"Unexpected result from adjust-index-lookup, should be a Def(_) or [namespace | path]"));
      ?all-defs
    <+
      one-level := <index-get-children(|namespace, prefix)> Def([namespace | path]);
      if [_ | path'] := path then
        all-defs := <concat> [one-level, <index-lookup-all-levels(fail |namespace, path', prefix)> x]
      else
        all-defs := one-level
      end

  /** @Deprecated */
  index-lookup-contained(|namespace, prefix) =
    index-lookup-children(|namespace, prefix)
    
  /** @Deprecated */
  index-lookup-contained-all-levels(|namespace, name) =
    index-lookup-descendants(|namespace, name)

  /**
   * Given an annotated AST node, resolves it,
   * and returns all child Defs of its definition.
   *
   * @param namespace Only child Defs with this namespace are returned.
   * @param prefix    Only child Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-children(|namespace, prefix):
    x{[ns | path]} -> defs
    with
      if !ns => Unresolved(_) then
        Def([_ | def-path]) := <index-lookup>;
        defs := <index-lookup-one-level(id|namespace, def-path, prefix)> x 
      else
        defs := <index-lookup-one-level(id|<index-namespace-unwrap> namespace, path, prefix)>
      end
    <+
      defs := []

  /**
   * Given an annotated AST node, resolves it,
   * and returns all descendant Defs of its definition.
   *
   * @param namespace Only child Defs with this namespace are returned.
   * @param prefix    Only child Defs with a name that starts with this
   *                  string are returned.
   */
  index-lookup-descendants(|namespace, name):
    x{[ns | path]} -> defs
    with
      if !ns => Unresolved(_) then
        Def([_ | def-path]) := <index-lookup>;
        defs := <index-lookup-all-levels(id|namespace, def-path, name)> x
      else
        defs := <index-lookup-all-levels(id|<index-namespace-unwrap> namespace, path, name)>
      end
    <+
      defs := []

/** @internal */
rules // Internal helpers
       
  index-is-name-substring(|name):
    template -> <id>
    with
      [_, uri-name | _] := <index-uri>
    where
      <is-substring(!name)> uri-name

  index-namespace-unwrap =
    \Unresolved(n) -> n\ <+ id
  
  do-adjust-index-lookup(|namespace, path, use, prefix) =
    // UNDONE: try(origin-term);
    repeat-until(
      prim("SSL_EXT_get_parent", <id>)
    , adjust-index-lookup(origin-equal(|use) |namespace, path, prefix)
    )

  index-select(|namespace, path, use) =
    getfirst(
      where(
        ?Def([_, <SRTS-EXT-eq-ignore-annos(|use)> | _])
      )
    )

  index-select-all(|namespace, path, use) =
    filter(
      where(
        ?Def([_, <SRTS-EXT-eq-ignore-annos(|use)> | _])
      )
    )
  
  do-adjusted-index-path(|namespace, path, def) =
    adjust-index-path(origin-equal(|def) |namespace, path)
  <+
    ![def | path]
  
  index-eq(|namespace, expected) =
    where(
      ?Def([_, name | _]);
      <SRTS-EXT-eq-ignore-annos(|expected)> name
    )
  
  external SRTS-EXT-eq-ignore-annos(|t)

rules // interface for generated code
  
  nam-get-def(|namespace):
    x -> Def([namespace, x | <IndexPath <+ ![]> namespace])
    
  nam-annotate-use(|namespace):
    t -> t{[Unresolved(namespace), t | <IndexPath <+ ![]> namespace]}
    
  nam-get-scope-types = fail
  nam-get-definition = fail
  nam-get-definition-key = fail
  nam-annotate-names(|def-path) = fail

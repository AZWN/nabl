module namingexperiment

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/NamingExperiment
  lib/editor-common.generated
  check
  generate
  prettyprint
  refactorings
  analysis-manual
  analysis-generated
  analysis-library

rules // Main editor interface (defined by editor/NamingExperiment-Builders and -References.esv)
  
  // Analyzes the current program, returning a tuple with errors, warnings, and notes;
  // each a list of (term, message) tuples or simply (message) terms.
  editor-analyze:
    (ast, path, project-path) -> (ast', errors, warnings, notes)
    with
      editor-init;
      (ast', defs, uses, data) := <analyze-top>;
      errors   := <collect-all(constraint-error, conc)> ast';
      warnings := <collect-all(constraint-warning, conc)> ast';
      notes    := <collect-all(constraint-note, conc)> ast'
  
  // Transforms a selection to Java
  generate-java:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"java")> path;
      result   := <to-java> selected
  
  // Prints the abstract syntax ATerm of a selection.
  generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path;
      result   := selected // we just return the input term
  
  // Prints the abstract syntax ATerm of a selection.
  generate-analyzed:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      editor-init;
      filename := <guarantee-extension(|"analyzed.aterm")> path;
      result   := <analyze-top> (selected, path, project-path) // we just return the input term
  
  // Resolves a reference when the user control-clicks or presses F3 in the editor.
  // Only succeeds for nodes of the form Type(_) for which GetEntity is defined.
  editor-resolve:
    (node, position, ast, path, project-path) -> target
    where
      // TODO: shouldn't work on parent node?
      !node => <one(?subnode) <+ ?subnode>;
      def     := <index-lookup> node;
      target  := /*<index-get-term>*/ def

  // Returns "hover help" information for a particular node in the editor.
  // For references, this rule is invoked using the resolved term.
  editor-hover:
    (target, position, ast, path, project-path) -> $[[def-path'']]
    where
      // FIXME: this only works for definition paths
      //<debug(!2)> target;
      def-term           := <nam-get-definition-key> target;
      _{[ns | def-path]} := def-term;
      def-path'  := <take-until(?Anon())> def-path;
      def-path'' := <reverse; separate-by(|".")> def-path'

  // Completes an identifier when the user presses control-space
  // (the completion identifier in the AST provides additional context information)
  editor-complete:
    (node, position, ast, path, project-path) -> proposals'
    where
      editor-init;
      (ast', _, _, _) := <analyze-top> (ast, path, project-path);
      x              := <collect-one(?COMPLETION(_))> ast';
      proposals      := <index-lookup-all-levels> x;
      proposals'     := <map(def-to-name)> proposals

  def-to-name:
    Def([namespace, name | _]) -> name

module lib/compilation-library

imports
  libstratego-lib
  lib/editor-common.generated
  lib/index-library
  lib/analysis-library
  
rules // Extension points
    
  // Should compile given analysed ast.
  index-compile-ast(|file, subfile, project-path) = fail
  
rules // Compilation
  
  index-schedule-compilation:
    <with(?(_, _, _, _, _) | "(selected, position, ast, path, project-path) tuple expected")> -> None()
    with
      queue-strategy(|"index-compilation", "Compiling! ⚔⚔⚔")
    
  index-compilation:
    (_, _, ast, path, project-path) -> <index-compilation(|path, project-path)> ast
  
  index-compilation(|path, project-path):
    ast -> None()
    with
      // Init
      full-path := $[[project-path]/[path]];
      language  := <index-origin-language> ast;
      index-setup(|language, [project-path], full-path)
    with
      // Determine the files to compile by looking at changed files.
      diffs         := <analyze-get-diffs>;
      files         := <map(index-compilation-restore-read-file)> diffs;
      filteredFiles := <make-set> <remove-all(index-compilation-filter-file)> files;
      
      // Clean compile time reads.
      <filter(index-compilation-clean-reads)> filteredFiles;
      
      // Compile the files
      <filter(index-compilation-file(|language, project-path))> filteredFiles;
      
      // Clean diffs
      analyze-clean-diff

  index-compilation-file(|language, project-path):
    (file, subfile) -> None()
    with
      // Parse and analyze ast.
      ast                                 := <parse-file> file;
      AnalysedResult(ast', _, _, _, _, _) := <analyze-top-internal(|Compile(), language, file)> (ast, file, project-path)
    with
      {| Index-ReadSet:
        readSet := <new-iset>;
        rules(Index-ReadSet: _ -> readSet);
        
        // Compile file
        <index-compile-ast(|file, subfile, project-path)> ast';
        
        // Store compile-time reads.
        reads := <iset-elements> readSet;
        <index-add-all(|(file, subfile))> reads;
        <index-add-all(|<index-compilation-file-tuple> (file, subfile))> reads
      |}

  index-compilation-filter-file = 
    ?(<id>, _); (is-test-file <+ index-is-fake-file <+ not(file-exists))

rules // Compile time reads

  index-compilation-restore-read-file:
    (file, subfile) -> (file', subfile)
    with
      file' := <string-replace(|<index-compilation-read-path>, "")> file
      
  index-compilation-clean-reads = 
    ?(file, subfile); index-compilation-file-tuple; index-clear-file
      
  index-compilation-file-tuple:
    (file, subfile) -> ($[[<index-compilation-read-path>]/[file]], subfile)
    
  index-compilation-read-path =
    !"/.internal/reads/compile"
    
module nabl2/lang/signatures/NameResolution

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/ScopeGraph
  nabl2/shared/common/Sorts
  nabl2/shared/common/Resolution

template options

  keyword -/- [a-zA-Z0-9\']

context-free syntax

  NaBL2-Signature.NameResolution = <
    name resolution
      <{NaBL2-NameResolutionParam "\n\n"}*>
  > {longest-match}

  NaBL2-NameResolutionParam.Labels        = <labels <{NaBL2-PathLabel " "}*>>
  NaBL2-NameResolutionParam.Order         = <order <{NaBL2-LabelOrder ",\n"}*>>
  NaBL2-NameResolutionParam.WF            = <well-formedness <NaBL2-LabelRE>>
  NaBL2-NameResolutionParam.DefaultELabel = <default edge label <NaBL2-PathLabel>>
  NaBL2-NameResolutionParam.DefaultNLabel = <default named label <NaBL2-PathLabel>>

context-free syntax

  NaBL2-Signature.Namespaces = <
    namespaces
      <{NaBL2-NamespaceDef "\n\n"}*>
  > {longest-match}

  NaBL2-NamespaceDef.NamespaceDef = <<NaBL2-Namespace> <NaBL2-NamespaceType> <NaBL2-NamespaceProperties>>

  NaBL2-NamespaceType.NoType  =
  NaBL2-NamespaceType.Type    = <: <NaBL2-SortRef>>

  NaBL2-NamespaceProperties = <{ <{NaBL2-NamespaceProperty ",\n"}*> }>
  NaBL2-NamespaceProperties = {ast("[]")}
  
  NaBL2-NamespaceProperty.PropertyDef = <<NaBL2-PropertyId> : <NaBL2-SortRef>>

module nabl2/shared/constraints/Base

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/Messages
  nabl2/shared/common/CTerms

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  Constraint.CTrue   = <true <MessagePosition>>
  Constraint.CFalse  = <false <Message>>

  Constraint         = <( <Constraint> )> {bracket}

  Constraint.CConj   = <
    <Constraint>,
    <Constraint>
  > {right}

  Constraint.CExists = <
    exists {<{CVar " "}+>}
      <Constraint>
  >

  Constraint.CNew = <new <{CVar " "}+> <MessagePosition>>

context-free priorities

  { 
    Constraint.CNew
    Constraint.CExists
  } > { right:
    Constraint.CConj
  }

lexical syntax

  ConstraintPriority = "!"*

lexical syntax

  Keyword = "true" | "false" | "exists" | "new"

module nabl2/runtime/analysis/base

imports

  runtime/editor/annotations
  runtime/editor/origins

  nabl2shared

rules // ast indices

  index-ast(|source) =
    ?ast;
    {| NodeIndex:
	    node-counter := <new-counter>;
	    rules( NodeIndex: _ -> <next-counter> node-counter );
	    <bottomup(
	       is-list
	    <+ \ t -> <add-annotation(|<copy-origin(|t)> ASTIndex(source,<NodeIndex>))> t \
	     )> ast
    |}

  get-ast-index =
    get-annotation(?ASTIndex(_,_))
 <+ warn(|"Cannot get AST index");
    !ASTIndex("FAKE", <next-random>)

  has-ast-index = where(get-annotation(?ASTIndex(_,_)))

  set-ast-index(|idx) = add-annotation(|idx)

  get-ast-metadata(|key) = prim("SG_get_ast_metadata", key)

  set-ast-metadata(|key,value) = prim("SG_set_ast_metadata", key, value)

rules // origins

  copy-origin(|from) =
    origin-location-offset-set(|<origin-location-offset> from)
 <+ warn(|"Cannot copy origin from")


module nabl2/runtime/analysis/base

imports

  runtime/editor/annotations
  runtime/editor/origins

  nabl2shared
  nabl2runtime

rules // ast indices

  index-ast(|source) =
    ?ast;
    {| NodeIndex:
	    node-counter := <new-counter>;
	    rules( NodeIndex: _ -> <next-counter> node-counter );
	    <bottomup(
	       is-list
	    <+ \ t -> <add-annotation(|<copy-origin(|t)> I(source,<NodeIndex>))> t \
	     )> ast
    |}

  is-ast-index = ?I(_,_)

  get-ast-index =
    get-annotation(is-ast-index)

  get-ast-source =
    get-annotation(?I(<id>,_))

  get-ast-index-or-default(|source) =
    get-ast-index <+ !I(source,())

  has-ast-index = where(get-annotation(is-ast-index))

  set-ast-index(|idx) = add-annotation(|idx)

  get-ast-metadata(|key) = prim("SG_get_ast_metadata", key)

  set-ast-metadata(|key,value) = prim("SG_set_ast_metadata", key, value)

  get-ast-refs = prim("SG_get_ast_references")

rules // origins

  copy-origin(|from) =
    origin-location-offset-set(|<origin-location-offset> from)
 <+ warn(|"Cannot copy origin from")


module nabl2/runtime/analysis/generation

imports

  nabl2/api

  libspoofax/editor/completion/interface

  signatures/nabl2/shared/common/-
  signatures/nabl2/shared/constraints/-
  nabl2/shared/-

  signatures/nabl2/runtime/common/-
  signatures/nabl2/runtime/constraints/-
  nabl2/runtime/analysis/-
  nabl2/runtime/prelude/-
  nabl2/runtime/pp

signature

  constructors
    Params        : Term -> Args
    ParamsAndType : Term * Term -> Args

rules // top-level functions that are called during analysis

  nabl2-debug-constraint-generation-hook = fail

  nabl2--generate-constraint-init(|source) = nabl2--strict(
    nabl2--generate-constraint-init-rule(|source)
  )

  nabl2--generate-constraint-file(|source,params) = nabl2--strict(
    nabl2--generate-constraint(nabl2--generate-constraint-default-rule(|source,params)|"default",source,params)
  )

rules

  nabl2--new-var(|source): n -> CVar(source,n')
    with n' := <nabl2--fresh> (source, <is-string <+ !"v"> n)

  nabl2--new-scope(|source): n -> Scope(source,n')
    with n' := <nabl2--fresh> (source, <is-string <+ !"s"> n)

rules

  // signatures of the generated rules
  nabl2--generate-constraint-init-rule(|source) = fail
  nabl2--generate-constraint-default-rule(|source,params) = fail

  // error throwing wrappers for recursive invocations
  nabl2--generate-constraint(s|name,source,args) =
    nabl2--error-no-index
 <+ nabl2--generate-constraint-placeholder(|source,args)
 <+ s
 <+ nabl2--generate-constraint-missing(|name,source,args)

  nabl2--error-no-index: term -> constraint*
    where not(nabl2--get-ast-index)
    with msg := $[Constraint generator traverses over non-AST node [<pp-NaBL2-Term> term]];
         if nabl2-debug-constraint-generation-hook then
           warn(|msg)
         end;
         constraint* := [CFalse(Message(Error(),msg,NoIndex()))]

  nabl2--generate-constraint-placeholder(|source,args): term -> constraint*
    where <is-SDF-placeholder> term
    with index := <nabl2--get-ast-index> term;
         if ParamsAndType(params,type) := args then
           constraint* := [CAstProperty(index, Params(), params),
                           CAstProperty(index, Type(), type)]
         else if Params(params) := args then
           constraint* := [CAstProperty(index, Params(), params)]
         end end

  nabl2--generate-constraint-missing(|name,source,args):
      term -> <conc> (ast-constraint*, constraint*)
    with index := <nabl2--get-ast-index> term;
    if ParamsAndType(params,type) := args then
      ast-constraint* := [CAstProperty(index, Params(), params),
                          CAstProperty(index, Type(), type)];
      msg := ${Missing constraint generation rule: {name} [[ {<nabl2--pp-flat-term> term} {<nabl2--pp-flat-args> args} ]]};
      if nabl2-debug-constraint-generation-hook then
        warn(|msg)
      end;
      constraint* := [CFalse(Message(Warning(),msg,index))]
    else if Params(params) := args then
      ast-constraint* := [CAstProperty(index, Params(), params)];
      msg := ${Missing constraint generation rule: {name} [[ {<nabl2--pp-flat-term> term} {<nabl2--pp-flat-args> args} ]]};
      if nabl2-debug-constraint-generation-hook then
        warn(|msg)
      end;
      constraint* := [CFalse(Message(Warning(),msg,index))]
    end end

rules

  nabl2--pp-flat-term: c#(a*) -> $[[c]([as])] 
    where not(is-list)
    with as := <map(!"_");separate-by(|",");concat-strings> a*

  nabl2--pp-flat-term: c#(a*) -> ${[{as}]} 
    where is-list
    with as := <map(!"_");separate-by(|",");concat-strings> a*

  nabl2--pp-flat-args: Params(a*) -> $[^ ([p])]
    with if <is-tuple> a* then
      p := <TupleToList;map(!"_");separate-by(|",");concat-strings> a*
    else
      p := "_"
    end

  nabl2--pp-flat-args: ParamsAndType(a*,_) -> $[[p] : _]
    with p := <nabl2--pp-flat-args> Params(a*)

  nabl2--pp-term-sig: a* -> ${[{a'}]}
    where is-list
    with a' := <map(nabl2--pp-term-sig);separate-by(|",");concat-strings> a*

  nabl2--pp-term-sig: c#(a*) -> $[[c]([a'])]
    where not(is-list)
    with a' := <map(!"_");separate-by(|",");concat-strings> a*

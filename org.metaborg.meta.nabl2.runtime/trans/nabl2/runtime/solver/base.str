module nabl2/runtime/solver/base

imports

  nabl2shared
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

rules
	
  solve-fact = fail
  
  solve-constraint = fail	
	
  constraint-priority = fail	
	
rules

  initial-solution = sol-new

	solve-ignore-placeholders: (c,sol) -> [(sol,[])]
	where <is-NaBL2-PrimitiveConstraint;
	       has-placeholder> c

  has-placeholder =
    not(?Message(_,_,_));
    (is-SDF-placeholder <+ one(has-placeholder))

  solve-facts =
    csolve-exhaustive( time-analysis(!"specialize",solve-specialise)
                     , solve-ignore-placeholders + solve-fact
                     , id
                     , solve-cost
                     , constraint-lt
                     )

  solve-constraints =
    csolve-greedy( time-analysis(!"specialize",solve-specialise)
                 , solve-ignore-placeholders + solve-constraint
                 , postprocess-errors
                 , solve-cost
                 , constraint-lt
                 )

  solve-specialise: (sol,c) -> res
  with <sol-s> sol => s*
     ; <S-apply(is-var)> (s*,c) => c'
     ; {| ReduceErrors:
            rules(ReduceErrors : _ -> [])
          ; <bottomup(try(reduce-op(|sol)))> c' => c''
          ; re* := <ReduceErrors>
          ; if [] := re*
            then
              !(sol,c'')
            else
              msg' := <get-constraint-message> c'
            ; msg'' := <foldr(!msg',{ ?(re,<id>) ; detail-message(|re) })> re*
            ; sol' := <add-message(id|msg'')> sol
            ; !(sol',CTrue())
            end
       |} => res

  add-reduce-error(|msg) =
    where(msg* := <ReduceErrors> ; rules(ReduceErrors : _ -> [msg|msg*]))

  solve-cost =
    strict(Fst; sol-e; length)

rules
 
  solve-fact = time-analysis(!"base",solve-base-fact) 
  solve-constraint = time-analysis(!"base",solve-base-constraint) 
 
  solve-base-fact = solve-junction

  solve-base-constraint: (CTrue(),sol) -> [(sol,[])]
  solve-base-constraint: (CFalse(msginfo),sol) -> [(sol',[])]
    with msg  := "False never succeeds";
         sol' := <add-message(default-message(|msg)|msginfo)> sol
  solve-base-constraint = solve-junction

  solve-junction: (CConj(c*),sol) -> [(sol,c*)]
  solve-junction: (CDisj(c*),sol) -> <map(\ c -> (sol,[c]) \)> c*
 
  constraint-priority: CTrue() -> 0
  constraint-priority: CFalse(_) -> 0
  constraint-priority: CConj(_) -> 0
  constraint-priority: CDisj(_) -> 99

rules

  constraint-lt =
    strict((constraint-priority-or-default,constraint-priority-or-default));
    lt
  
  constraint-priority-or-default = constraint-priority <+ !99

rules
 
  debug-solve(s): i@(c,sol) -> o*
    with <debug> $[solve [<pp-NaBL2-Constraint> c]]
    where if o* := <s> i then
            <with(map(Snd;debug;map(pp-NaBL2-Constraint);debug(!"--> ")))> o*
          else
            <debug> "--> (unsolved)"; fail
          end


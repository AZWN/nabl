module generation/core/bindings/rules

imports
  libstrc
  src-gen/signatures/-
  src-gen/signatures/common/-
  src-gen/signatures/core/-
  src-gen/signatures/formulas/-
  src-gen/signatures/terms/-
  
  generation/util
  generation/core/namespaces/-
  generation/core/scope/-
  generation/formulas/-
  
rules
  
  binding-to-rule:
    Binding(rid, p, Defines(u, ns, name, scope)) ->
    RULE(rid, SCOPEVARS(), p, p', ASSIGNDEF(name, ns', scope'))
    where
      p'     := <strip-annos; replace-all(|[(<strip-annos> name, DEFVAR())])> p
    ; ns'    := <ns-to-term> ns
    ; scope' := <scope-to-str> scope
    
  binding-to-rule(|rs):
    Binding(rid, p, RefersTo(ns, name, scope, formula)) -> 
    RULE(rid, [CTX()|SCOPEVARS()], p, p', ASSIGNREF(name, ns', scope, tasks))
    where
      p'    := <strip-annos; replace-all(|[(<strip-annos> name, REFVAR())])> p
    ; ns'   := <ns-to-term> ns
    ; tasks := <formula-to-tasks(|[], [], rs)> formula
      
  binding-to-rule:
    Binding(rid, p, Alias(var1, var2)) -> <fail>
  
  binding-to-rule:
    Binding(rid, p, Wildcard(ns, from-scope, to-scope, formula)) -> <fail>

overlays
  
  DEFVAR() = Var("def__")
  REFVAR() = Var("ref__")

  ASSIGNDEF(n, ns, s) = 
    Assign(DEFVAR(), App(CALL("nablc-def", [ns, s]), n))

  ASSIGNREF(n, ns, s, ts) = 
    Seq(ts, Assign(REFVAR(), App(CALL("nablc-ref", [ns, s]), n)))

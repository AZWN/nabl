templates // namespaces

  ModuleSection.Namespaces  = <
  namespaces   
    <NamespaceDef*>
  > 
  
  NamespaceDef.NamespaceDef = 
		<<NamespaceID>
		>
  
  NamespaceRef.NamespaceRef = <<NamespaceID>>

lexical syntax

  NamespaceID = Id
                
templates // properties

  ModuleSection.Properties = <
  properties
    <PropertyDef*>
  >  
    
  PropertyDef.PropertyDef = <
  <PropertyID> of <NamespaceRef+; separator=", "> : <Sort>>
  
  PropertyRef.TypeProp    = <type>  
  PropertyRef.PropertyRef = <<PropertyID>>

  PropertyTerm.PropertyTerm       = <of <PropertyRef> <Term>>
  PropertyPattern.PropertyPattern = <of <PropFilter> <PropertyRef> <Pattern>>

  PropFilter.Equal      = <>
  PropFilter.Conformant = <conformant>
  
  Constraint.PropertyConstraint = <where <Term> has <PropertyRef> <Pattern>>
    
lexical syntax

  PropertyID = Id
  PropertyID = "type" {reject}
  
templates // binding rules
    
  ModuleSection.BindingRules = <
  binding rules
    <BindingRule*>
  >
  
  BindingRule.BindingRule = <
  <Pattern> <Constraint*> : <BindingClause+>> 

templates // clauses

  BindingClause.DefClause = <
    <DefKind> defines <Unique> <NamespaceRef> <Term> <PropertyTerm*> <InDefScopes> <Constraint*>
  > 
  
  DefKind.Explicit = <>
  DefKind.Implicit = <implicitly>
  
  Unique.Unique    = <>
  Unique.Unique    = <unique>
  Unique.NonUnique = <non-unique>
    
  BindingClause.ScopeClause = <
    scopes <NamespaceRef+; separator=", ">
  > 
  
  BindingClause.RefClause     = <
    <RefClausePart+; separator="otherwise">
  >
    
  RefClausePart.RefClausePart = <
  refers to <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope> <Constraint*>>
  
  BindingClause.ImportClause = <
    <ImportClausePart+; separator="otherwise\n">
  > 
  
  ImportClausePart.SingleImport = <
  imports <Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <FromScope> <Alias> <IntoScopes> <Constraint*>>             

  ImportClausePart.WildcardImport = <
  imports <ImportRef+; separator=", "> <PropertyPattern*> <FromScope> <IntoScopes> <Constraint*>> 
  
  Alias.None  = <>
  Alias.Alias = <<Term>>
 
  ImportRef.Direct     = <<NamespaceRef>>
  ImportRef.Transitive = <imported <NamespaceRef>>
   
templates // scopes

  InDefScopes.Current = <>
  InDefScopes         = <in <DefScopes>>   {bracket}
  
  InRefScope.Current  = <>
  InRefScope          = <in <RefScope>>    {bracket}
  
  FromScope.Current   = <>
  FromScope           = <from <RefScope>>  {bracket}
  
  IntoScopes.Current  = <>
  IntoScopes          = <into <DefScopes>> {bracket}
 
  DefScopes.Current   = <current scope>
  DefScopes.DefScopes = <<DefScope+; separator=", ">> 
  DefScope.Subsequent = <subsequent scope>
  DefScope.Context    = <<Term>>
  
  RefScope.Current    = <current scope>
  RefScope.Enclosing  = <enclosing <NamespaceRef>>
  RefScope.Context    = <<Disambiguator> <NamespaceRef> <Term> <PropertyPattern*> <InRefScope>>

  Disambiguator.All   = <>
  Disambiguator.Best  = <best>
  
templates // constraints

  Constraint.Reference = <
    where <Term> refers to <NamespaceRef> <Pattern> <PropertyPattern*> <InRefScope>> 
  
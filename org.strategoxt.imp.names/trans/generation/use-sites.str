module generation/use-sites

imports
  
  libstrc
  include/NameBindingLanguage
  
  generation/util
  generation/main
  generation/constraints
  generation/def-sites
  
rules
  
  uses-to-str(|replace*, bound*, rule*, task*, pattern) =
    filter(?UseSite(<id>) + ?ImportSite(<id>));
    if nonempty then
      (rep*, task'*) := <foldl(use-to-str(|replace*, bound*, rule*))> (<id>, ([], task*));
      ubody  := <foldr(<replace-all(|rep*)> pattern, to-seq)> task'* ;
      <iset-add(|USE_RULE(Seq(Match(pattern), ubody)))> rule*
    end
    
  use-to-str(|replace*, bound*, rule*):
    (u, (r*, task*)) -> ([(t', USE_CALL(c*)), r*], task'*)
    where 
      < ?[Reference(NameBinding(_, _, t), _, _, _)|_] 
      + ?[SingleImport(NameBinding(_, _, t), _, _, _, _)|_] 
      + ?[WildcardImport(_, _, RefScope(NameBinding(_, _, t)), _, _)|_]
      > u;
      t'           := <to-ppable-str> t;
      (c*, task'*) := <foldl(to-ppable-str; (ref-to-str(|replace*, bound*, rule*) <+ Snd))> (u, ([], task*))
    
  ref-to-str(|replace*, bound*, rule*): 
    (Reference(NameBinding(_, ns, _), prop*, All(), constr*), (c*, task*)) -> ([c*, CANDIDATE(<nabl-to-str> ns, prop'*)], task'*)
    where
      replace2* := <hashtable-copy> replace*;
      bound2*   := <iset-copy> bound*;
      task'*    := <constraints-to-str(|replace2*, bound2*, rule*, task*)> constr*;
      prop'*    := <map(prop-to-str); alltd(replace(|replace2*))> prop*;
      <hashtable-destroy> replace2*;
      <iset-destroy> bound2*
      
  // ref-to-str(|replace*, bound*, rule*, n): 
  //   Reference(NameBinding(_, ns, _), [], RefScope(NameBinding(_, ns', t), _, _), []) -> CANDIDATE(<nabl-to-str> ns, <nabl-to-str> ns', <to-ppable-str> t)
  
  prop-to-str:
    PropertyBinding(_, p, t) -> PROPERTY(<nabl-to-str> p, <to-ppable-str> t)     

overlays
     
  USE_RULE(call) =
  SDefT(
    "nabl-use-site"
  , []
  , [DefaultVarDec("context"), DefaultVarDec("uris"), DefaultVarDec("deps"), DefaultVarDec("states")]
  , call
  )
  
  USE_CALL(candidates) =
  CallT(
    SVar("nabl-use")
  , []
  , [ Var("context"), Var("uris"), Var("deps"), LIST(candidates)] 
  )
  
  CANDIDATE(ns, ps) =
  TERM("UseCandidate", [ns, LIST(ps), TERM("Current"), TERM("All")])

  CANDIDATE(ns1, ns2, n) =
  TERM("UseCandidate", [ns1, LIST([]), TERM("Current"), TERM("Context", [n, ns2, LIST([]), TERM("All")])])

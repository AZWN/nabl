module generate

imports
	libstrc
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/NameDefinitionLanguage
  lib/editor-common.generated
  lib/compilation-library.generated

rules // Incremental code generation of project using compilation library.
      
	generate-name-analysis:
		(selected, position, ast@Naming(name, _, _, _), path, project-path) -> (filename, result)
		with
			filename   := <guarantee-extension(|"str")> path ;
			str-module := <to-analysis> ast ;
			result     := <pp-stratego-string> str-module

overlays
	
	TERM(o, args) = NoAnnoList(Op(o, args))
	TERM(o)       = NoAnnoList(Op(o, []))
	CALL(c)       = CallNoArgs(SVar(c))
	CALL(c, t)    = CallT(SVar(c), [], [t])

	RULE(n, lhs, rhs, clauses) = RDefNoArgs(n, Rule(lhs, rhs, clauses))
		
overlays
	
	GEN_IMPORTS      = Imports([Import("lib/analysis-library.generated"), Import("lib/index-library.generated")])
	GEN_CONSTRUCTORS = Constructors([CONS_DECL("Type", "DefDataKind")])
		
	CONS_DECL(c, t) = OpDecl(c, ConstType(SortNoArgs(t)))
	
	ADJUST_DATA(term, kind, data, clauses) = 
		RDefT(
			  "adjust-index-def-data"
			, [DefaultVarDec("store")]
			, [DefaultVarDec("namespace"), DefaultVarDec("path")]
			, Rule(
			  	  term
			  	, App(CALL("store"), TERM("DefData", [Var("path"), TERM(kind), data]))
				, clauses
				)
      		)

rules
	
	to-analysis:
		Naming(name, import*, namespace*, rule*) -> 
		Module(
			  name
			, [ GEN_IMPORTS
			  , Imports(import*)
			  , Signature([GEN_CONSTRUCTORS, Constructors(<map(to-opdecl)> namespace*)])
			  | <filter(to-rules)> rule*
			  ]
			)
			
	to-opdecl:
		Namespace(ns) -> CONS_DECL(ns, "Namespace")
		
	to-rules:
		ND-Rule(term, part*, cond*) -> Rules(<not(?[])> [scope-rule*, definition-rule*, annotate-rule*, type-rule*])
		where
			<debug> term 
		where
			clause1*         := <filter(to-clauses)> part* ;
			clause2*         := <filter(to-clauses)> cond* ; 
			clause*          := <conc> (clause1*, clause2*) ;
			scope-rule*      := <to-scope-rules(|term, clause*)> part* ;
			definition-rule* := <filter(to-definition-rules(|term, clause*)) ; concat> part* ;
			annotate-rule*   := <to-annotate-rules(|term, clause*)> part* ;
			type-rule*       := <filter(to-type-rules(|term, clause*)) ; concat> part*

rules
				
	to-clauses:
		ND-Ref(_, name, TypeBinding(type), _) -> 
		WhereClause(
			Assign(
				  NoAnnoList(List([type]))
				, App(Seq(CALL("index-lookup"), CALL("index-get-data-all", TERM("Type"))), name)
				)
			)
		
	to-clauses:
		Where(term, TypeCheck(type)) -> WhereClause(Assign(type, App(CALL("type-of"), term)))

rules
	
	to-scope-rules(|term, clause*):
		part* -> result
			where
			scope* := <filter(to-scope)> part* ;
			if <?[]> scope* then
				result := [] 
			else
				result := [RULE("nam-get-scope-types", term, List(scope*), clause*)]
			end
	
	to-scope:
		ND-Scope(_, NsRef(namespace), _) -> TERM(namespace)
			
rules
			
	to-definition-rules(|term, clause*):
		ND-Def(NsRef(namespace), name, _, None()) ->
		[ RULE("nam-get-definition-key", term, name, clause*)
		, RULE("nam-get-definition", term, App(CALL("nam-get-def", TERM(namespace)), name), clause*) ]

rules
	
	to-annotate-rules(|term, clause*):
		part* -> result
		where
			replacement* := <filter(to-annotate-replacement)> part* ;
			if <?[]> replacement* then
				result := [] 
			else
				result := [SDefT("nam-annotate-names", [], [DefaultVarDec("path")], <alltd(replace(|replacement*) <+ introduce-id)> term)]
			end
		
	to-annotate-replacement:
		ND-Def(_, name, _, None())-> (name, CALL("nam-annotate-def", Var("path")))
			
	to-annotate-replacement:
		ND-Ref(NsRef(namespace), name, _, None()) -> (name, CALL("nam-annotate-use", TERM(namespace)))
			
	replace(|replacement*): t1 -> t2 where <fetch-elem(?(t1, t2))> replacement*
	
	introduce-id: Wld()  -> Id()
	introduce-id: Var(_) -> Id()

rules
		
	to-type-rules(|term, clause*):
		ND-Def(NsRef(namespace), name, TypeBinding(type), _) -> 
		[ ADJUST_DATA(term, "Type", type, clause*)
		, RULE("type-of", term, type, clause*) ]
		
	to-type-rules(|term, clause*):
		ND-Type(type) -> [RULE("type-of", term, type, clause*)]
	
		
	
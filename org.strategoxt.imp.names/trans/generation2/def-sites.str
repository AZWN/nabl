module generation2/def-sites

imports
  
  libstrc
  include/NameBindingLanguage
  
  generation2/util
  generation2/main
  generation2/scopes
  generation2/constraints

rules
  
  def-to-name = ?DefClause(Explicit(), _, _, <id>, _, _, _)
    
rules
  
  clause-to-def-rule(|pattern, scope*):
    (o, DefClause(Explicit(), unique, ns, term, _, defscope, constr*)) -> [scope-clause*, congruence] 
    where
      ns-term := <nabl-to-str> ns
    ; i := <dec> o
    ; (replacement*, scope-clause*, defscope-var, _) := <scope-to-str(|ns-term, i, 1)> defscope
    ; replacement := (term, DEF_CALL(ns-term, <nabl-to-str> unique, defscope-var, scope*, $[child-uri__[i]], $[sibl-uri__[i]], $[child-uri__[o]], $[sibl-uri__[o]]))
    ; congruence := <replace-all(|[replacement, replacement*])> pattern
   
    
  clause-to-prop-rule(|pattern, glob-replacement*, glob-bound*):
    (o, DefClause(Explicit(), _, ns, term, prop*, _, constr*)) -> (match-rule*, [loc-task*, congruence])
    where
      ns-term     := <nabl-to-str> ns
    ; i := <dec> o
    ; patt-bound* := <collect-all(?Var(_))> term
    ; (loc-replacement*, loc-bound*, match-rule*, loc-task*, _) := <constraints-to-str(|glob-replacement*, [glob-bound*, patt-bound*], i, 1)> constr*
    ; replacement := (term, PROP_CALL(<alltd(replace(|loc-replacement*)); map(prop-to-str)> prop*))
    ; congruence  := <replace-all(|[replacement])> pattern
      
  clause-to-rules(counter|pattern, scope*, glob-replacement*, glob-bound*, glob-task*, n):
    def-clause* -> [COORD_RULE(Match(pattern)), DEF_RULE(Match(pattern), def-seq), rule*,  PROP_RULE(Match(pattern), prop-seq)]//, prop-rule*]
    where
      map-with-index(clause-to-def-rule(|pattern, scope*)); concat => clause*
    ; m := <length> def-clause*
    ; def-seq := <to-seq> [clause*, App(CALL("child-uris__"), Var($[child-uri__[m]])), App(CALL("sibl-uris__"), Var($[sibl-uri__[m]]))] 
    where
      map-with-index(clause-to-prop-rule(|pattern, glob-replacement*, glob-bound*)); unzip; (concat, concat) => (rule*, prop-clause*)
    ; prop-seq := <to-seq> [glob-task*, prop-clause*, Id()]
     
  clause-to-str(|pattern, scopes, repl*, bound*, n):
    DefClause(Implicit(), u, ns, t, ps, ds, constr*) -> (rule*, [sc*, dcall], [pr*, app], k)
    where
      ns' := <nabl-to-str> ns
    ; (r*, sc*, ds', m) := <scope-to-str(|ns', n)> ds
    ; dcall := <replace-all(|r*)> pattern
    ; b*    := <collect-all(?Var(_))> t
    ; (r2*, b3*, rule*, pr*, k) := <constraints-to-str(|repl*, [bound*, b*], m)> constr*
    ; ps' := <map(prop-to-str); alltd(replace(|r2*))> ps
    ; app := DEF_APPLY(ns', <nabl-to-str> u, ds', t, ps')

rules
         
  prop-to-str: PropertyTerm(p, t) -> PROPERTY(<nabl-to-str> p, t)     

  nabl-to-str: NonUnique() -> TERM("NonUnique")
  nabl-to-str: Unique()    -> TERM("Unique")
  
overlays
  
  NAME_RULE(lhs, rhs) = RDefNoArgs("nabl-get-name", RuleNoCond(lhs, rhs))

overlays
  	
	DEF_RULE(match, call) =
  SDefT(
    "nabl-def-site"
  , [DefaultVarDec("child-uris__"), DefaultVarDec("sibl-uris__")]
  , [ DefaultVarDec("lang__")
    , DefaultVarDec("partition__")
    , DefaultVarDec("uniques__")
    , DefaultVarDec("elems__")
    , DefaultVarDec("child-uri__0")
    , DefaultVarDec("sibl-uri__0")
    , DefaultVarDec("states__")]
  , Seq(match, call)
  )  
	
  DEF_CALL(ns, u, ds, s, in-child-uris, in-sibl-uris, out-child-uris, out-sibl-uris) = 
  CallT(
    SVar("nabl-def")
  , [Match(Var(out-child-uris)), Match(Var(out-sibl-uris))]
  , [ Var("lang__")
    , Var("partition__")
    , Var("uniques__")
    , Var("elems__")
    , Var(in-child-uris)
    , Var(in-sibl-uris)
    , ns, u, ds, LIST(s)]
  )
  
overlays
  
	PROP_RULE(match, call)  =
  SDefT(
    "nabl-prop-site"
  , []
  , [ DefaultVarDec("lang__")
    , DefaultVarDec("partition__")
    , DefaultVarDec("elems__")
    , DefaultVarDec("tasks__")
    , DefaultVarDec("states__")]
  , Seq(match, call)
  )

  PROP_CALL(ps) = 
  CallT(
    SVar("nabl-props")
  , []
  , [ Var("elems__"), LIST(ps)]
  )
  
  PROPERTY(p, v) = TERM("Prop", [p, v])

overlays
    
  COORD_RULE(match) =
  SDefT(
    "nabl-name-site"
  , [DefaultVarDec("child-uris__"), DefaultVarDec("sibl-uris__")]
  , [ DefaultVarDec("lang__")
    , DefaultVarDec("partition__")
    , DefaultVarDec("uniques__")
    , DefaultVarDec("elems__")
    , DefaultVarDec("tasks__")
    , DefaultVarDec("uris__")
    , DefaultVarDec("states__")]
  , Seq(
      match
    , Seq(
        DEF_SITE_CALL()
      , Seq (
          RECURSION()
        , PROP_SITE_CALL()
        )
      )
    )
  )
  
  DEF_SITE_CALL = 
  CallT(
    SVar("nabl-def-site")
  , [Match(Var("child-uris__")), CALL("sibl-uris__")]
  , [ Var("lang__")
    , Var("partition__")
    , Var("uniques__")
    , Var("elems__")
    , Var("uris__")
    , Var("states__")
    ]
  ) 

  RECURSION =
  CallT(
    SVar("nabl-children")
  , []
  , [ Var("lang__")
    , Var("partition__")
    , Var("uniques__")
    , Var("elems__")
    , Var("tasks__")
    , Var("child-uris__")]
  )
  
  PROP_SITE_CALL = 
  CallT(
    SVar("nabl-prop-site")
  , []
  , [ Var("lang__")
    , Var("partition__")
    , Var("elems__")
    , Var("tasks__")
    , Var("states__")
    ]
  ) 


overlays
  
  IMP_DEF_CALL(ns, u, ds, s) = 
  CallT(
    SVar("nabl-def")
  , []
  , [ Var("lang__")
    , Var("partition__")
    , Var("uniques__")
    , Var("elems__")
    , Var("uris__")
    , ns, u, ds, LIST(s)]
  )
  
  DEF_APPLY(ns, u, ds, t, ps) = 
  Where(App(Seq(IMP_DEF_CALL(ns, u, ds, []), PROP_CALL(ps)), t))
  


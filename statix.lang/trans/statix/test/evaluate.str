module statix/test/evaluate

imports

  nabl2/api
  
  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-

  signatures/statix/test/-
  pp/statix/test/-
  statix/test/-
  
  statixruntime

signature
  constructors
    SUCCEEDS : TestResult
    FAILS    : TestResult

rules
  
  editor-evaluate:
      (_, _, Module(_, _), path, project-path) -> None()

  editor-evaluate:
      (_, _, ast@Test(_, _, _), path, project-path) -> (filename, result)
    with result := <evaluate;pp-Statix-string(prettyprint-Statix-TestResult)> ast;
         filename := <guarantee-extension(|"stxresult")> path

rules

  evaluate-test: ast -> result
    with if (_, [], _, _) := <evaluate> ast then
           result := SUCCEEDS()
         else
           result := FAILS()
         end

  evaluate: ast@Test(bvs, body, _) -> result
    with a      := <nabl2-get-ast-analysis> ast;
         ext    := <nabl2-get-custom-analysis> a;
         spec   := <strip-annos;eliminate-all;spec-ast-to-aterm> (ast, ext);
         level  := <stx--log-level>;
         body'  := <eliminate-all;stx--index-ast(|""/*path*/)> body;
         result := <stx--solve-constraint(|spec, level)> (bvs, body')

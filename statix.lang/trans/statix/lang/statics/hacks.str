module statix/lang/statics/hacks

imports

  signatures/statix/lang/-
  statix/lang/statics/-

rules

//lift-types : ( [L], Type ) -> Type

  lift-type: (lsorts, PRED(tys)) -> PRED(tys')
    where tys' := <lift-sorts> (lsorts, tys)

  lift-type: (lsorts, FUN(intys, outtys)) -> FUN(intys', outtys')
    where (inlsorts, outlsorts) := <split-at(|<length> intys)> lsorts;
          intys'  := <lift-sorts> (inlsorts, intys);
          outtys' := <lift-sorts> (outlsorts, outtys)
  
  lift-sorts = zip; map(lift-sort)

  lift-sort: (Id(), ty) -> ty
  lift-sort: (ToList(), ty) -> LIST(ty)

rules

  inout-type = inout-types; tys-to-ty
  inout-types: PRED(tys)          -> tys
  inout-types: FUN(intys, outtys) -> <conc> (intys, outtys)

  in-type = in-types; tys-to-ty
  in-types: PRED(tys)          -> tys
  in-types: FUN(intys, outtys) -> intys

  out-type = out-types; tys-to-ty
  out-types: PRED(tys)          -> tys
  out-types: FUN(intys, outtys) -> outtys

  tys-to-ty: [ty] -> ty
  tys-to-ty: tys  -> TUPLE(tys)
    where not([_] := tys)

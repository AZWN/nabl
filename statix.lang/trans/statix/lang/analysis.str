module statix/lang/analysis

imports

  libspoofax/analysis/constraint

  nabl2shared
  nabl2/shared/ast // for index-ast
  nabl2runtime
  nabl2/api
  nabl2/runtime/analysis/main

  signatures/statix/lang/-

  statix/lang/-
  statix/lang/statics/-
  statix/lang/normalize
  statix/lang/scope-extension

  statix/runtime/pp
  pp/statix/lang/-
  pp

signature
  sorts CustomAnalysis constructors
    CustomUnitAnalysis : List(Error) * Extensions -> CustomAnalysis
    CustomAnalysis : Extensions -> CustomAnalysis

rules // Analysis

  editor-analyze = nabl2-analyze(desugar-all)

  nabl2-custom-analysis-unit-hook: (resource, ast, _) -> CustomUnitAnalysis(or-e*, ext-c*)
    with or-e* := <detect-overlapping-rules> ast
       ; ext-c* := <collect-ext-constraints> ast

  nabl2-custom-analysis-final-hook(|a): (resource, _, units) -> (e*, [], [], ca)
    with ext-c* := <map(?CustomUnitAnalysis(_, <id>));concat> units
       ; ext-c'* := <explicate-ext-constraints(|a)> ext-c*
       ; <ext-log(prettyprint-Statix-ExtConstraints|"Extension constraints:")> ext-c'*
       ; (ext, ext-e*) := <solve-ext-constraints> ext-c'*
       ; unit-e* := <map(?CustomUnitAnalysis(<id>, _));concat> units
       ; e* := [unit-e*, ext-e*]
       ; ca  := CustomAnalysis(ext)

  nabl2-prettyprint-hook   = prettyprint-Statix-IType
  prettyprint-Statix-IType = nabl2-prettyprint-term

  nabl2-prettyprint-hook   = prettyprint-Statix-TType
  prettyprint-Statix-TType = nabl2-prettyprint-term


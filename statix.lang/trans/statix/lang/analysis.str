module statix/lang/analysis

imports

  libspoofax/analysis/constraint

  nabl2shared
  nabl2/shared/ast // for index-ast
  nabl2runtime
  nabl2/api
  nabl2/runtime/analysis/main
  nabl2/runtime/transform/query // for private set-custom-analysis

  signatures/statix/lang/-

  statix/lang/statics/-
  statix/lang/normalize

  pp

rules // Analysis

  editor-analyze:
      AnalyzeSingle(change*) -> AnalysisResult(result*)
    with
      ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; result* := <map({ resource, ast, ast', a', error*, warning*, note*:
                        ?(resource, ast)
                      ; ( (ast', a', error*, warning*, note*) :=
                              <editor--analyze> (resource, ast)
                        < !(resource, Full(ast', a', error*, warning*, note*))
                        + !(resource, Failed())
                        )
                      })> ast*

  editor--analyze:
      (resource, ast) -> (analyzed-ast, result, error*, warning*, note*)
    with nabl2-custom-analysis-info-msg(|"Desugaring AST...");
         desugared-ast := <desugar-all> ast;
         nabl2-custom-analysis-info-msg(|"Desugared AST.");

         nabl2-custom-analysis-info-msg(|"Analyzing AST...");
         (analyzed-ast, a, e*, w*, n*) :=
            <analyze(|resource)> desugared-ast;

         if [] := e* then
           (normalized-ast, a', a-e*, _, _) :=
               <normalize(|a);analyze(|resource)> analyzed-ast;
           if not([] := a-e*) then
             fatal-err-msg(|"Normalization introduced errors")
           end;

           nabl2-custom-analysis-info-msg(|"Detecting overlapping rules...");
           or-e* := <detect-overlapping-rules> analyzed-ast;

           nabl2-custom-analysis-info-msg(|"Computing scope extensions...");
           (ext*, ext-e*, ext-w*, ext-n*) :=
               <compute-scope-extensions> normalized-ast;

           result   := <nabl2--set-custom-analysis(|a)> (normalized-ast, a', ext*);
           error*   := [e*, ext-e*, or-e*];
           warning* := [w*, ext-w*];
           note*    := [n*, ext-n*]
         else
           result   := a;
           error*   := e*;
           warning* := w*;
           note*    := n*
         end


  analyze(|resource) =
    nabl2-custom-analysis-info-msg(|"| Analyzing...");
    nabl2-erase-ast-indices;nabl2-analyze-ast(|resource)

  nabl2-prettyprint-hook   = prettyprint-Statix-IType
  prettyprint-Statix-IType = nabl2-prettyprint-term

  nabl2-prettyprint-hook   = prettyprint-Statix-TType
  prettyprint-Statix-TType = nabl2-prettyprint-term

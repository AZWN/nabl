module statix/lang/normalize/queries

imports

  libspoofax/term/annotation
  libspoofax/term/origin

  nabl2/shared/vars
  nabl2/api

  signatures/statix/lang/-
  statix/lang/statics/-
  statix/lang/normalize/util

rules

  // normalize namespace based resolve stand-ins
  // before functional relations

  normalize-resolves = ?(a, <id>);
    nabl2-custom-analysis-info-msg(|"| Normalize namespace-based queries...");
    bottomup(
      try(normalize-resolve(|a))
    );
    !(a, <id>)

  normalize-resolve(|a): t@ResolveMatch(Occurrence(ns, ts, _), rel) -> wf'
    with wf := <norm-get-ast-decl-property(|a, "wf")> t;
         wf' := Concat(wf, rel)

  normalize-resolve(|a): t@ResolveLt(_, rel) -> ord'
    with ord := <norm-get-ast-decl-property(|a, "ord")> t;
         ord' := <topdown(try(?EOP();!rel))> ord

  normalize-resolve(|a): OccurrenceMatch(Occurrence(ns, ts, _)) -> lam
    with NS(tys) := <norm-get-ast-decl-type(|a)> ns;
         us := <make-fresh-vars(|"u")> tys;
         d := Occurrence(ns, us, Wld());
         lam := LLam([d], [], CEqual(<build-terms(|tys)> us, <build-terms(|tys)> ts))
  
rules

  // normalize queries
  // before functional relations, before wildcards

  normalize-queries = ?(a, <id>);
    nabl2-custom-analysis-info-msg(|"| Normalize queries...");
    bottomup(
      try(normalize-query(|a))
    ; try(normalize-query-ops(|a))
    );
    !(a, <id>)


  normalize-query(|a): CAskRel(rel, ts, st) -> c
    with c := <ask-rel-query> (rel, st, ts, [])


  normalize-query-ops(|a) =
    normalize-in-rules(replace-functional-relop(|a),id,functional-relop-to-c(|a))

  replace-functional-relop(|a): AskRelOp(rel, in_ts, st) -> out_t
    with DATA(ty) := <norm-get-ast-type(|a)> rel;
         outtys := <out-types> ty;
         out_ts := <make-fresh-vars(|"t")> outtys;
         out_t := <build-terms(|outtys)> out_ts

  functional-relop-to-c(|a): (AskRelOp(rel, in_ts, st), out_t) -> c
    with DATA(ty) := <norm-get-ast-type(|a)> rel;
         outtys := <out-types> ty;
         out_ts := <match-terms(|outtys)> out_t;
         c := <ask-rel-query> (rel, st, in_ts, out_ts)


  ask-rel-query: (rel, st, in_ts, out_ts) -> c
    with ts       := <conc> (in_ts, out_ts);

         us       := <make-fresh-vars(|"u")> in_ts;
         eq       := CEqual(<build-terms(|in_ts)> us, <build-terms(|in_ts)> in_ts);

         p        := <build-terms(|in_ts)> us;
         match    := LLam([p], [], eq);
         filter   := Filter(rel, match);

         min      := Min([], LLam([Wld(), Wld()], [], CFalse()));

         in_wlds  := <map(!Wld())> in_ts;
         t        := <conc;build-terms(|ts)> (in_wlds, out_ts);
         pts      := List([Tuple([Wld(), t])]);

         c        := CResolveQuery(filter, min, st, pts)

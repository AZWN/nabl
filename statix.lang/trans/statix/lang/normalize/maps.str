module statix/lang/normalize/maps

imports

  libspoofax/term/annotation
  libspoofax/term/origin

  nabl2/shared/vars
  nabl2/api

  signatures/statix/lang/-
  statix/lang/statics/-
  statix/lang/normalize/util

rules

  // replace functional and predicate maps to explicit constraints and rules

  normalize-mapping-constraints(|a):
      Module(x, section*){a*} -> Module(x, section''*){a*}
    with nabl2-custom-analysis-info-msg(|"| Normalize mapping constraints...")
    with (section'*, maps) := <collect-map(replace-map(|a))> section*;
         rule* := <map(map-to-rules(|a)); concat> maps;
         section''* := <conc> (section'*, <rules-to-sections> rule*)

  rules-to-sections = ?[] <+ ![Rules(<id>)]


  replace-map(|a): CDeriveMap(x, _, _) -> CDecl(ind, x, sort)
    with ind := <norm-get-ast-decl-property(|a,"ind")> x;
         ty := <norm-get-ast-decl-type(|a)> x;
         sort := <type-to-sort;norm-set-ast-type(|ty)> ty

  map-to-rules(|a): (CDeriveMap(x, y, lift*), _) -> [base-rule, elem-rule]
    with ty@PRED(tys) := <norm-get-ast-decl-type(|a)> x;
         ty_lift* := <zip> (tys, lift*);
         base-rule := <cmap-base-rule(|ty)> (x, y, ty_lift*);
         elem-rule := <cmap-elem-rule(|ty)> (x, y, ty_lift*)

  cmap-base-rule(|ty): (x, y, ty_lift*) -> Rule(NoLabel(), h, [], CTrue())
    with ps := <map(lift-base)> ty_lift*;
         h  := C(x, ps)

  cmap-elem-rule(|ty): (x, y, ty_lift*) -> Rule(NoLabel(), h, [], CConj(call, rec))
    with ps   := <lift-head-elems(|"x")> ty_lift*;
         call := C(y, <lift-call-elems(|"x")> ty_lift*, NoMessage());
         rec  := C(x, <lift-rec-elems(|"x")> ty_lift*, NoMessage());
         h    := <norm-set-ast-type(|ty)> C(x, ps)


  replace-map(|a): FDeriveMap(x, _, _, _) -> CDecl(ind, x, sort)
    with ind := <norm-get-ast-decl-property(|a,"ind")> x;
         ty := <norm-get-ast-decl-type(|a)> x;
         sort := <type-to-sort;norm-set-ast-type(|ty)> ty

  map-to-rules(|a): (FDeriveMap(x, y, in-lift*, out-lift), _) -> [base-rule, elem-rule]
    with ty@FUN(intys, outty) := <norm-get-ast-decl-type(|a)> x;
         FUN(_, elem_outty) := <norm-get-ast-decl-type(|a)> y;
         in-ty_lift* := <zip> (intys, in-lift*);
         out-ty_lift := (outty, out-lift);
         base-rule := <fmap-base-rule(|ty, elem_outty)> (x, y, in-ty_lift*, out-ty_lift);
         elem-rule := <fmap-elem-rule(|ty, elem_outty)> (x, y, in-ty_lift*, out-ty_lift)

  fmap-base-rule(|ty, elem_outty): (x, y, in-ty_lift*, out-ty_lift) -> Rule(NoLabel(), h, [], CTrue())
    with inps := <map(lift-base)> in-ty_lift*;
         outp := <lift-base> out-ty_lift;
         h    := <norm-set-ast-type(|ty)> F(x, inps, outp)

  fmap-elem-rule(|ty, elem_outty): (x, y, in-ty_lift*, out-ty_lift) -> Rule(NoLabel(), h, [], CConj(call, rec))
    with FUN(_, rec_outty) := ty;
         inps     := <lift-head-elems(|"x")> in-ty_lift*;
         outp     := <lift-elem-head(|"y")> (1, out-ty_lift);
         in-call  := <lift-call-elems(|"x")> in-ty_lift*;
         out-call := <lift-elem-call(|"y")> (1, out-ty_lift);
         call-op  := <norm-set-ast-type(|elem_outty)> COp(y, in-call);
         call     := CEqual(call-op, out-call, NoMessage());
         in-rec   := <lift-rec-elems(|"x")> in-ty_lift*;
         out-rec  := <lift-elem-rec(|"y")> (1, out-ty_lift);
         rec-op   := <norm-set-ast-type(|rec_outty)> COp(x, in-rec);
         rec      := CEqual(rec-op, out-rec, NoMessage());
         h        := <norm-set-ast-type(|ty)> F(x, inps, outp)

  lift-base:
      (ty, IdLift())
      -> <mk-Wld(|ty)>
  lift-base:
      (LIST(ty), ListLift())
      -> <mk-Nil(|ty)>
  lift-base:
      (TUPLE(tys), TupleLift(lifts))
      -> <mk-Tuple>(<zip(lift-base)> (tys, lifts))


  lift-head-elems(|x) = map-with-index(lift-elem-head(|x))

  lift-elem-head(|x):
      (i, (ty, IdLift()))
      -> <lift-Var(|ty, x)> i
  lift-elem-head(|x):
      (i, (LIST(ty), ListLift()))
      -> <mk-ListTail>([<lift-Var(|ty,x)> i], <lift-Var(|LIST(ty), $[[x]s])> i)
  lift-elem-head(|x):
      (i, (TUPLE(tys), TupleLift(lifts)))
      -> <mk-Tuple>(<zip;lift-head-elems(|<lift-name(|x)> i)> (tys, lifts))


  lift-call-elems(|x) = map-with-index(lift-elem-call(|x))

  lift-elem-call(|x):
      (i, (ty, IdLift()))
      -> <lift-Var(|ty, x)> i
  lift-elem-call(|x):
      (i, (LIST(ty), ListLift()))
      -> <lift-Var(|ty,x)> i
  lift-elem-call(|x):
      (i, (TUPLE(tys), TupleLift(lifts)))
      -> <mk-Tuple>(<zip;lift-call-elems(|<lift-name(|x)> i)> (tys, lifts))


  lift-rec-elems(|x) = map-with-index(lift-elem-rec(|x))

  lift-elem-rec(|x):
      (i, (ty, IdLift()))
      -> <lift-Var(|ty, x)> i
  lift-elem-rec(|x):
      (i, (LIST(ty), ListLift()))
      -> <lift-Var(|LIST(ty), $[[x]s])> i
  lift-elem-rec(|x):
      (i, (TUPLE(tys), TupleLift(lifts)))
      -> <mk-Tuple>(<zip;lift-rec-elems(|<lift-name(|x)> i)> (tys, lifts))

  lift-name(|x) = !$[[x]_[<int-to-string>]]

  lift-Var(|ty,x) = lift-name(|x); mk-Var(|ty)

module statix/lang/scope-extension/constraints

imports

  nabl2/api

  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-

  pp

rules

  explicate-ext-constraints(|a): c* -> c'''*
    with (b*, c'*) := <partition(ext-explicate-barrier(|a))> c*
       ; <ext-log(prettyprint-Statix-ExtBarriers|"barriers:")> b*
       ; c''* := <map(try(ext-escape-constraint(|a, b*)))> c'*
       ; c'''* := <map(ext-explicate-constraint(|a))> c''*


  ext-explicate-barrier(|a): ExtBarrier(x) -> s
    with s := <nabl2-get-ast-property(|a,"scope")> x


  ext-escape-constraint(|a, b*): ExtProvides(v, s, o) -> ExtProvidesEsc(v, s, o)
    where <ext-var-escapes(|a, b*)> v

  ext-escape-constraint(|a, b*): ExtRequires(v, s, o) -> ExtRequiresEsc(v, s, o)
    where <ext-var-escapes(|a, b*)> v

  ext-var-escapes(|a, b*): ExtVar(x) -> <id>
    with if r := <nabl2-get-ast-property(|a, "ref")> x
         ; (d, p) := <nabl2-get-resolved-name(|a)> r then
           s* := <ext-path-scopes> p
         else
           s* := []
         end
    where [_|_] := <isect> (s*, b*)


  ext-explicate-constraint(|a) =
    ExtProvides(ext-explicate-set(|a), ext-explicate-set(|a), id)

  ext-explicate-constraint(|a) =
    ExtRequires(ext-explicate-set(|a), ext-explicate-set(|a), id)

  ext-explicate-constraint(|a) =
    ExtIsCovered(ext-explicate-set(|a), id)

  ext-explicate-constraint(|a) =
    ExtProvidesEsc(ext-explicate-set(|a), ext-explicate-set(|a), id)

  ext-explicate-constraint(|a) =
    ExtRequiresEsc(ext-explicate-set(|a), ext-explicate-set(|a), id)


  ext-explicate-set(|a): ExtParam(x, i) -> ExtParam(d, i)
    with d := <nabl2-get-ast-property(|a, "decl")> x

  ext-explicate-set(|a): ExtVar(x) -> ExtVar(d)
    with d := <nabl2-get-ast-property(|a, "decl")> x

  ext-explicate-set(|a): ExtLit(_) -> <id>

  ext-explicate-set(|a): ExtAll() -> <id>


  ext-get-id: ExtParam(d, i) -> <try(nabl2-get-occurrence-name)> d

  ext-get-id: ExtVar(d) -> <try(nabl2-get-occurrence-name)> d


  ext-get-barrier(|name): t -> b
    with b := <nabl2-copy-ast-index(|t)> name


  ext-path-scopes = nabl2-get-path-scopes; init

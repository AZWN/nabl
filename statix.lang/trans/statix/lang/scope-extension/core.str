module statix/lang/scope-extension/core

imports

  nabl2/api

  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-
  statix/lang/scope-extension/-

rules // collect extension constraints

  ext-constraints: Rule(C(x, arg*), _, body) -> c
    with c := < add-indices
              ; filter(param-ext-constraints(|x))
              ; combine-ext-constraints
              > arg*

  param-ext-constraints(|x): (i, t) -> c
    where (term-ext, term-c) := <term-ext> t
    with param-ext := ParamExt(x, i)
       ; origin := <nabl2-get-ast-index> x
       ; provide* := [Provide(term-ext, [()])]
         // Provide(param-ext, term-ext) for exporting permissions
         // But need to figure out how to solve those, since a constriant
         // should only provide permission if *all* its rules to so.
       ; require* := [Require(term-ext, param-ext, origin)]
       ; param-c := (provide*, require*)
       ; c := <combine-ext-constraints> [term-c, param-c]


  ext-constraints: CNew(vs) -> c
    with (term-ext*, term-c*) := <map(term-ext);unzip> vs
       ; provide* := <map(!Provide(<id>,[()]))> term-ext*
       ; new-c := (provide*, [])
       ; c := <combine-ext-constraints> [new-c|term-c*]

  ext-constraints: CTellEdge(t, lbl, _) -> c
    where (term-ext, term-c) := <term-ext> t
    with origin := <nabl2-get-ast-index> t
       ; require* := [Require([lbl], term-ext, origin)]
       ; edge-c := ([], require*)
       ; c := <combine-ext-constraints> [term-c, edge-c]

  ext-constraints: CTellRel(lbl, _, t) -> c
    where (term-ext, term-c) := <term-ext> t
    with origin := <nabl2-get-ast-index> t
       ; require* := [Require([lbl], term-ext, origin)]
       ; data-c := ([], require*)
       ; c := <combine-ext-constraints> [term-c, data-c]

  // FIXME this also matches rule heads!
  ext-constraints: C(x, arg*) -> c
    with c := < add-indices
              ; filter(arg-ext-constraints(|x))
              ; combine-ext-constraints
              > arg*

  arg-ext-constraints(|x): (i, t) -> c
    where (term-ext, term-c) := <term-ext> t
    with param-ext := ParamExt(x, i)
       ; origin := <nabl2-get-ast-index> t
       ; require* := [Require(param-ext, term-ext, origin)]
       ; arg-c := ([], require*)
       ; c := <combine-ext-constraints> [term-c, arg-c]


  term-ext: Var(x) -> (ext, c)
    with ext := <with-origin(|x)> VarExt(x)
       ; c := ([], [])


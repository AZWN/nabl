module statix/lang/scope-extension/sugar

imports

  nabl2/api

  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-
  statix/lang/scope-extension/-

rules // collect extension constraints

  ext-constraints: CDeriveMap(x, x', lift*) -> c
    with c := < add-indices
              ; filter(lift-ext-constraints(|x, x'))
              ; combine-ext-constraints
              > lift*

  ext-constraints: FDeriveMap(x, x', inlift*, outlift) -> c
    with c := < add-indices
              ; filter(lift-ext-constraints(|x, x'))
              ; combine-ext-constraints
              > inlift*

  lift-ext-constraints(|x, x'): (i, IdLift()) -> c
    with provide* := []
       ; ext := <with-origin(|x)> ParamExt(x, i)
       ; ext' := <with-origin(|x')> ParamExt(x', i)
       ; require* := [Require(ext, ext')]
       ; c := (provide*, require*)


  ext-constraints: Rule(F(x, inarg*, outarg), _, _) -> c
    with arg* := <conc> (inarg*, [outarg])
       ; c := < add-indices
              ; filter(param-ext-constraints(|x))
              ; combine-ext-constraints
              > arg*


  ext-constraints: COp(x, arg*) -> c
    with c := < add-indices
              ; filter(arg-ext-constraints(|x))
              ; combine-ext-constraints
              > arg*


  term-ext: COp(x, inarg*) -> (ext, c)
    with ext := <with-origin(|x)> ParamExt(x, <length;inc> inarg*)
       ; c := ([], [])

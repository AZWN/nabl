module generate

imports

  libstrc

  libspoofax/core/language
  
  nabl2/api

  signatures/-
  statics/-

rules

  generate-aterm:
    (_, _, ast@Module(name,_), path, project-path) -> None()
    where a := <nabl2-get-resource-analysis> $[[project-path]/[path]];
          <nabl2-analysis-has-errors> a

  generate-aterm:
    (_, _, Module(name,_), path, project-path) -> (spec-file, spec)
    where a := <nabl2-get-resource-analysis> $[[project-path]/[path]];
          not(<nabl2-analysis-has-errors> a)
    with (ast, ext) := <nabl2-get-custom-analysis;strip-annos;eliminate-all> a;
         rule* := <collect-all(?Rule(_, _, _, _, _))> ast;
         label* := <collect-all(?Labels(<id>));concat> ast;
         rel* := <collect-all(?RelDecl(<id>, _))> ast;
         spec-file := <src-gen-file(|"statix/", ".spec.aterm")> name;
         spec := (label*, rel*, rule*, ext)
 
  src-gen-file(|sub-dir, ext): name -> file-name
    with file-name := $[[<src-gen-dir(|sub-dir)>]/[name][ext]]

  src-gen-dir(|sub-dir): _ -> dir-name
    with dir-name := <language-srcgen-dir> sub-dir

rules

  eliminate-all =
    eliminate-ascribe

  eliminate-ascribe = topdown(try(\ Ascribe(t, _) -> t \))

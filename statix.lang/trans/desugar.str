module desugar

imports

  signatures/-

rules

  desugar-all =
    desugar-params;
    desugar-funrels;
    desugar-reldecls;
    desugar-constraints;
    desugar-rules

  desugar-params = try(sometd(desugar-param))
  desugar-param: Param(sort) -> Param(In(), sort)

  // HACK: we cannot type check this uniformly, so for now concat here
  desugar-funrels = try(sometd(desugar-funrel))
  desugar-funrel: FunctionalRelation(ins,out) -> SimpleRelation(types)
    with types := <conc> (ins, [out])

  desugar-reldecls = try(sometd(desugar-reldecl))
  desugar-reldecl: DefaultRelScope() -> GlobalRel()

  desugar-rules = try(sometd(desugar-rule))
  desugar-rule: Rule(h) -> Rule(h, NoLocalVars(), CTrue(), CTrue())
  desugar-rule: Rule(h, vs, b) -> Rule(h, vs, CTrue(), b)

  desugar-constraints = try(sometd(desugar-constraint))
  desugar-constraint: CPathDisamb(ord, p1, p2) -> CPathLt(ord, p1, p2)

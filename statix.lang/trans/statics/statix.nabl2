module statics/statix

imports

  signatures/-

/** How to deal with AST terms?
 *
 * Terms from the AST are somehwat special, as they are indexed. We want
 * to record information per index, and same AST terms are sometimes considered
 * different based on location even though the terms are the same.
 *
 * This is an issue when recording the scope of an expression, or when
 * taking the index of something.
 */

signature

  sorts
    TClass
    SKind

  constructors
    SimpleSort : SKind
    SortAlias  : SKind

    OccurrenceEdge : TClass
    DirectEdge     : TClass
    NamedEdge      : TClass
    CanQuery       : TClass

  namespaces
    C
    Lang
    Lbl
    Ns
    Op
    Rel
    Sort
    Query

  relations
    in  : Type * TClass

  name resolution
    labels P I
    well-formedness P* I?
    order D < P, I < P

  functions
    normRelType : TYPE -> TYPE {
      REL([ty]) -> ty,
      REL(tys)  -> TUPLE(tys)
    }

rules

  init ^ (s) :=
    new s,

    (SCOPE(),      SCOPE()     ) <in! DirectEdge(),
    (OCCURRENCE(), SCOPE()     ) <in! OccurrenceEdge(),
    (SCOPE(),      OCCURRENCE()) <in! OccurrenceEdge(),
    (OCCURRENCE(), SCOPE()     ) <in! NamedEdge(),
    (SCOPE(),      OCCURRENCE()) <in! NamedEdge(),

    OCCURRENCE() <in! CanQuery().

/******************************************************************
 * Modules                                                        *
 ******************************************************************/

  [[ Module(x, sections) ^ (s) ]] :=
    new s',
    Lang{x} <- s, Lang{x} ===> s',
    Lang{x} -> s, Lang{x} <=== s',
    Map1[[ sections ^ (s') ]],
    (D(s')/C    lsect/name (W(s')/C    minus D(s')/C   )) subseteq 0 | error $[Duplicate constraint [NAME]] @NAMES,
    (D(s')/Lbl  lsect/name (W(s')/Lbl  minus D(s')/Lbl )) subseteq 0 | error $[Duplicate relation [NAME]] @NAMES,
    (D(s')/Ns   lsect/name (W(s')/Ns   minus D(s')/Ns  )) subseteq 0 | error $[Duplicate namespace [NAME]] @NAMES,
    (D(s')/Op   lsect/name (W(s')/Op   minus D(s')/Op  )) subseteq 0 | error $[Duplicate constructor [NAME]] @NAMES,
    (D(s')/Rel  lsect/name (W(s')/Rel  minus D(s')/Rel )) subseteq 0 | error $[Duplicate relation [NAME]] @NAMES,
    (D(s')/Sort lsect/name (W(s')/Sort minus D(s')/Sort)) subseteq 0 | error $[Duplicate sort [NAME]] @NAMES.

  [[ Signature(sigs) ^ (s) ]] :=
    Map1[[ sigs ^ (s) ]].

  [[ Rules(rs) ^ (s) ]] :=
    Map1[[ rs ^ (s) ]].

/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  [[ Constraints(cs) ^ (s) ]] :=
    Map1[[ cs ^ (s) ]].

  [[ CDecl(x, params, kind) ^ (s) ]] :=
    C{x} <- s,
    C{x} : CON(tys) !,
    Map1T[[ params ^ (s) : tys ]].

  [[ CParam(inOut, sort) ^ (s) : ty ]] :=
    SortRef[[ sort ^ (s) : ty ]].

  [[ Rule(h, vs, g, c) ^ (s) ]] :=
    new s', s' ---> s,
    distinct/name D(s')/Var | error $[Duplicate local variable [NAME]] @NAMES,
    UserConstraint(NonUniqueVarDecl)[[ h ^ (s') ]],
    [[ vs ^ (s') ]],
    [[ g ^ (s') ]],
    [[ c ^ (s') ]].

  [[ NoLocalVars() ^ (s) ]].

  [[ LocalVars(vs) ^ (s) ]] :=
    Map1T(VarDecl)[[ vs ^ (s) : _ ]].

  UserConstraint(V)[[ C(x, args) ^ (s) ]] :=
    C{x} -> s,
    C{x} |-> d,
    d : CON(tys),
    Map1T(Term(V))[[ args ^ (s) : tys ]].

  [[ c@C(_, _) ^ (s) ]] :=
    UserConstraint(VarRef)[[ c ^ (s) ]].

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

  [[ Relations(rdecls) ^ (s) ]] :=
    Map1[[ rdecls ^ (s) ]].

  [[ RelDecl(props, rel, reltype) ^ (s) ]] :=
    RelDecl[[ rel ^ (s) : ty ]],
    [[ reltype ^ (s) : ty ]].

  [[ SimpleRelation(sorts) ^ (s) : REL(tys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : tys ]].

  [[ FunctionalRelation(insorts, outsort) ^ (s) : REL(tys) ]] :=
    Map1T(SortRef)[[ insorts ^ (s) : intys ]],
    SortRef[[ outsort ^ (s) : outty ]],
    tys is `conc` of (intys, [outty]).

  [[ CTellRel(rel, ts, st) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : REL(tys) ]],
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].

  [[ CAskRel(rel, ts, st) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : REL(tys) ]],
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].

  RelDecl[[ Rel(x) ^ (s) : ty ]] :=
    Rel{x} <- s,
    Rel{x} : ty !.

  RelRef[[ Rel(x) ^ (s) : ty ]] :=
    Rel{x} -> s,
    Rel{x} |-> d,
    d : ty.

  RelRef[[ x@Decl() ^ (s) : REL([OCCURRENCE()]) ]].

  RelRef[[ x@Ref() ^ (s) : REL([OCCURRENCE()]) ]].

/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

  [[ CTrue() ^ (_) ]].

  [[ CFalse() ^ (_) ]].

  [[ CConj(c1, c2) ^ (s) ]] :=
    [[ c1 ^ (s) ]],
    [[ c2 ^ (s) ]].

  [[ CNew(vs) ^ (s) ]] :=
    Map1t(VarRef)[[ vs ^ (s) : SCOPE() ]].

  [[ CScope(vs, c) ^ (s) ]] :=
    new s', s' ---> s,
    Map1T(VarDecl)[[ vs ^ (s') : _ ]],
    [[ c ^ (s') ]].

  [[ CEqual(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]].

  [[ CInequal(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]].

/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

  [[ Namespaces(nss) ^ (s) ]] :=
    Map1[[ nss ^ (s) ]].

  [[ NsDecl(x, sorts) ^ (s) ]] :=
    Ns{x} <- s,
    Map1T(SortRef)[[ sorts ^ (s) : tys ]],
    Ns{x} : NS(tys) !.

  [[ NameResolution(params) ^ (s) ]] :=
    Map1[[ params ^ (s) ]].

  [[ Labels(lbls) ^ (s) ]] :=
    Map1(LabelDecl)[[ lbls ^ (s) ]].

  LabelDecl[[ Rel(x) ^ (s) ]] :=
    Lbl{x} <- s,
    Rel{x} <- s.

  [[ ResolutionQuery(r, filter, min) ^ (s) ]] :=
    new s', s' ---> s,
    VarDecl[[ r ^ (s') : OCCURRENCE() ]],
    rty == TUPLE([PATH(), OCCURRENCE()]),
    [[ filter ^ (s', rty) ]],
    [[ min ^ (s', rty) ]].

  [[ CPathMatch(re, p) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]],
    Build[[ p ^ (s) : PATH() ]].

  [[ CPathLt(prs, p1, p2) ^ (s) ]] :=
    Build[[ p1 ^ (s) : PATH() ]],
    Build[[ p2 ^ (s) : PATH() ]],
    Map1[[ prs ^ (s) ]].

  [[ LabelPair(lbl1, lbl2) ^ (s) ]] :=
    LabelRef[[ lbl1 ^ (s) ]],
    LabelRef[[ lbl2 ^ (s) ]].

  LabelRE[[ Empty() ^ (_) ]].
  LabelRE[[ Epsilon() ^ (_) ]].
  LabelRE[[ lbl@Label(_) ^ (s) ]] :=
    LabelRef[[ lbl ^ (s) ]].
  LabelRE[[ Closure(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].
  LabelRE[[ Neg(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].
  LabelRE[[ Concat(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].
  LabelRE[[ And(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].
  LabelRE[[ Or(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].

  LabelDecl[[ Label(x) ^ (s) ]] :=
    Lbl{x} <- s,
    Rel{x} <- s, Rel{x} : REL([SCOPE()]).

  LabelRef[[ Label(x) ^ (s) ]] :=
    Lbl{x} -> s, Lbl{x} |-> _.

  LabelRef[[ EOP() ^ (s) ]].

  [[ CTellDirectEdge(t1, lbl, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty1 ]],
    LabelRef[[ lbl ^ (s) ]],
    Build[[ t2 ^ (s) : ty2 ]],
    (ty1, ty2) <in? DirectEdge().

  [[ CTellNamedEdge(t1, lbl, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty1 ]],
    LabelRef[[ lbl ^ (s) ]],
    Build[[ t2 ^ (s) : ty2 ]],
    (ty1, ty2) <in? NamedEdge().

/******************************************************************
 * Resolve & Query
 ******************************************************************/

  [[ CResolve(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : OCCURRENCE() ]],
    Build[[ t2 ^ (s) : LIST(TUPLE([PATH(), OCCURRENCE()])) ]].

  [[ CResolveIn(t1, t2, t3) ^ (s) ]] :=
    Build[[ t1 ^ (s) : OCCURRENCE() ]],
    Build[[ t2 ^ (s) : SCOPE() ]],
    Build[[ t3 ^ (s) : LIST(TUPLE([PATH(), OCCURRENCE()])) ]].

  [[ CQuery(target, filter, min, st, pt) ^ (s) ]] :=
    [[ target ^ (s, rty) ]],
    [[ filter ^ (s, rty) ]],
    [[ min ^ (s, rty) ]],
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ pt ^ (s) : LIST(rty) ]].

  [[ EmptyTarget() ^ (s, rty) ]] :=
    rty == PATH().

  [[ RelTarget(rel) ^ (s, rty) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    ty' is normRelType of ty,
    rty == TUPLE([PATH(),ty']).

  [[ NoFilter() ^ (s, rty) ]].

  [[ Filter(p, c) ^ (s, rty) ]] :=
    new s', s' ---> s,
    Match[[ p ^ (s') : rty ]],
    [[ c ^ (s') ]].

  [[ NoMin() ^ (s, rty) ]].

  [[ Min(mins) ^ (s, rty) ]] :=
    Map2[[ mins ^ (s, rty) ]].

  [[ MinClause(p1, p2, c) ^ (s, rty) ]] :=
    new s', s' ---> s,
    Match[[ p1 ^ (s') : rty ]],
    Match[[ p2 ^ (s') : rty ]],
    [[ c ^ (s') ]].

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

  [[ Sorts(sorts) ^ (s) ]] :=
    Map1(SortDecl)[[ sorts ^ (s) ]].

  SortDecl[[ SortDecl(x) ^ (s) ]] :=
    Sort{x} <- s,
    Sort{x} : SORT(Sort{x}) !!,
    Sort{x}.kind := SimpleSort().

  SortDecl[[ SortAlias(x, sort) ^ (s) ]] :=
    Sort{x} <- s,
    Sort{x} : ty !!,
    Sort{x}.kind := SortAlias(),
    SortRef[[ sort ^ (s) : ty ]].

  [[ Constructors(ctors) ^ (s) ]] :=
    Map1[[ ctors ^ (s) ]].

  [[ OpDecl(x, sort) ^ (s) ]] :=
    Op{x} <- s,
    Op{x} : ty !!,
    OpSort[[ sort ^ (s) : ty ]].

  OpSort[[ ConstOp(sort) ^ (s) : OP([], ty) ]] :=
    RealSortRef[[ sort ^ (s) : ty ]].

  OpSort[[ ArrowOp(args, sort) ^ (s) : OP(tys, ty) ]] :=
    Map1T(SortRef)[[ args ^ (s) : tys ]],
    RealSortRef[[ sort ^ (s) : ty ]].

  RealSortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s, Sort{x} |-> d,
    d : ty !,
    d.kind := k, k != SortAlias() | error $[Cannot refer to alias [d]].

  SortRef[[ IntSort() ^ (_) : INT() ]].
  SortRef[[ StringSort() ^ (_) : STRING() ]].
  SortRef[[ OccurrenceSort() ^ (_) : OCCURRENCE() ]].
  SortRef[[ PathSort() ^ (_) : PATH() ]].
  SortRef[[ ScopeSort() ^ (_) : SCOPE() ]].
  SortRef[[ TupleSort(sorts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : tys ]].
  SortRef[[ ListSort(sort) ^ (s) : LIST(ty) ]] :=
    SortRef[[ sort ^ (s) : ty ]].
  SortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s,
    Sort{x} |-> d,
    d : ty.

rules

  Match[[ t ^ (s) : ty ]] :=
    Term(VarDecl)[[ t ^ (s) : ty ]].

  Build[[ t ^ (s) : ty ]] :=
    Term(VarRef)[[ t ^ (s) : ty ]].

  NonUniqueVarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x @s} <- s,
    Var{x @s} : ty !.

  NonUniqueVarDecl[[ Wld() ^ (_) : _ ]].

  VarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x} <- s,
    Var{x} : ty !.

  VarDecl[[ Wld() ^ (_) : _ ]].

  VarRef[[ Var(x) ^ (s) : ty ]] :=
    Var{x} -> s,
    Var{x} |-> d,
    d : ty.

  VarRef[[ Wld() ^ (_) : _ ]].

  Term(V)[[ v@Var(_) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]].
  Term(V)[[ Wld() ^ (_) : ty ]].
  Term(V)[[ List(ts) ^ (s): LIST(ty) ]] :=
    Map1t(Term(V))[[ ts ^ (s) : ty ]].
  Term(V)[[ ListTail(ts, t) ^ (s) : LIST(ty) ]] :=
    Map1t(Term(V))[[ ts ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : LIST(ty) ]].
  Term(V)[[ ListLength(t) ^ (s): INT() ]] :=
    Term(V)[[ t ^ (s) : LIST(_) ]].
  Term(V)[[ Tuple(ts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(Term(V))[[ ts ^ (s) : tys ]].
  Term(V)[[ Op(x, args) ^ (s) : ty ]] :=
    Op{x} -> s,
    Op{x} |-> d,
    d : OP(tys, ty),
    Map1T(Term(V))[[ args ^ (s) : tys ]].
  Term(V)[[ Str(_) ^ (_) : STRING() ]].
  Term(V)[[ StrBuild(ts) ^ (s) : STRING() ]] :=
    Map1T(Term(V))[[ ts ^ (s) : _ ]].
  Term(V)[[ Int(_) ^ (_) : INT() ]].
  Term(V)[[ Occurrence(ns, ts, pos) ^ (s) : OCCURRENCE() ]] :=
    Ns{ns} -> s, Ns{ns} |-> d, d : NS(tys),
    Map1T(Term(V))[[ ts ^ (s) : tys ]],
    Pos(V)[[ pos ^ (s) ]].
  Term(V)[[ As(v, t) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : ty ]].
  Term(V)[[ Ascribe(t, sort) ^ (s) : ty ]] :=
    SortRef[[ sort ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : ty ]].

  Term(V)[[ PathSrc(t) ^ (s) : SCOPE() ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].
  Term(V)[[ PathScopes(t) ^ (s) : LIST(SCOPE()) ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].
  Term(V)[[ PathDst(t) ^ (s) : SCOPE() ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].

  Pos(V)[[ Position(v) ^ (s) ]] :=
    V[[ v ^ (s) : ty ]].

rules

rules

  Map1t(X)[[ [x|xs] ^ (s) : ty ]] :=
    X[[ x ^ (s) : ty ]],
    Map1t(X)[[ xs ^ (s) : ty ]].

  Map1t(X)[[ [] ^ (s) : _ ]].

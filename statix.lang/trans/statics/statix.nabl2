module statics/statix

imports

  signatures/-

/** How to deal with AST terms?
 *
 * Terms from the AST are somehwat special, as they are indexed. We want
 * to record information per index, and same AST terms are sometimes considered
 * different based on location even though the terms are the same.
 *
 * This is an issue when recording the scope of an expression, or when
 * taking the index of something.
 */

signature

  sorts
    SKind
    CKind

  constructors
    SimpleSort : SKind
    SortAlias  : SKind

    DeclaredC : CKind
    DerivedC  : CKind

  namespaces
    C
    Lang
    Lbl
    Ns
    Op
    Rel
    Sort
    Query

  relations
    in  : Type * TClass

  name resolution
    labels P I
    well-formedness P* I?
    order D < P, I < P

rules

  init ^ (s) :=
    new s.

/******************************************************************
 * Modules                                                        *
 ******************************************************************/

  [[ Module(x, sections) ^ (s) ]] :=
    new s',
    Lang{x} <- s, Lang{x} ===> s',
    Lang{x} -> s, Lang{x} <=== s',
    Map1[[ sections ^ (s') ]],
    (D(s')/C    lsect/name (W(s')/C    minus D(s')/C   )) subseteq 0 | error $[Duplicate constraint [NAME]] @NAMES,
    distinct/name D(s')/C | error $[Duplicate constraint [NAME]] @NAMES,
    (D(s')/Lbl  lsect/name (W(s')/Lbl  minus D(s')/Lbl )) subseteq 0 | error $[Duplicate label [NAME]] @NAMES,
    distinct/name D(s')/Lbl | error $[Duplicate label [NAME]] @NAMES,
    (D(s')/Ns   lsect/name (W(s')/Ns   minus D(s')/Ns  )) subseteq 0 | error $[Duplicate namespace [NAME]] @NAMES,
    distinct/name D(s')/Ns | error $[Duplicate namespace [NAME]] @NAMES,
    (D(s')/Op   lsect/name (W(s')/Op   minus D(s')/Op  )) subseteq 0 | error $[Duplicate constructor [NAME]] @NAMES,
    distinct/name D(s')/Op | error $[Duplicate constructor [NAME]] @NAMES,
    (D(s')/Rel  lsect/name (W(s')/Rel  minus D(s')/Rel )) subseteq 0 | error $[Duplicate relation [NAME]] @NAMES,
    distinct/name D(s')/Rel | error $[Duplicate relation [NAME]] @NAMES,
    (D(s')/Sort lsect/name (W(s')/Sort minus D(s')/Sort)) subseteq 0 | error $[Duplicate sort [NAME]] @NAMES,
    distinct/name D(s')/Sort | error $[Duplicate sort [NAME]] @NAMES.

  [[ Signature(sigs) ^ (s) ]] :=
    Map1[[ sigs ^ (s) ]].

  [[ Rules(rs) ^ (s) ]] :=
    Map1[[ rs ^ (s) ]].

/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  [[ Constraints(cs) ^ (s) ]] :=
    Map1[[ cs ^ (s) ]].

  [[ CDecl(ind, x, type) ^ (s) ]] :=
    [[ type ^ (s) : ty ]],                     // types
    C{x} <- s,                                 // decl
    C{x} : ty !!,
    C{x}.kind := DeclaredC() !!.

  [[ CDeriveMap(x, x', lsorts) ^ (s) ]] :=
    C{x'} -> s,
    C{x'} |-> d,
    d : ty,
    ty' is `lift-type` of (lsorts, ty),
    C{x} <- s,
    C{x} : ty' !!,
    C{x}.kind := DerivedC() !!.

  [[ Rule(h, gvs, g, bvs, c) ^ (s) ]] :=
    new s', s' ---> s,
    distinct/name D(s')/Var | error $[Duplicate local variable [NAME]] @NAMES,
    RuleHead[[ h ^ (s') ]],
    [[ gvs ^ (s') ]],
    [[ g ^ (s') ]],
    new s'', s'' ---> s',
    distinct/name D(s'')/Var | error $[Duplicate local variable [NAME]] @NAMES,
    (D(s'') lsect/name D(s')) subseteq 0 | error $[Shadowing local variable [NAME]] @NAMES,
    [[ bvs ^ (s'') ]],
    [[ c ^ (s'') ]].

  RuleHead[[ C(x, args) ^ (s) ]] :=
    C{x} -> s, C{x} |-> d,
    d.kind := DeclaredC() | error "Rules can only be defined for declared constraints.",
    d : ty, ty != FUN(_, _) | note "Using `c(ts) = t | ...` for functional constraints is preferred." @x,
    tys is `inout-types` of ty,
    Map1T(Term(NonUniqueVarDecl))[[ args ^ (s) : tys ]].

  RuleHead[[ F(x, inargs, outarg) ^ (s) ]] :=
    C{x} -> s, C{x} |-> d,
    d.kind := DeclaredC() | error "Rules can only be defined for declared constraints.",
    d : ty, ty == FUN(_, _) | error "Use `c(...) | ...` for non-functional constraints.",
    intys is `in-types` of ty,
    outty is `out-type` of ty,
    Map1T(Term(NonUniqueVarDecl))[[ inargs ^ (s) : intys ]],
    Term(NonUniqueVarDecl)[[ outarg ^ (s) : outty ]].

  [[ NoLocalVars() ^ (s) ]].

  [[ LocalVars(vs) ^ (s) ]] :=
    Map1T(VarDecl)[[ vs ^ (s) : _ ]].

  [[ C(x, args) ^ (s) ]] :=
    C{x} -> s, C{x} |-> d,
    d : ty,
    tys is `inout-types` of ty,
    Map1T(Term(VarRef))[[ args ^ (s) : tys ]].

  [[ LC(x) ^ (s) : ty ]] :=
    C{x} -> s, C{x} |-> d, d : ty.

  Term(V)[[ COp(x, args) ^ (s) : outty ]] :=
    C{x} -> s, C{x} |-> d,
    d : ty, ty == FUN(_, _) | error "Inline notation can only be used for functional constraints.",
    intys is `in-types` of ty,
    outty is `out-type` of ty,
    Map1T(Term(V))[[ args ^ (s) : intys ]].

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

  [[ Relations(rdecls) ^ (s) ]] :=
    Map1[[ rdecls ^ (s) ]].

  [[ RelDecl(rel, type) ^ (s) ]] :=
    [[ type ^ (s) : ty ]],
    RelDecl[[ rel ^ (s) : ty ]].

  [[ CTellRel(rel, ts, st) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    tys is `inout-types` of ty,
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].

  [[ CAskRel(rel, ts, st) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    tys is `inout-types` of ty,
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].

  Term(V)[[ AskRelOp(rel, ts, st) ^ (s) : outty ]] :=
    RelRef[[ rel ^ (s) : ty ]], ty == FUN(_, _) | error "Inline notation can only be used for functional relations.",
    intys is `in-types` of ty,
    outty is `out-type` of ty,
    Map1T(Term(V))[[ ts ^ (s) : intys ]],
    Term(V)[[ st ^ (s) : SCOPE() ]].

  RelDecl[[ Rel(x) ^ (s) : ty ]] :=
    Rel{x} <- s,
    Rel{x} : ty !.

  RelRef[[ Rel(x) ^ (s) : ty ]] :=
    Rel{x} -> s,
    Rel{x} |-> d,
    d : ty.

  RelRef[[ x@Decl() ^ (s) : PRED([OCCURRENCE()]) ]].

/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

  [[ CTrue() ^ (_) ]].

  [[ CFalse() ^ (_) ]].

  [[ CConj(c1, c2) ^ (s) ]] :=
    [[ c1 ^ (s) ]],
    [[ c2 ^ (s) ]].

  [[ CNew(vs) ^ (s) ]] :=
    Map1t(VarRef)[[ vs ^ (s) : SCOPE() ]].

  [[ CEqual(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]].

  [[ CInequal(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]].

  [[ LTrue() ^ (_) : PRED(_) ]].

  [[ LFalse() ^ (_) : PRED(_) ]].

  [[ LLam(vs, c) ^ (s) : PRED(tys) ]] :=
    new s', s' ---> s,
    Map1T(Term(NonUniqueVarDecl))[[ vs ^ (s') : tys ]],
    [[ c ^ (s') ]].

  Term(V)[[ NewOp() ^ (s) : SCOPE() ]].

/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

  [[ Namespaces(nss) ^ (s) ]] :=
    Map1[[ nss ^ (s) ]].

  [[ NsDecl(x, sorts) ^ (s) ]] :=
    Ns{x} <- s,
    Map1T(SortRef)[[ sorts ^ (s) : tys ]],
    Ns{x} : NS(tys) !.

  [[ NameResolution(params) ^ (s) ]] :=
    Map1[[ params ^ (s) ]].

  [[ Labels(lbls) ^ (s) ]] :=
    Map1(LabelDecl)[[ lbls ^ (s) ]].

  LabelDecl[[ Rel(x) ^ (s) ]] :=
    Lbl{x} <- s,
    Rel{x} <- s.

  [[ NamespaceQuery(x, filter, min) ^ (s) ]] :=
    Ns{x} -> s, Ns{x} |-> d,
    [[ filter ^ (s, OCCURRENCE()) ]],
    [[ min ^ (s, OCCURRENCE()) ]]. 

  [[ CPathMatch(re, p) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]],
    Build[[ p ^ (s) : PATH() ]].

  [[ CPathLt(prs, p1, p2) ^ (s) ]] :=
    Build[[ p1 ^ (s) : PATH() ]],
    Build[[ p2 ^ (s) : PATH() ]],
    Map1[[ prs ^ (s) ]].

  [[ LPathMatch(re) ^ (s) : PRED([PATH()]) ]] :=
    LabelRE[[ re ^ (s) ]].

  [[ LPathLt(prs) ^ (s) : PRED([PATH(), PATH()]) ]] :=
    Map1[[ prs ^ (s) ]].

  [[ LabelPair(lbl1, lbl2) ^ (s) ]] :=
    LabelRef[[ lbl1 ^ (s) ]],
    LabelRef[[ lbl2 ^ (s) ]].

  LabelRE[[ Empty() ^ (_) ]].
  LabelRE[[ Epsilon() ^ (_) ]].
  LabelRE[[ lbl@Label(_) ^ (s) ]] :=
    LabelRef[[ lbl ^ (s) ]].
  LabelRE[[ Closure(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].
  LabelRE[[ Neg(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].
  LabelRE[[ Concat(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].
  LabelRE[[ And(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].
  LabelRE[[ Or(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].

  LabelDecl[[ Label(x) ^ (s) ]] :=
    Lbl{x} <- s,
    Rel{x} <- s, Rel{x} : PRED([SCOPE()]).

  LabelRef[[ Label(x) ^ (s) ]] :=
    Lbl{x} -> s, Lbl{x} |-> _.

  LabelRef[[ EOP() ^ (s) ]].

  [[ CTellDirectEdge(t1, lbl, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : SCOPE() ]],
    LabelRef[[ lbl ^ (s) ]],
    Build[[ t2 ^ (s) : SCOPE() ]].

  [[ CTellDecl(st, dt) ^ (s) ]] :=
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ dt ^ (s) : OCCURRENCE() ]].

  [[ CTellDeclWithRel(st, dt, rel, rt) ^ (s) ]] :=
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ dt ^ (s) : OCCURRENCE() ]],
    RelRef[[ rel ^ (s) : ty ]],
    OCCURRENCE() is `in-type` of ty,
    outty is `out-type` of ty,
    Build[[ rt ^ (s) : outtype ]].

/******************************************************************
 * Resolve & Query
 ******************************************************************/

  [[ CResolveDecl(rt, st, pst) ^ (s) ]] :=
    Build[[ rt ^ (s) : OCCURRENCE() ]],
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ pst ^ (s) : LIST(TUPLE([PATH(), OCCURRENCE()])) ]].

  [[ CResolveRel(rel, rt, st, pst) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    OCCURRENCE() is `in-type` of ty,
    outty is `out-type` of ty,
    Build[[ rt ^ (s) : OCCURRENCE() ]],
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ pst ^ (s) : LIST(TUPLE([PATH(), outty])) ]].

  [[ CResolveQuery(target, filter, min, st, pt) ^ (s) ]] :=
    [[ target ^ (s, wfty, resty) ]],
    [[ filter ^ (s, wfty) ]],
    [[ min ^ (s, wfty) ]],
    Build[[ st ^ (s) : SCOPE() ]],
    Build[[ pt ^ (s) : LIST(resty) ]].

  [[ EmptyTarget() ^ (s, wfty, outty) ]] :=
    wfty == SCOPE(),
    outty == PATH().

  [[ RelTarget(rel) ^ (s, wfty, outty) ]] :=
    RelRef[[ rel ^ (s) : rty ]],
    wfty is `in-type` of rty,
    inoutty is `inout-type` of rty,
    outty == TUPLE([PATH(), inoutty]).

  [[ Filter(lp, lt) ^ (s, ty) ]] :=
    [[ lp ^ (s) : PRED([PATH()]) ]],
    [[ lt ^ (s) : PRED([ty]) ]].

  [[ Min(lp, lt) ^ (s, ty) ]] :=
    [[ lp ^ (s) : PRED([PATH(), PATH()]) ]],
    [[ lt ^ (s) : PRED([ty, ty]) ]].

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

  [[ Sorts(sorts) ^ (s) ]] :=
    Map1(SortDecl)[[ sorts ^ (s) ]].

  SortDecl[[ SortDecl(x) ^ (s) ]] :=
    Sort{x} <- s,
    Sort{x} : SORT(Sort{x}) !!,
    Sort{x}.kind := SimpleSort().

  SortDecl[[ SortAlias(x, sort) ^ (s) ]] :=
    Sort{x} <- s,
    Sort{x} : ty !!,
    Sort{x}.kind := SortAlias(),
    SortRef[[ sort ^ (s) : ty ]].

  [[ Constructors(ctors) ^ (s) ]] :=
    Map1[[ ctors ^ (s) ]].

  [[ OpDecl(x, sort) ^ (s) ]] :=
    Op{x} <- s,
    Op{x} : ty !!,
    OpSort[[ sort ^ (s) : ty ]].

  OpSort[[ ConstOp(sort) ^ (s) : OP([], ty) ]] :=
    RealSortRef[[ sort ^ (s) : ty ]].

  OpSort[[ ArrowOp(args, sort) ^ (s) : OP(tys, ty) ]] :=
    Map1T(SortRef)[[ args ^ (s) : tys ]],
    RealSortRef[[ sort ^ (s) : ty ]].

  RealSortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s, Sort{x} |-> d,
    d : ty !,
    d.kind := k, k != SortAlias() | error $[Cannot refer to alias [d]].

  SortRef[[ IntSort() ^ (_) : INT() ]].
  SortRef[[ StringSort() ^ (_) : STRING() ]].
  SortRef[[ OccurrenceSort() ^ (_) : OCCURRENCE() ]].
  SortRef[[ PathSort() ^ (_) : PATH() ]].
  SortRef[[ ScopeSort() ^ (_) : SCOPE() ]].
  SortRef[[ TupleSort(sorts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : tys ]].
  SortRef[[ ListSort(sort) ^ (s) : LIST(ty) ]] :=
    SortRef[[ sort ^ (s) : ty ]].
  SortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s,
    Sort{x} |-> d,
    d : ty.

  [[ SimpleType(sorts) ^ (s) : PRED(intys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : intys ]].

  [[ FunType(insorts, outsorts) ^ (s) : FUN(intys, outtys) ]] :=
    Map1T(SortRef)[[ insorts ^ (s) : intys ]],
    Map1T(SortRef)[[ outsorts ^ (s) : outtys ]].

rules

  Match[[ t ^ (s) : ty ]] :=
    Term(VarDecl)[[ t ^ (s) : ty ]].

  Build[[ t ^ (s) : ty ]] :=
    Term(VarRef)[[ t ^ (s) : ty ]].

  NonUniqueVarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x @s} <- s,
    Var{x @s} : ty !.

  NonUniqueVarDecl[[ Wld() ^ (_) : _ ]].

  VarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x} <- s,
    Var{x} : ty !.

  VarDecl[[ Wld() ^ (_) : _ ]].

  VarRef[[ Var(x) ^ (s) : ty ]] :=
    Var{x} -> s,
    Var{x} |-> d,
    d : ty.

  VarRef[[ Wld() ^ (_) : _ ]].

  Term(V)[[ v@Var(_) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]].
  Term(V)[[ Wld() ^ (_) : ty ]].
  Term(V)[[ List(ts) ^ (s): LIST(ty) ]] :=
    Map1t(Term(V))[[ ts ^ (s) : ty ]].
  Term(V)[[ ListTail(ts, t) ^ (s) : LIST(ty) ]] :=
    Map1t(Term(V))[[ ts ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : LIST(ty) ]].
  Term(V)[[ ListLength(t) ^ (s): INT() ]] :=
    Term(V)[[ t ^ (s) : LIST(_) ]].
  Term(V)[[ Tuple(ts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(Term(V))[[ ts ^ (s) : tys ]].
  Term(V)[[ Op(x, args) ^ (s) : ty ]] :=
    Op{x} -> s,
    Op{x} |-> d,
    d : OP(tys, ty),
    Map1T(Term(V))[[ args ^ (s) : tys ]].
  Term(V)[[ Str(_) ^ (_) : STRING() ]].
  Term(V)[[ StrBuild(ts) ^ (s) : STRING() ]] :=
    Map1T(Term(V))[[ ts ^ (s) : _ ]].
  Term(V)[[ Int(_) ^ (_) : INT() ]].
  Term(V)[[ Occurrence(ns, ts, pos) ^ (s) : OCCURRENCE() ]] :=
    Ns{ns} -> s, Ns{ns} |-> d, d : NS(tys),
    Map1T(Term(V))[[ ts ^ (s) : tys ]],
    Pos(V)[[ pos ^ (s) ]].
  Term(V)[[ As(v, t) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : ty ]].
  Term(V)[[ Ascribe(t, sort) ^ (s) : ty ]] :=
    SortRef[[ sort ^ (s) : ty ]],
    Term(V)[[ t ^ (s) : ty ]].

  Term(V)[[ PathSrc(t) ^ (s) : SCOPE() ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].
  Term(V)[[ PathScopes(t) ^ (s) : LIST(SCOPE()) ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].
  Term(V)[[ PathDst(t) ^ (s) : SCOPE() ]] :=
    Term(V)[[ t ^ (s) : PATH() ]].

  Pos(V)[[ Position(v) ^ (s) ]] :=
    V[[ v ^ (s) : ty ]].

  Pos(V)[[ NoPosition() ^ (s) ]].

rules

  Map1t(X)[[ [x|xs] ^ (s) : ty ]] :=
    X[[ x ^ (s) : ty ]],
    Map1t(X)[[ xs ^ (s) : ty ]].

  Map1t(X)[[ [] ^ (s) : _ ]].

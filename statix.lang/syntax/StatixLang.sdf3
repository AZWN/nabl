module StatixLang

imports

  Layout
  Common
  Sugar

template options

  keyword -/- [a-zA-Z0-9\_\-\']

context-free start-symbols

  Start

context-free syntax

  Start = Module


/******************************************************************
 * Modules                                                        *
 ******************************************************************/

context-free syntax

  Module.Module = <
    module <ID>

    <{Section "\n\n"}*>
  > {longest-match}

  Section.Signature = <
    signature

      <{Signature "\n\n"}*>
  > {longest-match}

  Section.Rules = <
    rules

    <{Rule "\n\n"}*>
  > {longest-match}

/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  Signature.Constraints = <
    constraints
      <{CDecl "\n"}*>
  > {longest-match}

  Rule.CDecl       = [[CInd] constraint [ConstraintId] : [{CParam " * "}+]]
  Rule             = <constraint <CDerive>>

  CDecl.CDecl      = [[CInd] [ConstraintId] : [{CParam " * "}+]]
  CDecl            = CDerive

  CDerive.CDeriveMap = [[ConstraintId] maps [ConstraintId]([{LiftSort ", "}*])]

  LiftSort.Id     = "*"
  LiftSort.ToList = "list(*)"

  CInd.InductiveC   =
  CInd.CoinductiveC = <coinductive>

  CParam.CParam = SortRef
  CParam.CParam = <out <SortRef>>

  Rule.Rule = <
    <UserConstraint> | <LocalVars> <Constraint> :- <LocalVars> <Constraint>.
  >

  LocalVars.NoLocalVars = <>
  LocalVars.LocalVars   = <{<{Var " "}+>}>

  Constraint = UserConstraint

  UserConstraint.C = [[ConstraintId]([{Term ", "}*])]

lexical syntax

  ConstraintId = "coinductive" | "maps" {reject}

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

context-free syntax

  Signature.Relations = <
    relations
      <{RelDecl "\n"}*>
  > {longest-match}

  RelDecl.RelDecl = <<{RelProp " "}*> <Rel> : <RelType>>

  Rule = <relation <RelDecl>>

  Rel.Rel = RelationId

  RelRef      = Rel
  RelRef.Decl = "decl"
  RelRef.Ref  = "ref"

  RelType.SimpleRelation     = [[{SortRef " * "}+]]
  RelType.FunctionalRelation = [[{SortRef " * "}+] -> [SortRef]] // FIXME Turning SortRef into a list breaks parsing

  RelProp.Reflexive     = <reflexive>
  RelProp.Irreflexive   = <irreflexive>
  RelProp.Transitive    = <transitive>
  RelProp.NonTransitive = <non-transitive>
  RelProp.Symmetric     = <symmetric>
  RelProp.AntiSymmetric = <anti-symmetric>

syntax

  Constraint-CF.CTellRel = "!" RelRef-CF "[" CommaTerms-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF
  Constraint-CF.CAskRel  = "?" RelRef-CF "[" CommaTerms-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF

lexical syntax

  RelationId = ID
  RelationId = "decl" {reject}
  RelationId = "ref" {reject}

/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

context-free syntax

  Constraint.CTrue  = <true>
  Constraint.CFalse = <false>

  Constraint         = <(<Constraint>)> {bracket}
  Constraint.CConj   = <<Constraint>, <Constraint>> {left}
  Constraint.CNew    = <new <{Var " "}+>>

  WrappedClause.CScope = <{ <{Var " "}+>: <Constraint> }>
  WrappedClause        = <{ <Constraint> }>

  Constraint.CEqual   = [[Term] == [Term]]
  Constraint.CInequal = [[Term] != [Term]]

lexical syntax

  ConstraintId = LCID
  ConstraintId = "true" | "false"
               | "new" | "disamb" {reject}

/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

context-free syntax

  Signature.Namespaces = <
    namespaces
      <{NsDecl "\n"}*>
  > {longest-match}

  Signature.NameResolution = <
    name-resolution
      <{ResolutionParam "\n"}*>
  > {longest-match}

  ResolutionParam.Labels = <labels <{Rel " "}*>>
  ResolutionParam.ResolutionQuery = <resolve ref(<Var>) to decl<Filter><Min>>

  Label.Label = RelationId

  LabelRef     = Label
  LabelRef.EOP = "$"

  NsDecl.NsDecl = <<UCID> : <{SortRef " * "}*>>

  Rule = <namespace <NsDecl>>

  LabelPairs          = <<{LabelPair ","}*>>
  LabelPair.LabelPair = [[LabelRef] < [LabelRef]]

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <e>
  LabelRE            = Label
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Neg        = <~<LabelRE>>
  LabelRE.Concat     = <<LabelRE> <LabelRE>>     {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>             {bracket}

context-free priorities

    LabelRE.Neg
  > LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

syntax

  Term-CF.PathSrc    = Term-CF "." "src"
  Term-CF.PathScopes = Term-CF "." "scopes"
  Term-CF.PathDst    = Term-CF "." "dst"

  Constraint-CF.CPathMatch  = "pathMatch" "[" LAYOUT?-CF LabelRE-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF ")"
  Constraint-CF.CPathLt     = "pathLt" "[" LAYOUT?-CF LabelPairs-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF "," LAYOUT?-CF Term-CF LAYOUT?-CF ")"

  Constraint-CF.CTellDirectEdge = Term-CF LAYOUT?-CF "-" Label-CF "->" LAYOUT?-CF Term-CF
  Constraint-CF.CTellNamedEdge  = Term-CF LAYOUT?-CF "=" Label-CF "=>" LAYOUT?-CF Term-CF

lexical syntax

  ConstraintId = "pathMatch" | "pathDisamb" {reject}

/******************************************************************
 * Resolve & Query
 ******************************************************************/

context-free syntax

  Constraint.CResolve   = [[Term] |-> [Term]]
  Constraint.CResolveIn = [[Term] in [Term] |-> [Term]]

  Constraint.CQuery = <query <QueryTarget><Filter><Min> in <Term> to <Term>>

  QueryTarget.RelTarget   = RelRef
  QueryTarget.EmptyTarget = "()"

  Filter.NoFilter =
  Filter.Filter   = < filter { <Term> : <Constraint> }>

  Min.NoMin =
  Min.Min   = < min <{MinClause "; "}+>>

  MinClause.MinClause = <{ <Term>, <Term> : <Constraint> }>

lexical syntax

  QueryId = ID
  QueryId = "ref" {reject}

  RelationId = "e" {reject}

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

context-free syntax

  Signature.Sorts = <
    sorts
      <{SortDecl "\n"}*>
  > {longest-match}

  Signature.Constructors = <
    constructors
      <{OpDecl "\n"}*>
  > {longest-match}

  SortDecl.SortDecl  = SortId
  SortDecl.SortAlias = <<SortId> = <SortRef>>

  Sort.SimpleSort = SortId

  SortRef                = Sort
  SortRef.ListSort       = <list(<SortRef>)>
  SortRef.TupleSort      = <(<{SortRef " * "}*>)>
  SortRef.IntSort        = "int"
  SortRef.StringSort     = "string"
  SortRef.PathSort       = "path"
  SortRef.OccurrenceSort = "occurrence"
  SortRef.ScopeSort      = "scope"

  OpDecl.OpDecl  = <<SID> : <OpSort>>

  OpSort.ConstOp = Sort
  OpSort.ArrowOp = [[{SortRef " * "}+] -> [Sort]]

lexical syntax

  SortId = UCID
  SortId = "list" | "int" | "string" | "occurrence" | "scope" | "path" | "in" | "out" {reject}

/******************************************************************
 * Terms                                                          *
 ******************************************************************/

context-free syntax

  Var.Var = VARID
  Var.Wld = <_>

  Term            = Var
  Term.Int        = INT
  Term.List       = <[<{Term ", "}*>]>
  Term.ListTail   = <[<{Term ", "}*>|<Term>]>
  Term.Tuple      = <(<{Term ", "}*>)>
  Term            = Occurrence
  Term.As         = <<Var>@<Term>>
  Term.Ascribe    = <<Term> : <SortRef>>

  CommaTerms = <<{Term ", "}*>>
  SpaceTerms = <<{Term " "}*>>

syntax

  Term-CF.ListLength = Term-CF "." "length"
  Term-CF.Op         = UCID-LEX "(" LAYOUT?-CF CommaTerms-CF LAYOUT?-CF ")"
  Term-CF.Str        = "\"" StrChars-LEX "\""
  Term-CF.StrBuild   = "$" "[" StrBuild-CF* "]"

  StrBuild-CF.Str2 = StrBuildChars-LEX
  StrBuild-CF      = "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"

  Occurrence-CF.Occurrence = UCID-LEX "{" LAYOUT?-CF SpaceTerms-CF LAYOUT?-CF Position-CF LAYOUT?-CF "}"

  Position-CF.Position = "@" Var-CF

lexical syntax

  StrChars   = StrChar*
  StrChar    = ~[\"\\\t\r\n]
  StrChar    =  "\\\"" | "\\\\" | "\\t" | "\\r" | "\\n"

  StrBuildChars = StrBuildChar+
  StrBuildChar  = ~[\[\]\\\t\r\n]
  StrBuildChar  =  "\\[" | "\\]" | "\\\\" | "\\t" | "\\r" | "\\n"

lexical restrictions

  StrChars -/- ~[\"]

  StrBuildChars -/- ~[\[\]]

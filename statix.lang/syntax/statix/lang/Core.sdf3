module statix/lang/Core

imports

  statix/lang/Common

/******************************************************************
 * Modules                                                        *
 ******************************************************************/

context-free syntax

  Module.Module = <
    module <ModuleId>

    <{Section "\n\n"}*>
  > {longest-match}

  Section.Signature = <
    signature

      <{Signature "\n\n"}*>
  > {longest-match}

  Section.Rules = <
    rules

      <{Rule "\n\n"}*>
  > {longest-match}

/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  Signature.Constraints = <
    constraints
      <{CDecl "\n"}*>
  > {longest-match}

  CDecl.CDecl      = [[CInd][ConstraintId] : [Type]]

  CInd.InductiveC   =
  CInd.CoinductiveC = <coinductive >

  Rule.Rule = <
    <RuleHead> :- <LocalVars>
      <Constraint>.
  >

  RuleHead   = UserConstraint

  LocalVars = <{<{Var " "}*>}>

  Constraint = UserConstraint

  UserConstraint.C = [[ConstraintId]([{Term ", "}*])]

  HoConstraint.LLam = <{ <{Term ", "}*> :- <LocalVars> <Constraint> }>

lexical syntax

  ConstraintId = LCID
  ConstraintId = Keyword {reject}

  Keyword = "coinductive"

/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

context-free syntax

  Constraint.CTrue  = <true>
  Constraint.CFalse = <false>

  Constraint         = <(<Constraint>)> {bracket}
  Constraint.CConj   = <
    <Constraint>,
    <Constraint>
  > {right}
  Constraint.CNew    = <new <{Var " "}+>>

  Constraint.CEqual   = [[Term] == [Term]]
  Constraint.CInequal = [[Term] != [Term]]

  Constraint.CTermId = <termId(<Term>, <Term>)>

lexical syntax

  Keyword = "true" | "false" | "new" | "termId"

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

context-free syntax

  Signature.Relations = <
    relations
      <{RelDecl "\n"}*>
  > {longest-match}

  RelDecl.RelDecl = <<Label> : <Type>>

  Rule = <relation <RelDecl>>

syntax

  Constraint-CF.CTellRel = "!" LabelRef-CF "[" CommaTerms-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF

lexical syntax

  RelationId = ID
  RelationId = Keyword {reject}

  Keyword = "in"

/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

context-free syntax

  Signature.Namespaces = <
    namespaces
      <{NsDecl "\n"}*>
  > {longest-match}

  Signature.NameResolution = <
    name-resolution
      <{ResolutionParam "\n"}*>
  > {longest-match}

  // labels

  ResolutionParam.Labels = <labels <{Label " "}*>>

  Label.Label = RelationId

  LabelRef      = Label
  LabelRef.Decl = "decl"
  LabelRef.EOP  = "$"

  // namespaces

  NsDecl.NsDecl = <<NamespaceId> : <{SortRef " * "}*>>

  Rule = <namespace <NsDecl>>

  LabelPairs          = <<{LabelPair ", "}*>>
  LabelPair.LabelPair = [[LabelRef] < [LabelRef]]
  LabelOrd            = LabelPairs

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <e>
//LabelRE.Any        = <.>
  LabelRE            = LabelRef
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Neg        = <~<LabelRE>>
  LabelRE.Concat     = <<LabelRE> <LabelRE>>     {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>             {bracket}

context-free priorities

    LabelRE.Neg
  > LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

context-free syntax

  Term = Occurrence
  Term = Path

  Occurrence = OccurrenceLit // this indirection is necessary to solve parse errors
  Path       = PathLit

  Position.NoId = "-"
  Position      = Var


syntax

  OccurrenceLit-CF.Occurrence = NamespaceId-LEX "{" LAYOUT?-CF SpaceTerms-CF LAYOUT?-CF "@" Position-CF LAYOUT?-CF "}"

  PathLit-CF.PathEmpty = "_PathEmpty" "(" LAYOUT?-CF Term-CF LAYOUT?-CF ")"
  PathLit-CF.PathStep  = "_PathStep" "(" LAYOUT?-CF Term-CF LAYOUT?-CF "," LAYOUT?-CF Term-CF LAYOUT?-CF "," LAYOUT?-CF Term-CF LAYOUT?-CF ")"

  Constraint-CF.CPathMatch  = "pathMatch" "[" LAYOUT?-CF LabelRE-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF ")"
  Constraint-CF.CPathLt     = "pathLt" "[" LAYOUT?-CF LabelOrd-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF "," LAYOUT?-CF Term-CF LAYOUT?-CF ")"

  Constraint-CF.CTellEdge = Term-CF LAYOUT?-CF "-" Label-CF "->" LAYOUT?-CF Term-CF

lexical syntax

  NamespaceId = UCID

  Keyword = "pathMatch" | "pathLt"

  RelationId = "e" {reject}                     // ambiguous with empty regex
  RelationId = "resolve" | "labels" {reject}    // ambiguous with signature keywords

/******************************************************************
 * Resolve & Query
 ******************************************************************/

context-free syntax

  Constraint.CResolveQuery = [
    query
     [QueryFilter]
     [QueryMin]
      in [Term] |-> [Term]
   ]

  QueryFilter.Filter = < filter <LabelRE> and <HoConstraint>>
  QueryMin.Min       = < min <LabelOrd> and <HoConstraint>>

lexical syntax

  Keyword = "filter" | "min" | "and"

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

context-free syntax

  Signature.Sorts = <
    sorts
      <{SortDecl "\n"}*>
  > {longest-match}

  Signature.Constructors = <
    constructors
      <{OpDecl "\n"}*>
  > {longest-match}

  SortDecl.SortDecl  = SortId
  SortDecl.SortAlias = <<SortId> = <SortRef>>

  Sort.SimpleSort = SortId

  SortRef                = Sort
  SortRef.ListSort       = <list(<SortRef>)>
  SortRef.Tuple0Sort     = <()>
  SortRef.TupleNSort     = <(<SortRef> * <{SortRef " * "}+>)>
  SortRef.IntSort        = "int"
  SortRef.StringSort     = "string"
  SortRef.PathSort       = "path"
  SortRef.LabelSort      = "label"
  SortRef.OccurrenceSort = "occurrence"
  SortRef.ScopeSort      = "scope"
  SortRef.TermIdSort     = "termId"
  SortRef                = <(<SortRef>)> {bracket}

  OpDecl.OpDecl  = <<OpId> : <OpSort>>

  OpSort.ConstOp = Sort
  OpSort.ArrowOp = [[{SortRef " * "}+] -> [Sort]]

  Type.SimpleType = [[{SortRef " * "}*]]
  Type.FunType    = [[{SortRef " * "}*] -> [{SortRef " * "}+]]

lexical syntax

  SortId = STRID
  SortId = "list" | "int" | "string" | "occurrence" | "scope" | "path" | "label" | "termId" {reject}

  OpId = STRID

/******************************************************************
 * Terms                                                          *
 ******************************************************************/

context-free syntax

  Var.Var = VARID
  Var.Wld = <_>

  Term            = Var
  Term.Int        = INT
  Term.List       = <[<{Term ", "}*>]>
  Term.ListTail   = <[<{Term ", "}*>|<Term>]>
  Term.Tuple0     = <()>
  Term.TupleN     = <(<Term>, <{Term ", "}+>)>
  Term.As         = <<Var>@<Term>>
  Term.Ascribe    = <<Term> : <SortRef>>
  Term            = <(<Term>)> {bracket}

  CommaTerms = <<{Term ", "}*>>
  SpaceTerms = <<{Term " "}*>>

context-free priorities

    Term.As
  > Term.Ascribe

syntax

//Term-CF.ListLength = Term-CF "." "length"
  Term-CF.Op         = OpId-LEX "(" LAYOUT?-CF CommaTerms-CF LAYOUT?-CF ")"
  Term-CF.Str        = "\"" StrChars-LEX "\""
//Term-CF.StrBuild   = "$" "[" StrBuild-CF* "]"

  StrBuild-CF.Str2 = StrBuildChars-LEX
  StrBuild-CF      = "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"

lexical syntax

  StrChars   = StrChar*
  StrChar    = ~[\"\\\t\r\n]
  StrChar    =  "\\\"" | "\\\\" | "\\t" | "\\r" | "\\n"

  StrBuildChars = StrBuildChar+
  StrBuildChar  = ~[\[\]\\\t\r\n]
  StrBuildChar  =  "\\[" | "\\]" | "\\\\" | "\\t" | "\\r" | "\\n"

lexical restrictions

  StrChars -/- ~[\"]

  StrBuildChars -/- ~[\[\]]

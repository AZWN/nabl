module Core

imports

  Common

/******************************************************************
 * Modules                                                        *
 ******************************************************************/

context-free syntax

  Module.Module = <
    module <ID>

    <{Section "\n\n"}*>
  > {longest-match}

  Section.Signature = <
    signature

      <{Signature "\n\n"}*>
  > {longest-match}

  Section.Rules = <
    rules

      <{Rule "\n\n"}*>
  > {longest-match}

/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  Signature.Constraints = <
    constraints
      <{CDecl "\n"}*>
  > {longest-match}

  CDecl.CDecl      = [[CInd][ConstraintId] : [Type]]

  CInd.InductiveC   =
  CInd.CoinductiveC = <coinductive >

  Rule.Rule = <
    <RuleHead> | <LocalVars> <Constraint> :- <LocalVars>
      <Constraint>.
  >

  RuleHead   = UserConstraint

  LocalVars = <{<{Var " "}*>}>

  Constraint = UserConstraint

  UserConstraint.C = [[ConstraintId]([{Term ", "}*])]

  HoConstraint.LC = ConstraintId

lexical syntax

  ConstraintId = LCID
  ConstraintId = Keyword {reject}

  Keyword = "coinductive"

/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

context-free syntax

  Constraint.CTrue  = <true>
  Constraint.CFalse = <false>

  Constraint         = <(<Constraint>)> {bracket}
  Constraint.CConj   = <
    <Constraint>,
    <Constraint>
  > {left}
  Constraint.CNew    = <new <{Var " "}+>>

  Constraint.CEqual   = [[Term] == [Term]]
  Constraint.CInequal = [[Term] != [Term]]

lexical syntax

  Keyword = "true" | "false" | "new"

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

context-free syntax

  Signature.Relations = <
    relations
      <{RelDecl "\n"}*>
  > {longest-match}

  RelDecl.RelDecl = <<Rel> : <Type>>

  Rule = <relation <RelDecl>>

  Rel.Rel = RelationId

  RelRef = Rel

syntax

  Constraint-CF.CTellRel = "!" RelRef-CF "[" CommaTerms-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF

lexical syntax

  RelationId = ID
  RelationId = Keyword {reject}

  Keyword = "in"

/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

context-free syntax

  Signature.Namespaces = <
    namespaces
      <{NsDecl "\n"}*>
  > {longest-match}

  Signature.NameResolution = <
    name-resolution
      <{ResolutionParam "\n"}*>
  > {longest-match}

  // labels

  ResolutionParam.Labels = <labels <{Label " "}*>>

  Label.Label = RelationId

  LabelRef     = Label
  LabelRef.EOP = "$"

  // default resolution policies
  NamespaceQuery.NamespaceQuery = <resolve <NamespaceId><NamespaceFilter><NamespaceMin>>

  ResolutionParam = NamespaceQuery
  Rule            = NamespaceQuery

  NamespaceFilter.NamespaceFilter = < filter <HoConstraint>>

  NamespaceMin.NamespaceMin = < min <HoConstraint>>

  // namespaces

  NsDecl.NsDecl = <<NamespaceId> : <{SortRef " * "}*>>

  Rule = <namespace <NsDecl>>

  LabelPairs          = <<{LabelPair ","}*>>
  LabelPair.LabelPair = [[LabelRef] < [LabelRef]]

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <e>
  LabelRE            = Label
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Neg        = <~<LabelRE>>
  LabelRE.Concat     = <<LabelRE> <LabelRE>>     {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>             {bracket}

context-free priorities

    LabelRE.Neg
  > LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

context-free syntax

  Constraint.CPathSrc    = <src(<Term>, <Term>)>
  Constraint.CPathDst    = <dst(<Term>, <Term>)>
  Constraint.CPathScopes = <scopes(<Term>, <Term>)>

syntax

  Term-CF.Occurrence = NamespaceId-LEX "{" LAYOUT?-CF SpaceTerms-CF LAYOUT?-CF Position-CF LAYOUT?-CF "}"

  Position-CF.Position   = "@" Var-CF
  Position-CF.NoPosition = "@-"

  Constraint-CF.CPathMatch  = "pathMatch" "[" LAYOUT?-CF LabelRE-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF ")"
  Constraint-CF.CPathLt     = "pathLt" "[" LAYOUT?-CF LabelPairs-CF LAYOUT?-CF "](" LAYOUT?-CF Term-CF LAYOUT?-CF "," LAYOUT?-CF Term-CF LAYOUT?-CF ")"

  QueryFilter-CF.ResolveFilter = LAYOUT?-CF "resolveFilter" "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"
  QueryMin-CF.ResolveMin       = LAYOUT?-CF "resolveMin" "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"

  Constraint-CF.CTellEdge = Term-CF LAYOUT?-CF "-" Label-CF "->" LAYOUT?-CF Term-CF

lexical syntax

  NamespaceId = UCID

  Keyword = "src" | "dst" | "scopes"
          | "pathMatch" | "pathDisamb"
          | "resolveFilter" | "resolveMin"

  RelationId = "e" {reject} // ambiguous with empty regex

/******************************************************************
 * Resolve & Query
 ******************************************************************/

context-free syntax

  Constraint.CResolveQuery = [query [QueryTarget][QueryFilter][QueryMin] in [Term] |-> [Term]]

  QueryTarget.RelTarget   = RelRef
  QueryTarget.EmptyTarget = "()"

  QueryFilter.Filter = < filter <HoConstraint> and <HoConstraint>>
  QueryMin.Min       = < min <HoConstraint> and <HoConstraint>>

lexical syntax

  QueryId = ID

  Keyword = "filter" | "min" | "and"

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

context-free syntax

  Signature.Sorts = <
    sorts
      <{SortDecl "\n"}*>
  > {longest-match}

  Signature.Constructors = <
    constructors
      <{OpDecl "\n"}*>
  > {longest-match}

  SortDecl.SortDecl  = SortId

  Sort.SimpleSort = SortId

  SortRef                = Sort
  SortRef.ListSort       = <list(<SortRef>)>
  SortRef.TupleSort      = <(<{SortRef " * "}*>)>
  SortRef.IntSort        = "int"
  SortRef.StringSort     = "string"
  SortRef.PathSort       = "path"
  SortRef.OccurrenceSort = "occurrence"
  SortRef.ScopeSort      = "scope"

  OpDecl.OpDecl  = <<OpId> : <OpSort>>

  OpSort.ConstOp = Sort
  OpSort.ArrowOp = [[{SortRef " * "}+] -> [Sort]]

  Type.SimpleType = [[{SortRef " * "}*]]
  Type.FunType    = [[{SortRef " * "}*] -> [{SortRef " * "}+]]

lexical syntax

  SortId = UCID
  SortId = "list" | "int" | "string" | "occurrence" | "scope" | "path" {reject}

  OpId = UCID

/******************************************************************
 * Terms                                                          *
 ******************************************************************/

context-free syntax

  Var.Var = VARID
  Var.Wld = <_>

  Term            = Var
  Term.Int        = INT
  Term.List       = <[<{Term ", "}*>]>
  Term.ListTail   = <[<{Term ", "}*>|<Term>]>
  Term.Tuple0     = <()>
  Term.TupleN     = <(<Term>, <{Term ", "}+>)>
  Term.As         = <<Var>@<Term>>
  Term.Ascribe    = <<Term> : <SortRef>>
  Term            = <(<Term>)> {bracket}

  CommaTerms = <<{Term ", "}*>>
  SpaceTerms = <<{Term " "}*>>

syntax

//Term-CF.ListLength = Term-CF "." "length"
  Term-CF.Op         = OpId-LEX "(" LAYOUT?-CF CommaTerms-CF LAYOUT?-CF ")"
  Term-CF.Str        = "\"" StrChars-LEX "\""
//Term-CF.StrBuild   = "$" "[" StrBuild-CF* "]"

  StrBuild-CF.Str2 = StrBuildChars-LEX
  StrBuild-CF      = "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"

lexical syntax

  StrChars   = StrChar*
  StrChar    = ~[\"\\\t\r\n]
  StrChar    =  "\\\"" | "\\\\" | "\\t" | "\\r" | "\\n"

  StrBuildChars = StrBuildChar+
  StrBuildChar  = ~[\[\]\\\t\r\n]
  StrBuildChar  =  "\\[" | "\\]" | "\\\\" | "\\t" | "\\r" | "\\n"

lexical restrictions

  StrChars -/- ~[\"]

  StrBuildChars -/- ~[\[\]]

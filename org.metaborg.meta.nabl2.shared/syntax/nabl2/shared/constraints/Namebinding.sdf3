module nabl2/shared/constraints/Namebinding

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/Messages
  nabl2/shared/common/Resolution
  nabl2/shared/common/Vars

context-free syntax

  Scope                 = Var
  Comparable            = Scope

  Occurrence            = Var
  Comparable            = Occurrence

  MessagePosition.NAMES = <@ NAMES>

context-free syntax

  PrimitiveConstraint.CGDecl       = [[Occurrence] <- [Scope]]
  PrimitiveConstraint.CGRef        = [[Occurrence] -> [Scope]]

  PrimitiveConstraint.CResolve     = [[Occurrence] |-> [Occurrence] [Message]]

syntax

  PrimitiveConstraint-CF.CGDirectEdge = Scope-CF LAYOUT?-CF "-" ELabel-CF "->" LAYOUT?-CF Scope-CF
  PrimitiveConstraint-CF.CGAssoc      = Occurrence-CF LAYOUT?-CF "=" NLabel-CF "=>" LAYOUT?-CF Scope-CF
  PrimitiveConstraint-CF.CGNamedEdge  = Occurrence-CF LAYOUT?-CF "<=" NLabel-CF "=" LAYOUT?-CF Scope-CF

  PrimitiveConstraint-CF.CAssoc       = Occurrence-CF LAYOUT?-CF "?=" NLabel-CF "=>" LAYOUT?-CF Scope-CF LAYOUT?-CF Message-CF


context-free syntax

  ELabel.P = "-"
  ELabel   = PathLabel

  NLabel.I = "="
  NLabel   = PathLabel


context-free syntax

  PrimitiveConstraint.CDistinct = <!<Names> <Message>>
  PrimitiveConstraint.CSubset   = <<Names> in <Names> <Message>>
  PrimitiveConstraint.CDisjoint = <<Names> # <Names> <Message>>


syntax

  Names-CF.Declarations   = "D(" Scope-CF ")" NamespacePattern-CF
  Names-CF.References     = "R(" Scope-CF ")" NamespacePattern-CF
  Names-CF.Visibles       = "V(" Scope-CF ")" NamespacePattern-CF
  Names-CF.Reachables     = "W(" Scope-CF ")" NamespacePattern-CF

  NamespacePattern-CF.Default   =
  NamespacePattern-CF.All       = "/*"
  NamespacePattern-CF.Namespace = "/" NamespaceId-LEX

lexical syntax

  Keyword = "in" | "unique"


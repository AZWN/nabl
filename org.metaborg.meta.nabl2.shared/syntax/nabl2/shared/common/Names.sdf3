module nabl2/shared/common/Names

imports

  nabl2/shared/common/AST
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Native
  nabl2/shared/common/Terms
  nabl2/shared/common/Vars

context-free syntax

  Scope.Scope           = <scope [<VarId>]>

  Occurrence.Occurrence = <<NamespaceRef>{<Term> <TermIndex>}>

  Namespace.Namespace   = NamespaceId

  NamespaceRef         = Namespace
  NamespaceRef.Default =

  Label.Label    = LabelId

  PathLabel      = Label
  PathLabel.P    = "P"
  PathLabel.I    = "I"
 
  AnyLabel       = PathLabel
  AnyLabel.D     = "D"
 
context-free syntax

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <e>
  LabelRE            = PathLabel
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Concat     = <<LabelRE>.<LabelRE>>     {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>             {bracket}

  LabelOrder.Lt      = [[AnyLabel] < [AnyLabel]]

context-free priorities

    LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

lexical syntax

  Keyword     = "scope" | "NAMES"
  Constructor = "Scope" | "Occurrence"
  

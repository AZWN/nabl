module nabl2/shared/common/Names

imports

  nabl2/shared/common/AST
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Native
  nabl2/shared/common/Terms
  nabl2/shared/common/Vars

context-free syntax

  Scope          = Var
  Scope.Global   = "global"
  Scope.Scope    = <scope [<Int>]>
  Comparable     = Scope
 
  Occurrence            = Var
  Occurrence.Occurrence = <<Namespace>{<Term> <ASTIndex>}>
  Comparable            = Occurrence

  Namespace.Default   =
  Namespace.Namespace = NamespaceId

  Label.Label    = LabelId

  LabelRef       = Label
  LabelRef.P     = "P"
  LabelRef.I     = "I"
 
  AllLabelRef    = LabelRef
  AllLabelRef.D  = "D"
 
  MessagePosition.NAMES = <@ NAMES>
 
context-free syntax

  LabelRE.Empty      = <0>
  LabelRE.Epsilon    = <>
  LabelRE            = LabelRef
  LabelRE.Closure    = <<LabelRE>*>
  LabelRE.Concat     = <<LabelRE>.<LabelRE>>   {right}
  LabelRE.And        = <<LabelRE> & <LabelRE>>   {left}
  LabelRE.Or         = <<LabelRE> | <LabelRE>>   {left}
  LabelRE            = <(<LabelRE>)>           {bracket}

  LabelOrder.Lt      = [[AllLabelRef] < [AllLabelRef]]

context-free priorities

    LabelRE.Closure
  > LabelRE.Concat
  > LabelRE.And
  > LabelRE.Or

lexical syntax

  Keyword     = "global" | "scope" | "NAMES"
  Constructor = "Global" | "Scope" | "Occurrence"
  

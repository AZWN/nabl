module nabl2/lang/common/Injections

imports

  nabl2/shared/common/CTerms
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Resolution
  nabl2/shared/common/ScopeGraph
  nabl2/shared/common/Sets
  nabl2/shared/constraints/Base
  nabl2/lang/common/Terms

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  NaBL2-Message.DefaultMessage =

  NaBL2-MessagePosition.DefaultMessagePosition =

  NaBL2-LabelRE.Option    = <<NaBL2-LabelRE>?>
  NaBL2-LabelRE.ConcatDot = <<NaBL2-LabelRE>.<NaBL2-LabelRE>>  {right,deprecated("The dot for concatenation is unnecessary, and can be omitted.")}
  NaBL2-LabelRE.NonEmpty = <<NaBL2-LabelRE>+>

  NaBL2-ELabel.DefaultELabel = "-"
  NaBL2-NLabel.DefaultNLabel = "="

  NaBL2-Constraint.CDeclType = [[NaBL2-Occurrence] : [NaBL2-CTerm] [NaBL2-ConstraintPriority] [NaBL2-Message]]

  NaBL2-Constraint.CGen  = [[NaBL2-CTerm] genOf [NaBL2-CTerm] [NaBL2-Message]]
  NaBL2-Constraint.CInst = [[NaBL2-CTerm] instOf [NaBL2-CTerm] [NaBL2-Message]]

syntax

  NaBL2-TermIndex-CF.TermIndexFrom = "@" NaBL2-Var-CF

  NaBL2-OccurrenceIndex-CF.DefaultOccurrenceIndex =
  NaBL2-OccurrenceIndex-CF.OccurrenceIndexFrom    = LAYOUT?-CF "@" NaBL2-Var-CF

  NaBL2-Constraint-CF.CSetEq = NaBL2-CSet-CF LAYOUT?-CF "seteq" NaBL2-SetProj-CF LAYOUT?-CF NaBL2-CSet-CF LAYOUT?-CF NaBL2-Message-CF

context-free priorities

    { NaBL2-LabelRE.Closure NaBL2-LabelRE.Option NaBL2-LabelRE.NonEmpty }
  > { right: NaBL2-LabelRE.Concat NaBL2-LabelRE.ConcatDot }
  > NaBL2-LabelRE.And
  > NaBL2-LabelRE.Or

lexical syntax

  NaBL2-Keyword = "seteq" | "genOf" | "instOf" | "new" | "this"

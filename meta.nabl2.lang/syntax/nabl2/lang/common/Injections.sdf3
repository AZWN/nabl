module nabl2/lang/common/Injections

imports

  nabl2/shared/common/CTerms
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Resolution
  nabl2/shared/common/ScopeGraph
  nabl2/shared/common/Sets
  nabl2/shared/constraints/Base
  nabl2/lang/common/Terms

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  Message.DefaultMessage =

  MessagePosition.DefaultMessagePosition =

  LabelRE.Option    = <<LabelRE>?>
  LabelRE.ConcatDot = <<LabelRE>.<LabelRE>>     {right,deprecated("The dot for concatenation is unnecessary, and can be omitted.")}
  LabelRE.NonEmpty = <<LabelRE>+>

  ELabel.DefaultELabel = "-"
  NLabel.DefaultNLabel = "="

  Constraint.CDeclType = [[Occurrence] : [CTerm] [ConstraintPriority] [Message]]

  Constraint.CGen  = [[CTerm] genOf [CTerm] [Message]]
  Constraint.CInst = [[CTerm] instOf [CTerm] [Message]]

syntax

  TermIndex-CF.TermIndexFrom    = "@" Var-CF

  OccurrenceIndex-CF.DefaultOccurrenceIndex =
  OccurrenceIndex-CF.OccurrenceIndexFrom    = LAYOUT?-CF "@" Var-CF

  Constraint-CF.CSetEq = CSet-CF LAYOUT?-CF "seteq" SetProj-CF LAYOUT?-CF CSet-CF LAYOUT?-CF Message-CF

context-free priorities

    { LabelRE.Closure LabelRE.Option LabelRE.NonEmpty }
  > { right: LabelRE.Concat LabelRE.ConcatDot }
  > LabelRE.And
  > LabelRE.Or

lexical syntax

  Keyword = "seteq" | "genOf" | "instOf" | "new" | "this"

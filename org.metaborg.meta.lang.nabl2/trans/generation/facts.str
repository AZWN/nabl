module generation/facts

imports
  
  libstrc
  src-gen/signatures/rules/core/-
  generation/constraints
  
rules
  
  fact-to-strategy:
    NewScope(scope, _) -> |[ ~scope := Scope(<NewScope>) ]|

rules
    
  fact-to-terms:
    Declaration(term, scope, assoc-scope) -> [ Term |[ FDecl(~decl, ~scope) ]| , aconstr* ]
    with
      decl     := <term-to-decl> term
    ; aconstr* := <assoc-scope-to-terms(|decl)> assoc-scope
  
  fact-to-terms:
    Reference(term, scope) -> [ Term |[ FRef(~ref, ~scope) ]| ]
    with
      ref := <term-to-ref> term
  
  fact-to-terms:
    Import(term, scope) -> [ Term |[ FImportR(~ref, ~scope) ]| ]
    with
      ref := <term-to-ref> term
  
  fact-to-terms:
    ImportScope(v, scope) -> [ Term |[ FImportS(~v, ~scope) ]| ]
  
  fact-to-terms:
    NewScope(scope, Global()) -> [ Term |[ FParent(~scope, NoneScope()) ]| ]
  
  fact-to-terms:
    NewScope(scope, parent) -> [ Term |[ FParent(~scope, SomeScope(~parent)) ]| ]
    where not(Global() := parent)
    
  fact-to-terms:
  	SubType(ty1, ty2) -> [ Term |[ FSubType(~ty1', ~ty2', position) ]| ]
  	with
  	  ty1' := <type-pattern> ty1
  	; ty2' := <type-pattern> ty2
      
  assoc-scope-to-terms(|decl):
    None() -> []
    
  assoc-scope-to-terms(|decl):
    Assoc(scope) -> [ Term |[ FAssoc(~decl, ~scope) ]| ]  
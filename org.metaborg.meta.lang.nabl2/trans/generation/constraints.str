module generation/constraints

imports
  
  libstrc
  src-gen/signatures/rules/core/-
  src-gen/signatures/terms/-
  generation/facts
  
rules
  
  constraint-to-strategies:
    HasType(_, type) -> str*
    with var* := <collect-all(?Var(_) + ?ListVar(_))> type
       ; str* := <map(!|[ ~<id> := Var(<NewVar>) ]|)> var*
         
  constraint-to-strategies:
    c@LubType(type1, type2, type3) -> str*
    with var* := <collect-all(?Var(_) + ?ListVar(_))> c
       ; str* := <map(!|[ ~<id> := Var(<NewVar>) ]|)> var*
  
  constraint-to-strategies:
    c@SubType(type1, type2) -> []
    
  var-to-strategy:
    VarRef(v) -> |[ ~v := Var(<NewVar>) ]|
      
  constraint-to-strategies:
    Declares(v, dv) -> [ |[ ~dv := ~decl ]| ]
    where
    	decl := <binder-to-decl> v

  constraint-to-strategies:
    Resolves(_, _, v) -> [ |[ ~v := Var(<NewVar>) ]| ]
    
  constraint-to-strategies:
    HasScope(_, v) -> [ |[ ~v := Var(<NewVar>) ]| ]
  
  constraint-to-strategies:
    Match(_, pattern) -> str*
    with var* := <collect-all(?Var(_) + ?ListVar(_))> pattern
       ; str* := <map(!|[ ~<id> := Var(<NewVar>) ]|)> var*
  
  constraint-to-strategies:
    Success(c, _) -> str*
    with str* := <constraint-to-strategies> c
  
  constraint-to-strategies:
    Failure(c, _) -> str*
    with str* := <constraint-to-strategies> c
    
  constraint-to-terms(|pos):
    Success(c, msg) -> t*
    with pos' := <message-to-term> msg
    with t*   := <constraint-to-terms(|pos')> c
    
  constraint-to-terms(|pos):
    Failure(c, msg) -> t*
    with pos' := <message-to-term> msg
    with t*   := <constraint-to-terms(|pos')> c
         
  message-to-term:
    Message(_, _, t) -> Term |[ <get-position> t ]|
  
  // dynamic check could be avoided with proper static analysis
  constraint-to-terms(|msg):
    HasType(v, t) -> [ Term |[ <get-type-constraint(|tvars, ~tp, ~msg)> ~v ]| ]
    with tp := <try(type-pattern)> t
  
  constraint-to-terms(|msg):
    LubType(t1, t2, t3) -> [ Term |[ CLubType(~tp1, ~tp2, ~tp3, ~msg) ]| ]
    with tp1 := <try(type-pattern)> t1
       ; tp2 := <try(type-pattern)> t2
       ; tp3 := <try(type-pattern)> t3
   
  constraint-to-terms(|msg):
    SubType(t1, t2) -> [ Term |[ CSubType(~t1, ~t2, ~msg) ]| ]
       
  constraint-to-terms(|msg):
    Resolves(v, ns, dv) -> [ Term |[ CResolves(~ref, ~dv, ~msg) ]| ]
    with
      ref := <binder-to-ref> Bind(ns, v)
  
  constraint-to-terms(|msg):
    Declares(_, _) -> []
    
  constraint-to-terms(|msg):
    HasScope(v, sv) -> [ Term |[ CAssoc(~v, ~sv, ~msg) ]| ]
   
  constraint-to-terms(|msg):
  	Match(v, pattern) -> [ Term |[ CEqType(~v, ~tp, ~msg) ]| ]
  	with tp := <try(type-pattern)> pattern

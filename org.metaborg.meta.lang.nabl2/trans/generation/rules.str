module generation/rules

imports
  
  libstrc
  signatures/rules/core/-
  signatures/terms/-
  analysis/-
  util
  generation/visits
  generation/constraints
  generation/facts
  generation/messages
  
rules // binding rules

  rules-to-str:
    Rules(r*) -> [Rules(<map(rule-to-strategy)> r*)]
 
  rule-to-strategy:
    ScopeRule(pattern, InScopes(scope*), fact*, visit*, constr*) ->
    |[ c(|facts__, constraints__, scopes__, position__):
         ~pattern -> ~ret
         with ?term__
         with [~scope* | _] := scopes__ <+ ( debug(!$[wrong scope count [<length> scopes__] for ]) ; fail )
         with ~fvar
         with ~cvar
         with <iset-addlist(|[~fcoll*])> facts__
            ;  <iset-addlist(|[~ccoll*])> constraints__
         with ~visit
    ]|
    with pvar*  := <collect-vars> pattern
       ; fvar   := <filterconcat(fact-to-strategies); to-seq> fact*
       ; fvar*  := <filterconcat(collect-fact-vars)> fact*
       ; cvar*  := <mapconcat(collect-constraint-vars); make-set> constr*
       ; cvar   := <diff; vars-to-init> (cvar*, <conc> (pvar*,fvar*))
       ; fcoll* := <mapconcat(fact-to-terms)> fact* 
       ; ccoll* := <mapconcat(constraint-to-terms(|<default-message>))> constr* 
       ; visit  := <map(visit-to-appl); to-seq> visit*
       ; if []:= visit* then
           c   := "nabl2-constraint"
         ; ret := |[ <fail> ]|
         else
           c   := "nabl2-scope"
         ; ret := |[ <id> ]|
         end

   rule-to-strategy:
    TypeRule(pattern, type, constr*) ->
    |[ nabl2-constraint(|facts__, constraints__, scopes__, position__):
         ~pattern -> <fail>
         with ?term__
         with ~var
         with <iset-addlist(|[CTypeOf(position__, ~tp, ~msg), ~ccoll*])> constraints__
    ]|
    with 
      tvar*  := <collect-vars> type
    ; cvar*  := <mapconcat(collect-constraint-vars)> constr*
    ; var    := <make-set; vars-to-init> [tvar*,cvar*]
    ; ccoll* := <mapconcat(constraint-to-terms(|<default-message>))> constr*
    ; tp     := <type-pattern> type
    ; msg    := <default-message>
        
    vars-to-init = map(!|[ ~<id> := <new-var> ]|); to-seq
       

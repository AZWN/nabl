module analysis/normalize

imports
  libstrc
  src-gen/signatures/-
  src-gen/signatures/constraints/-
  src-gen/pp/-
  src-gen/pp/constraints/-
  util

strategies
   
  normalize-all = normalize-module
  
rules
  
  normalize-module:
    Module(name, section*) -> Module(name, [Imports(i*), Rules(r*)])
    with i*  := <filterconcat(?Imports(<id>))> section*
       ; r*  := <filterconcat(?Rules(<map(normalize-rule)>))> section*
 
  normalize-rule:
    Rule(pattern, With(s*), w*, c*, v*) ->
    Rule(pattern, With(s'*), w*, c*, v*)
    with s'* := <qsort((var-name,var-name);string-lt)> s*

  normalize-rule:
    Rule(pattern, With(s*), part*) -> 
    Rule(pattern, With(s'*), w*, c*, v*)
    with w*  := <filter(is-Constraint)> part*
       ; c* := <filter(is-Fact; try(normalize-fact))> part*
       ; v*  := <filter(is-VisitClause; normalize-visit)> part* 
       ; s'* := <qsort((var-name,var-name);string-lt)> s*
  
  normalize-fact:
    Scope(s, None()) -> Scope(s, NoParent())
    
  normalize-visit:
    Visit(v, Args(a*)) -> Visit(v, Args(a'*))
    with a'* := <qsort((?Arg(<id>,_),?Arg(<id>,_));string-lt)> a*
module NameBindingLanguage2

imports 

	common/Layout
	common/Identifiers
  terms/Terms
  terms/Vars
  formulas/Constraints
  core/Modules
    
context-free start-symbols

	Start

context-free syntax

	Start = Module

  ModuleSection.Rules = <
    rules

    <Rule*>
  >

  Rule.Rule = <
    <Pattern> <With>:
    <Part*>
  >

  With.None =
  With.With = <with <{Var ","}+>>

  Part.Where = <where <{Constraint "and"}+>>

  Part.Create = <create <{Fact "and"}+>>
  
  Fact.Scope       = <scope <Var> <Parent>>
  Fact.Declaration = <declaration <Term> <InScope> <AssocScope>>
  Fact.Reference   = <reference <Term> <InScope>>
  Fact.Import      = <import <Term> <IntoScope>>
  Fact.ImportScope = <import from <VarRef> <IntoScope>>

  InScope.CurrentScope = <>
  InScope.InScope = <in <VarRef>>

  IntoScope.CurrentScope = <>
  IntoScope.IntoScope = <into <VarRef>>

  AssocScope.None = 
  AssocScope.Assoc = <with associated scope <VarRef>>
  
  Parent.None   = 
  Parent.Parent = <with parent <VarRef>>
  
  Part.Visit = <visit <{VisitClause "and"}+>>

  VisitClause.VisitClause = <<Term> <Args>>
  
  Args.None =
  Args.Args = <with <{Arg ","}+>>
  
  Arg.Arg = <<Var> = <VarRef>>
  
module scopegraphs/extension

language StatixLang

// direct extensions

test extend parameter scope in same rule succeeds [[
  module test
  signature
    relations foo : string
  rules
    c : scope
    c(s) :- !foo[""] in s.
]] analysis succeeds

test extend new scope in same rule succeeds [[
  resolve {s} new s, !foo[""] in s
  signature
    relations foo : string
  rules
    c : 
    c() :- {s} new s, !foo[""] in s.
]] analysis succeeds

test extend local scope in same rule fails [[
  resolve {s} !foo[""] in [[s]]
  signature
    relations foo : string
  rules
    c : 
    c() :- {s} !foo[""] in [[s]].
]] 2 errors //at #1, #2

// extension in one step

test extend parameter scope in via other constraint succeeds [[
  module test
  signature
    relations foo : string
  rules
    c0 : scope
    c0(s) :- c1(s).

    c1 : scope
    c1(s) :- !foo[""] in s.
]] analysis succeeds

test extend new scope in via other constraint succeeds [[
  resolve {s} new s, c1(s)
  signature
    relations foo : string
  rules
    c0 : 
    c0() :- {s} new s, c1(s).

    c1 : scope
    c1(s) :- !foo[""] in s.
]] analysis succeeds

test extend local scope in via other constraint fails [[
  resolve {s} c1([[s]])
  signature
    relations foo : string
  rules
    c0 : 
    c0() :- {s} c1([[s]]).

    c1 : scope
    c1(s) :- !foo[""] in s.
]] 2 errors //at #1, #2

// extension in two steps

test extend parameter scope in via two other constraints succeeds [[
  module test
  signature
    relations foo : string
  rules
    c0 : scope
    c0(s) :- c1(s).

    c1 : scope
    c1(s) :- c2(s).

    c2 : scope
    c2(s) :- !foo[""] in s.
]] analysis succeeds

test extend new scope in via two other constraints succeeds [[
  resolve {s} new s, c1(s)
  signature
    relations foo : string
  rules
    c0 : 
    c0() :- {s} new s, c1(s).

    c1 : scope
    c1(s) :- c2(s).

    c2 : scope
    c2(s) :- !foo[""] in s.
]] analysis succeeds

test extend local scope in via two other constraints fails [[
  resolve {s} c1([[s]])
  signature
    relations foo : string
  rules
    c0 : 
    c0() :- {s} c1([[s]]).

    c1 : scope
    c1(s) :- c2(s).

    c2 : scope
    c2(s) :- !foo[""] in s.
]] 2 errors //at #1, #2

// extension via sugar construct

test extend local scope via declaration sugar fails [[
  resolve {s} [[s]] -> Var{"x"@-}
  signature
    namespaces Var : string
  rules
    c : 
    c() :- {s} [[s]] -> Var{"x"@-}.
]] 2 errors //at #1, #2

test extend local scope via functional constraint fails [[
  resolve {s} c1([[s]]) == _
  signature
    namespaces Var : string
  rules
    c0 : 
    c0() :- {s} c1([[s]]) == _.

    c1 : scope -> ()
    c1(s) = () :- s -> Var{"x"@-}.
]] 2 errors //at #1, #2

// extension via mapped predicate constraint

test new extension of non-lifted parameter of lifted predicate succeeds [[
  resolve {s} new s, cs(s, [])
  signature
    relations
      r : int
  rules
    cs maps c(*, list(*))
    c : scope * int
    c(s, i) :- !r[i] in s.
    
    d :
    d() :- {s} new s, cs(s, []).
]] analysis succeeds

test local extension of non-lifted parameter of lifted predicate fails [[
  resolve {s1 s2} new s1, s1 == s2, cs([[s2]], [])
  signature
    relations
      r : int
  rules
    cs maps c(*, list(*))
    c : scope * int
    c(s, i) :- !r[i] in s.
    
    d :
    d() :- {s1 s2} new s1, s1 == s2, cs([[s2]], []).
]] 2 errors //at #1, #2

test extension of lifted parameter of lifted predicate fails [[
  resolve {s1 s2} new s1, s1 == s2, cs([s2], [])
  signature
    relations
      r : int
  rules
    cs maps c([[list(*)]], list(*))
    c : scope * int
    c(s, i) :- !r[i] in s.
    
    d :
    d() :- {s1 s2} new s1, s1 == s2, cs([s2], []).
]] 1 errors //at #1


// extension via mapped functional constraint

test new extension of non-lifted parameter of lifted functional succeeds [[
  resolve {s} new s, fs(s, []) == _
  signature
    relations
      r : int
  rules
    fs maps f(*, list(*)) = list(*)
    f : scope * int -> ()
    f(s, i) = () :- !r[i] in s.
    
    d :
    d() :- {s} new s, fs(s, []) == _.
]] analysis succeeds

test local extension of non-lifted parameter of lifted functional fails [[
  resolve {s1 s2} new s1, s1 == s2, fs([[s2]], []) == _
  signature
    relations
      r : int
  rules
    fs maps f(*, list(*)) = list(*)
    f : scope * int -> ()
    f(s, i) = () :- !r[i] in s.

    d :
    d() :- {s1 s2} new s1, s1 == s2, fs([[s2]], []) == _.
]] 2 errors //at #1, #2

test extension of lifted parameter of lifted functional fails [[
  resolve {s1 s2} new s1, s1 == s2, fs([s2], []) == _
  signature
    relations
      r : int
  rules
    fs maps f([[list(*)]], list(*)) = list(*)
    f : scope * int -> ()
    f(s, i) = () :- !r[i] in s.
    
    d :
    d() :- {s1 s2} new s1, s1 == s2, fs([s2], []) == _.
]] 1 errors //at #1

test extension of output of functional constraint fails [[
  resolve [[f()]] -> Var{"x"@-}
  signature
    namespaces Var : string
  rules
    c : 
    c() :- {s} [[f()]] -> Var{"x"@-}.
    f : -> scope
]] 2 errors //at #1, #2

test extension of target scope in query fails [[
  resolve {s} query () filter e and { s' :- [[s']] -P-> s } in s |-> _
  signature
    name-resolution
      labels P
]] 1 errors //at #1

test extension of scope data in query fails [[
  resolve {s} query r filter e and { s' :- [[s']] -P-> s } in s |-> _
  signature
    relations
      r : scope
    name-resolution
      labels P
]] 1 errors //at #1

module nabl2/lang/rules/CGen

imports
 
  nabl2/shared/common/CTerms
  nabl2/shared/common/Identifiers
  nabl2/shared/constraints/Base

  nabl2/lang/common/Identifiers
  nabl2/lang/common/Injections
  nabl2/lang/common/Terms
  nabl2/lang/rules/Rules

template options

  keyword -/- [a-zA-Z0-9\']

context-free syntax

  CGenName.NoName =
  CGenName.Name   = RuleId

  CGenDef.Name    = RuleId

  CGenRef.NoName  = "default"
  CGenRef.Name    = RuleId

  CGenDefs = {ast("[]")}
  CGenDefs = <(<{CGenDef ","}*>)>

  CGenRefs = {ast("[]")}
  CGenRefs = <(<{CGenRef ","}*>)>

context-free syntax

  Rule.CGenInitRuleNoConstraint = <
    init <CGenParamsTerm> <CGenTypeTerm>.
  >

  Rule.CGenInitRule = <
    init <CGenParamsTerm> <CGenTypeTerm> :=
        <{RuleClause ","}+>.
  >

  Rule.CGenMatchRuleNoConstraint = <
    <CGenName><CGenDefs> [[ <Pattern> <CGenParamsPattern> <CGenTypePattern> ]] .
  >

  Rule.CGenMatchRule = <
    <CGenName><CGenDefs> [[ <Pattern> <CGenParamsPattern> <CGenTypePattern> ]] :=
        <{RuleClause ","}+>.
  >

  RuleClause             = Constraint
  RuleClause             = NewScopes
  RuleClause.CGenRecurse = <<CGenName><CGenRefs> [[ <Var> <CGenParamsTerm> <CGenTypeTerm> ]]>


context-free syntax

  CGenParamsTerm.Tuple    = <^ (<{CTerm ","}*>)>

  CGenParamsPattern.Tuple = <^ (<{VarOrWld ","}*>)>

  CGenTypeTerm.NoType     =
  CGenTypeTerm.Type       = <: <CTerm>>

  CGenTypePattern.NoType  =
  CGenTypePattern.Type    = <: <CTerm>>


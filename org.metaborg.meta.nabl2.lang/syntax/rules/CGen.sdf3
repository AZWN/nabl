module rules/CGen

imports
 
  nabl2/shared/common/Identifiers
  nabl2/shared/common/Names
  nabl2/shared/common/Terms
  nabl2/shared/common/Types
  nabl2/shared/common/Vars

  common/Identifiers
  common/Types
  rules/Rules

template options

  keyword -/- [a-zA-Z0-9\']

context-free syntax

  Rule.CGenInitRule = <
    init <CGenParamsTerm> <CGenTypeTerm> :=
        <Constraint>.
  >

  Rule.CGenMatchRule = <
    <RuleName> [[ <Pattern> <CGenParamsPattern> <CGenTypePattern> ]] :=
        <Constraint>.
  >

context-free syntax

  CGenParamsTerm.List    = <^ (<{TypeTerm ","}*>)>

  CGenParamsPattern.List = <^ (<{VarOrWld ","}*>)>

  CGenTypeTerm.NoType    =
  CGenTypeTerm.Type      = <: <TypeTerm>>

  CGenTypePattern.NoType =
  CGenTypePattern.Type   = <: <TypePattern>>

context-free syntax

  Constraint.CGenRecurse = <<RuleName> [[ <Var> <CGenParamsTerm> <CGenTypeTerm> ]]>

  Constraint.New         = <new <{Var " "}+>>

lexical syntax

  Keyword        = "new"

 
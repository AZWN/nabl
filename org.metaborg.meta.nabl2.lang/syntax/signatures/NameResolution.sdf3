module signatures/NameResolution

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/ScopeGraph
  nabl2/shared/common/Sorts
  nabl2/shared/common/Resolution

template options

  keyword -/- [a-zA-Z0-9\']

context-free syntax

  Signature.NameResolution = <
    name resolution
      <{NameResolutionParam "\n"}*>
  > {longest-match}

  NameResolutionParam.Labels     = <labels <PathLabel*>>
  NameResolutionParam.Order      = <order <{LabelOrder ","}*>>
  NameResolutionParam.WF         = <well-formedness <LabelRE>>

context-free syntax

  Signature.Namespaces = <
    namespaces
      <{NamespaceDef "\n"}*>
  > {longest-match}

  NamespaceDef.NamespaceDef = <<Namespace> <NamespaceType> <NamespaceProperties>>

  NamespaceType.NoType  =
  NamespaceType.Type    = <: <SortRef>>

  NamespaceProperties = <{<{NamespaceProperty ","}*>}>
  NamespaceProperties = {ast("[]")}
  
  NamespaceProperty.PropertyDef = <<PropertyId> : <SortRef>>


module signatures/Relations

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/Relations
  nabl2/shared/common/Sorts

template options

  keyword -/- [a-zA-Z0-9\_\']

context-free syntax

  Signature.Relations = <
    relations
      <{RelationDef "\n"}*>
  > {longest-match}

context-free syntax

  RelationDef.RelationDef = <<{RelationOption ","}*> <Relation> <RelationType> <VariancePatterns>>

  RelationOption = Reflexivity
  RelationOption = Symmetry
  RelationOption = Transitivity

  RelationType.TypeSort =
  RelationType          = <: <SortRef>>

  VariancePatterns      = <{<{VariancePattern ","}*>}>
  VariancePatterns      = {ast("[]")}
 
  VariancePattern.List  = <[<Variance>]>
  VariancePattern.Tuple = <(<{Variance ","}*>)>
 
syntax 

  VariancePattern-CF.Op   = OpId-LEX "(" LAYOUT?-CF Variances-CF LAYOUT?-CF ")"

  Variance-CF.Invar     = "="
  Variance-CF.Covar     = "+" RelationRef-CF
  Variance-CF.Contravar = "-" RelationRef-CF

context-free syntax

  Variances = {Variance ","}*

module Layout

// This file is in the root, and should only be imported by the main SDF3 module.
// If this is imported in another project, bad things will happen.

lexical syntax

  LAYOUT = [\t\ \r\n]
  LAYOUT = LineComment
  LAYOUT = BlockComment

  EOF =  

  LineComment     = "//" ~[\r\n]* ([\r\n] | EOF)

  BlockComment    = "/*" CommentText (BlockComment CommentText)* ("*/" | EOF)
  CommentText     = CommentChar*
  CommentChar     = ~[\*\/] | CommentAsterisk | CommentSlash
  CommentSlash    = "/"
  CommentAsterisk = "*"

lexical restrictions

  EOF             -/- ~[]
  CommentText     -/- ~[\*\/]
  CommentAsterisk -/- [\/]
  CommentSlash    -/- [\*]

context-free restrictions

  LAYOUT? -/- [\ \t\r\n]   // whitespace
  LAYOUT? -/- [\/].[\/]    // line comment start
  LAYOUT? -/- [\/].[\*]    // block comment start

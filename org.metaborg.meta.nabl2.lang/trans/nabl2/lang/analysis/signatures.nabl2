module nabl2/lang/analysis/signatures

imports

  signatures/nabl2/lang/common/-
  signatures/nabl2/lang/signatures/-

  nabl2/lang/analysis/common

rules

  [[ Signature(signatures) ^ (s_mod, s_root) ]] :=
    Map1(Sig)[[ signatures ^ (s_mod) ]].

rules

  Sig[[ CGen(rulesigs) ^ (s) ]] :=
    Map1(Sig)[[ rulesigs ^ (s) ]].

  Sig[[ CGenSig(def, sort, params, type) ^ (s) ]].

rules 

  Sig[[ Constructors(_) ^ (_) ]]. // TODO

rules

  Sig[[ NameResolution(nameresparams) ^ (s) ]] :=
    new s_init, s_init -L-> s, InitMarker{()} -> s_init,
    InitMarker{()} |-> _ | error "name resolution parameters must be specified in module with init rule",
    Map1(Sig)[[ nameresparams ^ (s) ]].

  Sig[[ Labels(labels) ^ (s) ]] :=
    Map1(LabelDef)[[ labels ^ (s) ]].

  Sig[[ Order(orders) ^ (s) ]] :=
    Map1(LabelOrder)[[ orders ^ (s) ]].

  Sig[[ WF(re) ^ (s) ]] :=
    WellFormednessMarker{()} <- s,
    LabelRE[[ re ^ (s) ]].

rules

  Sig[[ Namespaces(namespacedefs) ^ (s) ]] :=
    new s_init, s_init -L-> s, InitMarker{()} -> s_init,
    InitMarker{()} |-> _ | error "namespaces must be specified in module with init rule",
    Map1(Sig)[[ namespacedefs ^ (s) ]].

  Sig[[ NamespaceDef(ns,type,properties) ^ (s) ]] :=
    NamespaceDef[[ ns ^ (s) ]]
    // TODO: type
    // TODO: properties
    .

rules 

  Sig[[ Relations(relsigs) ^ (s) ]] :=
    new s_init, s_init -L-> s, InitMarker{()} -> s_init,
    InitMarker{()} |-> _ | error "relations must be specified in module with init rule",
    Map1(Sig)[[ relsigs ^ (s) ]].

  Sig[[ RelationDef(opts,rel,type,variancepatterns) ^ (s) ]] :=
    RelationDef[[ rel ^ (s) ]],
    // TODO: type
    Map1(VariancePattern)[[ variancepatterns ^ (s) ]].

rules

  Sig[[ Functions(funsigs) ^ (s) ]] :=
    new s_init, s_init -L-> s, InitMarker{()} -> s_init,
    InitMarker{()} |-> _ | error "functions must be specified in module with init rule",
    Map1(Sig)[[ funsigs ^ (s) ]].

  Sig[[ FunctionDef(fun,type,cases) ^ (s) ]] :=
    FunctionDef[[ fun ^ (s) ]],
    // TODO: type
    Map0(FunctionCase)[[ cases ^ () ]].

  FunctionCase[[ (arg, result) ^ () ]] :=
    new s_case,
    Term(ScopeVarDecl)[[ arg ^ (s_case) ]],
    Term(VarRef)[[ result ^ (s_case) ]].
 
module nabl2/lang/analysis/normalize

imports

  signatures/nabl2/shared/common/-
  signatures/nabl2/shared/constraints/-

  signatures/nabl2/lang/common/-
  signatures/nabl2/lang/modules/-
  signatures/nabl2/lang/rules/-
  signatures/nabl2/lang/signatures/-

  nabl2/lang/generation/-
  nabl2/lang/util/-

rules

  normalize =
    normalize-axioms;
    normalize-wildcards;
    normalize-types;
    normalize-regex;
    normalize-constructors;
    normalize-arg-matches;
    normalize-new-scopes;
    normalize-rule-vars;
    normalize-constraints;
    normalize-messages;
    normalize-indices

rules

  normalize-axioms = try(sometd(normalize-axiom))

  // capture the matched term
  normalize-axiom:
    CGenInitRuleNoConstraint(params,type)
      -> CGenInitRule(params,type, [])

  normalize-axiom:
    CGenMatchRuleNoConstraint(rdef,pattern,params,type)
      -> CGenMatchRule(rdef,pattern,params,type, [])

rules

  normalize-wildcards =
    try(sometd(
      Rules(map(wlds-to-vars))
    + Functions(map(FunctionDef(id,id,map(wlds-to-vars))))
    ))

  wlds-to-vars = {| NextVar:
    with(var-counter := <new-counter>);
    rules(NextVar: _ -> Var($[nabl2_wld[<next-counter;int-to-string> var-counter]]));
    topdown(try(\ Wld() -> <NextVar> \))
  |}

rules

  normalize-types = try(sometd(normalize-types-signature))

  normalize-types-signature: Signature(sig*) -> Signature(sig'*)
    with sig'* := <map(normalize-types-section <+ MkSingleton);concat> sig*

  normalize-types-section:
    Types(typesig*) -> [ctors,rels]
    with opdecl* := <map(normalize-type-sig)> typesig*;
         ctors := Constructors(opdecl*);
         vardecl* := <filter(where(?TypeSig(_,<not(all(?(Invar(),_)))>)))> typesig*;
         relop* := <map(normalize-rel-type)> vardecl*;
         relopt* := [Reflexive(), AntiSymmetric(), Transitive()];
         listrelop := ListVariant(Covar(DefaultRelation()));
         rels := Relations([RelationDef(relopt*,DefaultRelation(),TypeSort(),[listrelop|relop*])])

  normalize-type-sig:
    TypeSig(op,a*) -> OpDecl(op, <map(Snd)> a*, TypeSort())

  normalize-rel-type:
    TypeSig(op,a*) -> OpVariant(op, <map(Fst;normalize-type-variance)> a*)

  normalize-type-variance: Invar() -> Invar()
  normalize-type-variance: Covar() -> Covar(DefaultRelation())
  normalize-type-variance: Contravar() -> Contravar(DefaultRelation())

rules

  normalize-regex = try(sometd(normalize-option))

  normalize-option:
    Option(re) -> Or(Epsilon(),re)

rules

  normalize-constructors = try(sometd(normalize-opdecl))

  normalize-opdecl:
    NullaryOpDecl(op,sort) -> OpDecl(op, [], sort)

rules

  normalize-arg-matches = try(sometd(normalize-type-match))

  // replace type terms with a variable and an equality constraint
  normalize-type-match:
    CGenMatchRule(rdef,pattern,param,Type(type),clause*)
      -> CGenMatchRule(rdef,pattern,param,Type(<type-var>),
             [CEqual(<type-var>,type,DefaultMessage()),clause*])
    where <not(is-var)> type

rules

  normalize-new-scopes = try(sometd(normalize-new-scope))

  normalize-new-scope = CGenInitRule(id,id,normalize-new-scope-clauses)

  normalize-new-scope = CGenMatchRule(id,id,id,id,normalize-new-scope-clauses)

  normalize-new-scope = FunDef(id,id,normalize-new-scope-clauses)

  normalize-new-scope-clauses: clause* -> [scope-clause*,other-clause*]
    where (new-scopes*@[_|_],other-clause*) := <partition(\ NewScopes(s*) -> s* \)> clause*
    with scope-clause* := <concat;map(\ s -> CEqual(s,NewScope(<var-name> s),DefaultMessage()) \)> new-scopes*

rules

  normalize-rule-vars = 
    try(sometd(normalize-free-vars))

  normalize-free-vars:
    CGenInitRule(params,type,clause*)
      -> CGenInitRule(params,type,[NewVars(free-var*),clause*])
    with new-var* := <clauses-new-vars> clause*;
         used-var* := <all-vars> (params,type,clause*);
         free-var* := <diff> (used-var*, [<term-var>,new-var*])

  normalize-free-vars:
    CGenMatchRule(rdef,pattern,params,type,clause*)
      -> CGenMatchRule(rdef,pattern,params,type,[NewVars(free-var*),clause*])
    with new-var* := <clauses-new-vars> clause*;
         bound-var* := <all-vars> (pattern,params,type,new-var*);
         used-var* := <all-vars> clause*;
         free-var* := <diff> (used-var*, bound-var*)

  normalize-free-vars:
    FunDef(name,arg*,clause*)
      -> FunDef(name,arg*,[NewVars(free-var*),clause*])
    with new-var* := <clauses-new-vars> clause*;
         used-var* := <all-vars> clause*;
         free-var* := <diff> (used-var*, [<term-var>,arg*,new-var*])

  clauses-new-vars =
    filter(clause-new-vars); concat

  clause-new-vars: NewScopes(v*) -> v*

rules

  normalize-constraints =
    topdown(try(normalize-constraint));
    topdown(try(CGenInitRule(id,id,flatten-list) + CGenMatchRule(id,id,id,id,flatten-list)))

  normalize-constraint:
    CDeclType(o,ty,prio,msginfo) ->
      CDeclProperty(o,Type(),ty,prio,msginfo)

  normalize-constraint:
    CSetEq(s1,proj,s2,msginfo) ->
      [CSubsetEq(s1,proj,s2,msginfo),CSubsetEq(s2,proj,s1,msginfo)]

rules

  normalize-messages = topdown(try(normalize-message))

  // create default error message
  normalize-message:
    DefaultMessage() ->
      Message(Error(),Default(),DefaultTermIndex())

  normalize-message:
    Message(kind,msg,NAME()) ->
      Message(kind,msg,NAMEWithOrigin())

rules

  normalize-indices = topdown(try(normalize-occurrence-index <+ normalize-default-index))

  // get index from occurrence, if name is from AST
  normalize-occurrence-index:
    Occurrence(ns,name,DefaultOccurrenceIndex()) ->
      Occurrence(ns,name,OccurrenceIndexFrom(name))
    where <is-var> name

  // get index from matched term
  normalize-default-index:
    DefaultOccurrenceIndex() -> OccurrenceIndexFrom(<term-var>)

  // get index from matched term
  normalize-default-index:
    DefaultTermIndex() -> TermIndexFrom(<term-var>)

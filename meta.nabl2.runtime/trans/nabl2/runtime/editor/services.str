module nabl2/runtime/editor/services

imports

  nabl2/runtime/analysis/-
  nabl2/runtime/prelude/-
  nabl2/runtime/pp
  nabl2/api
  signatures/nabl2/runtime/common/-

rules

  nabl2--editor-resolve: (node, _, _, _, _) -> decls
    where <nabl2-get-ast-index> node // HACK: necessary to prevent errors
                                     //       as we sometimes get nodes
                                     //       without the AST index here
    with a := <nabl2-get-ast-analysis> node
    where refs@[_|_] := <nabl2-get-ast-refs(|a)> node // fail if this node has no refs,
                                                      // so the resolver can try other
                                                      // nodes under the current point
    with decls := <mapconcat(nabl2-get-resolved-names(|a);
                             map(Fst;nabl2-get-occurrence-index))> refs

  nabl2--editor-hover: (node, _, _, _, _) -> label
    where <nabl2-get-ast-index> node // HACK: necessary to prevent errors
                                     //       as we sometimes get nodes
                                     //       without the AST index here
    with a := <nabl2-get-ast-analysis> node;
         ast-labels  := <nabl2--ast-hover(|a) <+ ![]> node;
         decl-labels := <nabl2--decl-hover(|a) <+ ![]> node;
         labels := <conc> (ast-labels, decl-labels)
    where not([] := labels)
    with label := <nabl2--lines> labels

  nabl2--ast-hover(|a): node -> labels
    where <nabl2-get-ast-index> node // HACK: necessary to prevent errors
                                     //       as we sometimes get nodes
                                     //       without the AST index here
    with if type := <nabl2-get-ast-type(|a)> node then
           labels := [$[Type: [<nabl2--hover-type-string(|a)> type]]]
         else
           labels := []
         end

  nabl2--decl-hover(|a): node -> labels
    where <nabl2-get-ast-index> node // HACK: necessary to prevent errors
                                     //       as we sometimes get nodes
                                     //       without the AST index here
    with decls := <nabl2-get-ast-decls(|a)> node;
         declTypes := <filter(!(<id>, <nabl2-get-type(|a)>))> decls
    with labels := <map(\ (decl, type) -> $[[<nabl2--hover-decl-string(|a)> decl] : [<nabl2--hover-type-string(|a)> type]] \)> declTypes

  nabl2--hover-decl-string(|a) = nabl2--strict(
    topdown(try(nabl2--strip-occurrence-index));
    nabl2--focus-term(|a);
    pp-NaBL2-CTerm;
    nabl2--xmlencode
  )

  nabl2--hover-type-string(|a) = nabl2--strict(
    topdown(try(nabl2--strip-occurrence-index));
    nabl2--focus-term(|a);
    pp-NaBL2-objlangterm;
    nabl2--xmlencode
  )

  nabl2--lines =
    separate-by(!"<br>")
  ; <conc> (<id>, ["<br>"])
  ; concat-strings
